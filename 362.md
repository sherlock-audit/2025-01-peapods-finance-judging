Bouncy Rainbow Poodle

High

# Attacker can drain PodUnwrapLocker contract

### Summary

The missing check in `PodUnwrapLocker.sol` for a redundant token address will lead to drain the contract fund.
https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/PodUnwrapLocker.sol#L60

### Root Cause

* The `debondAndLock` function in `PodUnwrapLocker` allows users to arbitrarily specify the `_pod` parameter. An attacker could exploit this using malicious contract by submitting a manipulated token list via `_podContract.getAllAssets()` containing repeated addresses, enabling them to drain previously locked funds from other users with a single sufficient deposit.
```solidity
    function debondAndLock(address _pod, uint256 _amount) external nonReentrant {
        require(_amount > 0, "D1");
        require(_pod != address(0), "D2");

        IERC20(_pod).safeTransferFrom(_msgSender(), address(this), _amount);

        IDecentralizedIndex _podContract = IDecentralizedIndex(_pod);
        IDecentralizedIndex.IndexAssetInfo[] memory _podTokens = _podContract.getAllAssets();
        address[] memory _tokens = new address[](_podTokens.length);
        uint256[] memory _balancesBefore = new uint256[](_tokens.length);

        // Get token addresses and balances before debonding
        for (uint256 i = 0; i < _tokens.length; i++) {
            _tokens[i] = _podTokens[i].token;
            _balancesBefore[i] = IERC20(_tokens[i]).balanceOf(address(this)); <@ audit
        }
        _podContract.debond(_amount, new address[](0), new uint8[](0)); <@

        uint256[] memory _receivedAmounts = new uint256[](_tokens.length);
        for (uint256 i = 0; i < _tokens.length; i++) {
            _receivedAmounts[i] = IERC20(_tokens[i]).balanceOf(address(this)) - _balancesBefore[i]; <@ audit
        }

        IDecentralizedIndex.Config memory _podConfig = _podContract.config();
        uint256 _lockId = currentLockId++;
        locks[_lockId] = LockInfo({
            user: _msgSender(),
            pod: _pod,
            tokens: _tokens,
            amounts: _receivedAmounts,
            unlockTime: block.timestamp + _podConfig.debondCooldown,
            withdrawn: false
        });

        emit LockCreated(
            _lockId, _msgSender(), _pod, _tokens, _receivedAmounts, block.timestamp + _podConfig.debondCooldown
        );
    }
```
* As we can see when the period end in `withdraw`, the function start to iterate over the list of token previously provided and send `_lock.amounts[i]` to the user.
```solidity
    function _withdraw(address _user, uint256 _lockId) internal {
        LockInfo storage _lock = locks[_lockId];
        require(_lock.user == _user, "W1");
        require(!_lock.withdrawn, "W2");
        require(block.timestamp >= _lock.unlockTime, "W3");

        _lock.withdrawn = true;

        for (uint256 i = 0; i < _lock.tokens.length; i++) {
            if (_lock.amounts[i] > 0) {
                IERC20(_lock.tokens[i]).safeTransfer(_user, _lock.amounts[i]);
            }
        }

        emit TokensWithdrawn(_lockId, _user, _lock.tokens, _lock.amounts);
    }
``` 

### Internal Pre-conditions

There are no Pre-conditions to comply with, the only requirement is that the contract must have funds.

### External Pre-conditions

The attacker should deposit an amount of the same tokens previously locked by other users.

### Attack Path

1. Attacker create a malicious `pod` contract follow the same standard.
2. Attacker lock a specific amount with `_podConfig.debondCooldown = 0`.
3. Attacker now is able to withdraw all the funds in the contract.

### Impact

This issue will lead to losing all the funds in the protocol.

### PoC

_No response_

### Mitigation

I think you should make sure that there is no redundant item in the list.