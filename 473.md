Keen Jetblack Deer

Medium

# spTKNMinimalOracle calculates debondFee twice, leading to inaccurate oracle price.


### Summary

spTKNMinimalOracle calculates debondFee twice, leading to inaccurate oracle price.

### Root Cause

In `spTKNMinimalOracle`, there are two places that need to calculate debondFees:

1. `_calculateBasePerPTkn` function: Conversion from underlyingTKN/baseTKN => pTKN/baseTKN. For example, from WETH/USDC to pWETH/USDC.
2. `_checkAndHandleBaseTokenPodConfig` function: Conversion from baseTKN/spTKN to poddedBaseTKN/spTKN. For example, if pairedLpTKN is pOHM, this converts OHM/spTKN to pOHM/spTKN.

In both scenarios, it calls `_accountForCBRInPrice()` which calls `DecentralizedIndex.convertToAssets` to calculate CBR rate, then call `_accountForUnwrapFeeInPrice()` to deduce the debondFee. The bug here is, `convertToAssets` already consider debond fees, and the current implementation would calculate it twice.

This would cause the oracle to be underpriced, leading to a lower asset/collateral ratio in lending, meaning the same amount of collateral (spTKN) can borrow more baseTKNs, which increases the lenidng risk.

Also, since the pairedLpTKN -> pTKN swap also uses this oracle [here](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/AutoCompoundingPodLp.sol#L319), this leads to inaccurate slippage.

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/oracle/spTKNMinimalOracle.sol

```solidity

    function _calculateSpTknPerBase(uint256 _price18) internal view returns (uint256 _spTknBasePrice18) {
        ...
        if (BASE_IS_POD) {
@>          _spTknBasePrice18 = _checkAndHandleBaseTokenPodConfig(_spTknBasePrice18);
        } else if (BASE_IS_FRAX_PAIR) {
            _spTknBasePrice18 = IFraxlendPair(BASE_TOKEN).convertToAssets(_spTknBasePrice18);
        }
    }

    function _calculateBasePerPTkn(uint256 _price18) internal view returns (uint256 _basePerPTkn18) {
        // pull from UniV3 TWAP if passed as 0
        if (_price18 == 0) {
            bool _isBadData;
            (_isBadData, _price18) = _getDefaultPrice18();
            if (_isBadData) {
                return 0;
            }
        }
        _basePerPTkn18 = _accountForCBRInPrice(pod, underlyingTkn, _price18);

        // adjust current price for spTKN pod unwrap fee, which will end up making the end price
        // (spTKN per base) higher, meaning it will take more spTKN to equal the value
        // of base token. This will more accurately ensure healthy LTVs when lending since
        // a liquidation path will need to account for unwrap fees
        _basePerPTkn18 = _accountForUnwrapFeeInPrice(pod, _basePerPTkn18);
    }

    function _checkAndHandleBaseTokenPodConfig(uint256 _currentPrice18) internal view returns (uint256 _finalPrice18) {
        _finalPrice18 = _accountForCBRInPrice(BASE_TOKEN, address(0), _currentPrice18);
        _finalPrice18 = _accountForUnwrapFeeInPrice(BASE_TOKEN, _finalPrice18);
    }

    function _accountForCBRInPrice(address _pod, address _underlying, uint256 _amtUnderlying)
        internal
        view
        returns (uint256)
    {
        require(IDecentralizedIndex(_pod).unlocked() == 1, "OU");
        if (_underlying == address(0)) {
            IDecentralizedIndex.IndexAssetInfo[] memory _assets = IDecentralizedIndex(_pod).getAllAssets();
            _underlying = _assets[0].token;
        }
        uint256 _pTknAmt =
            (_amtUnderlying * 10 ** IERC20Metadata(_pod).decimals()) / 10 ** IERC20Metadata(_underlying).decimals();
        return IDecentralizedIndex(_pod).convertToAssets(_pTknAmt);
    }

    function _accountForUnwrapFeeInPrice(address _pod, uint256 _currentPrice)
        internal
        view
        returns (uint256 _newPrice)
    {
        uint16 _unwrapFee = IDecentralizedIndex(_pod).DEBOND_FEE();
        _newPrice = _currentPrice - (_currentPrice * _unwrapFee) / 10000;
    }
```

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/WeightedIndex.sol#L120

```solidity
    function convertToAssets(uint256 _shares) external view override returns (uint256 _assets) {
        bool _firstIn = _isFirstIn();
        uint256 _percSharesX96_2 = _firstIn ? 2 ** (96 / 2) : (_shares * 2 ** (96 / 2)) / _totalSupply;
        if (_firstIn) {
            _assets = (indexTokens[0].q1 * _percSharesX96_2) / FixedPoint96.Q96 / 2 ** (96 / 2);
        } else {
            _assets = (_totalAssets[indexTokens[0].token] * _percSharesX96_2) / 2 ** (96 / 2);
        }
        // @audit-note: Debond fee is already calculated here.
@>      _assets -= ((_assets * _fees.debond) / DEN);
    }
```

### Internal pre-conditions

- debondFee is non-zero.

### External pre-conditions

N/A

### Attack Path

N/A

### Impact

1. Oracle price is undercalculated, leading to larger risk in lending market.
2. pairedLpTKN -> pTKN swap uses inaccurate slippage.

### PoC

N/A

### Mitigation

Only calculate debondFee once.
