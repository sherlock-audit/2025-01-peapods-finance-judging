Striped Champagne Pig

High

# Wrong logic in `UniswapDexAdapter::swapV2SingleExactOut` allows a user to drain the protocol of funds

## Impact
### Summary 
The `UniswapDexAdapter::swapV2SingleExactOut` function allows a user to swap some amounts of one token, say `tokenIn` in exchange for some amounts of another token, say `tokenOut`. However, a user can get some amounts of `tokenOut` without sending any amounts of `tokenIn` to the protocol. This results in a loss of funds for the protocol. 

### Vulnerability Details
The vulnerability lies in the fact that if a user specifies a value greater than zero for `tokenIn`, the said amount is transfered from the user to the protocol. However, when a user specifies zero as the amount of `tokenIn`, no amount of `tokenIn` is transfered from the user to the protocol whereas `tokenOut` is transfered to the user. 

This vulnerability can be seen [here](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/dex/UniswapDexAdapter.sol#L84-L109) or by taking a look at the code snippet below. 

```javascript
    function swapV2SingleExactOut(
        address _tokenIn,
        address _tokenOut,
        uint256 _amountInMax,
        uint256 _amountOut,
        address _recipient
    ) external virtual override returns (uint256 _amountInUsed) {
        uint256 _inBefore = IERC20(_tokenIn).balanceOf(address(this));
        if (_amountInMax == 0) {
93:            _amountInMax = IERC20(_tokenIn).balanceOf(address(this));
        } else {
95:            IERC20(_tokenIn).safeTransferFrom(_msgSender(), address(this), _amountInMax);
        }
        address[] memory _path = new address[](2);
        _path[0] = _tokenIn;
        _path[1] = _tokenOut;
        IERC20(_tokenIn).safeIncreaseAllowance(V2_ROUTER, _amountInMax);
        IUniswapV2Router02(V2_ROUTER).swapTokensForExactTokens(
            _amountOut, _amountInMax, _path, _recipient, block.timestamp
        );
        uint256 _inRemaining = IERC20(_tokenIn).balanceOf(address(this)) - _inBefore;
        if (_inRemaining > 0) {
            IERC20(_tokenIn).safeTransfer(_msgSender(), _inRemaining);
        }
        _amountInUsed = _amountInMax - _inRemaining;
    }
```


### Impact
A malicious user can drain the protocol of funds by specifying `_amountInMax = 0` in the `UniswapDexAdapter::swapV2SingleExactOut` function. From the computation on line 93 in the code snippet above, the protocol assumes the user has deposited `_tokenIn.balanceOf(address(this))`, calculates the equivalent amount of `_tokenOut` and sends to the user. 

## Proof of Concept

1. `userA` calls the `UniswapDexAdapter::swapV2SingleExactOut` function while specifying `_amountInMax` as `0`
2. `userA` gets some amount of `_tokenOut` corresponding to all the amount of `_tokenIn` in the protocol without sending any amount of `_tokenIn` into the protocol.


## Tools Used

Manual Review


## Recommended Mitigation Steps
Recommendation is to carry out the below two steps:
1. Remove the `else` statement so that line 95 in the code snippet above executes irrespective of the value passed in by the user for `_amountInMax`.
2. modify line 93 in the code snippet above to `_amountInMax = IERC20(_tokenIn).balanceOf(_msgSender());` to avoid reverts on line 72 when the user balance is less than the protocol balance. (This recommendation assumes that the protocol implies that a value of `_amountInMax = 0` means the user intends to swap all the `_tokenIn` in their balance)

```diff
    function swapV2SingleExactOut(
        address _tokenIn,
        address _tokenOut,
        uint256 _amountInMax,
        uint256 _amountOut,
        address _recipient
    ) external virtual override returns (uint256 _amountInUsed) {
        uint256 _inBefore = IERC20(_tokenIn).balanceOf(address(this));
        if (_amountInMax == 0) {
-            _amountInMax = IERC20(_tokenIn).balanceOf(address(this));
+            _amountInMax = IERC20(_tokenIn).balanceOf(_msgSender());
+       }
-        } else {
-            IERC20(_tokenIn).safeTransferFrom(_msgSender(), address(this), _amountInMax);
-        }
+        IERC20(_tokenIn).safeTransferFrom(_msgSender(), address(this), _amountInMax);
        address[] memory _path = new address[](2);
        _path[0] = _tokenIn;
        _path[1] = _tokenOut;
        IERC20(_tokenIn).safeIncreaseAllowance(V2_ROUTER, _amountInMax);
        IUniswapV2Router02(V2_ROUTER).swapTokensForExactTokens(
            _amountOut, _amountInMax, _path, _recipient, block.timestamp
        );
        uint256 _inRemaining = IERC20(_tokenIn).balanceOf(address(this)) - _inBefore;
        if (_inRemaining > 0) {
            IERC20(_tokenIn).safeTransfer(_msgSender(), _inRemaining);
        }
        _amountInUsed = _amountInMax - _inRemaining;
    }
```