Keen Jetblack Deer

Medium

# LeverageManager remove leverage may fail for self-lending pods due to not enough tokens for repaying flashloan.


### Summary

LeverageManager remove leverage may fail for self-lending pods due to not enough tokens for repaying flashloan.

### Root Cause

First we need to understand the workflow of removeLeverage in LeverageManager.

1. Borrow `_borrowAssetAmt` underlying token from flashloan source.
2. Repay these tokens to FraxlendPair.
3. Withdraw collateral (aspTKN) to borrowedTKN and pTKN. If borrowedTKN is not enough to repay the flashloan, swap pTKN to borrowedTKN.
4. Repay flashloan.
5. Send leftover borrowedTKN and pTKN back to position owner.

In step 3, we calculate the amount of borrowedTKNs that are short, and set that to the exact output of the pTKN -> borrowedTKN swap. However, in self-lending pods, since the pairedLpTKN is an fraxlend pair fTKN, we need to calculate how much fTKN shares are required to redeem borrowedTKNs.

The bug here is when calculating fTKN shares, `FraxlendPair.convertToShares()` is used, which rounds DOWN. This means we will very likely miss the amount of repayed borrowedTKN by a couple of wei, which means we will fail to repay the flashloan.

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/lvf/LeverageManager.sol

```solidity
    function _acquireBorrowTokenForRepayment(
        LeverageFlashProps memory _props,
        address _pod,
        address _borrowToken,
        uint256 _borrowNeeded,
        uint256 _podAmtReceived,
        uint256 _podSwapAmtOutMin,
        uint256 _userProvidedDebtAmtMax
    ) internal returns (uint256 _podAmtRemaining) {
        _podAmtRemaining = _podAmtReceived;
        uint256 _borrowAmtNeededToSwap = _borrowNeeded;
        if (_userProvidedDebtAmtMax > 0) {
            uint256 _borrowAmtFromUser =
                _userProvidedDebtAmtMax >= _borrowNeeded ? _borrowNeeded : _userProvidedDebtAmtMax;
            _borrowAmtNeededToSwap -= _borrowAmtFromUser;
            IERC20(_borrowToken).safeTransferFrom(_props.sender, address(this), _borrowAmtFromUser);
        }
        // sell pod token into LP for enough borrow token to get enough to repay
        // if self-lending swap for lending pair then redeem for borrow token
        if (_borrowAmtNeededToSwap > 0) {
            if (_isPodSelfLending(_props.positionId)) {
                _podAmtRemaining = _swapPodForBorrowToken(
                    _pod,
                    positionProps[_props.positionId].lendingPair,
                    _podAmtReceived,
                    // @audit-bug: This rounds down, which is wrong. This will lead to not enough repay tokens.
@>                  IFraxlendPair(positionProps[_props.positionId].lendingPair).convertToShares(_borrowAmtNeededToSwap),
                    _podSwapAmtOutMin
                );
                IFraxlendPair(positionProps[_props.positionId].lendingPair).redeem(
                    IERC20(positionProps[_props.positionId].lendingPair).balanceOf(address(this)),
                    address(this),
                    address(this)
                );
            } else {
                _podAmtRemaining = _swapPodForBorrowToken(
                    _pod, _borrowToken, _podAmtReceived, _borrowAmtNeededToSwap, _podSwapAmtOutMin
                );
            }
        }
    }


    function _swapPodForBorrowToken(
        address _pod,
        address _targetToken,
        uint256 _podAmt,
        uint256 _targetNeededAmt,
        uint256 _podSwapAmtOutMin
    ) internal returns (uint256 _podRemainingAmt) {
        IDexAdapter _dexAdapter = IDecentralizedIndex(_pod).DEX_HANDLER();
        uint256 _balBefore = IERC20(_pod).balanceOf(address(this));
        IERC20(_pod).safeIncreaseAllowance(address(_dexAdapter), _podAmt);
        _dexAdapter.swapV2SingleExactOut(
            _pod, _targetToken, _podAmt, _podSwapAmtOutMin == 0 ? _targetNeededAmt : _podSwapAmtOutMin, address(this)
        );
        _podRemainingAmt = _podAmt - (_balBefore - IERC20(_pod).balanceOf(address(this)));
    }
```

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/fraxlend/src/contracts/FraxlendPair.sol#L225

```solidity
    function toAssetShares(uint256 _amount, bool _roundUp, bool _previewInterest)
        public
        view
        returns (uint256 _shares)
    {
        if (_previewInterest) {
            (,,,, VaultAccount memory _totalAsset,) = previewAddInterest();
            _shares = _totalAsset.toShares(_amount, _roundUp);
        } else {
            _shares = totalAsset.toShares(_amount, _roundUp);
        }
    }

    function convertToShares(uint256 _assets) external view returns (uint256 _shares) {
        _shares = toAssetShares(_assets, false, true);
    }
```

### Internal pre-conditions

- Self-lending pods are used, and when removing leverage, a pTKN -> pairedLpTKN swap is required to acquire more borrowTKN for flashloan repay.

### External pre-conditions

N/A

### Attack Path

N/A

### Impact

removeLeverage will fail to repay flashloan for self-lending pods.

### PoC

N/A

### Mitigation

Round up when calculating shares.