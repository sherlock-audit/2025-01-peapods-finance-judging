Keen Jetblack Deer

High

# TokenRewards.sol does not handle paused reward tokens, leading to loss of funds for users.


### Summary

TokenRewards.sol does not handle paused reward tokens, leading to loss of funds for users.

### Root Cause

When spTKN are transferred, the transferrer and receiver both update their rewards. Rewards are distributed in the function `_distributeReward()`.

One feature is that if the reward is paused, the reward distribution for that token would simply skip. However, the critical bug is that `_resetExcluded()` is still called for the user, which updates the user's `rewards[_token][_wallet].excluded` as if the reward was already distributed.

This means if a reward token was paused, if a user A has some unclaimed rewards for this token, anyone can trigger a spTKN transfer to user A to make user A lose all existing rewards for this token. One should expect that if a reward token was unpaused, he can still claim his previous rewards, but this is not the case due to this bug.

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/TokenRewards.sol#L20

```solidity
    function _addShares(address _wallet, uint256 _amount) internal {
        if (shares[_wallet] > 0) {
            _distributeReward(_wallet);
        }
        uint256 sharesBefore = shares[_wallet];
        totalShares += _amount;
        shares[_wallet] += _amount;
        if (sharesBefore == 0 && shares[_wallet] > 0) {
            totalStakers++;
        }
@>      _resetExcluded(_wallet);
    }

    function _removeShares(address _wallet, uint256 _amount) internal {
        require(shares[_wallet] > 0 && _amount <= shares[_wallet], "RE");
        _distributeReward(_wallet);
        totalShares -= _amount;
        shares[_wallet] -= _amount;
        if (shares[_wallet] == 0) {
            totalStakers--;
        }
@>      _resetExcluded(_wallet);
    }

    function _distributeReward(address _wallet) internal {
        if (shares[_wallet] == 0) {
            return;
        }
        for (uint256 _i; _i < _allRewardsTokens.length; _i++) {
            address _token = _allRewardsTokens[_i];

@>          if (REWARDS_WHITELISTER.paused(_token)) {
                continue;
            }

            uint256 _amount = getUnpaid(_token, _wallet);
            rewards[_token][_wallet].realized += _amount;
            rewards[_token][_wallet].excluded = _cumulativeRewards(_token, shares[_wallet], true);
            if (_amount > 0) {
                rewardsDistributed[_token] += _amount;
                IERC20(_token).safeTransfer(_wallet, _amount);
                emit DistributeReward(_wallet, _token, _amount);
            }
        }
    }

    function _resetExcluded(address _wallet) internal {
        for (uint256 _i; _i < _allRewardsTokens.length; _i++) {
            address _token = _allRewardsTokens[_i];
            rewards[_token][_wallet].excluded = _cumulativeRewards(_token, shares[_wallet], true);
        }
    }

```

### Internal pre-conditions

- Admin pauses a certain reward token in RewardsWhitelist.sol.

### External pre-conditions

N/A

### Attack Path

Attacker can transfer 1 wei of spTKN to a victim and make the victim lost all unclaimed rewards of the paused token.

### Impact

The victim can lose all unclaimed rewards of the paused token.

### PoC

N/A

### Mitigation

Also skip update in the `_resetExcluded()` function for paused tokens.