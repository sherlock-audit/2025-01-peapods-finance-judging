Acidic Marmalade Robin

Medium

# Potential DoS of AutoCompoundingPodLp if Fraxlend Pair exceeds deposit limit

### Summary

In the `AutoCompoundingPodLp.sol` contract, the [deposit()](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/AutoCompoundingPodLp.sol#L124), [mint()](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/AutoCompoundingPodLp.sol#L148), [withdraw()](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/AutoCompoundingPodLp.sol#L162) and [redeem()](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/AutoCompoundingPodLp.sol#L176) functions call [_processRewardsToPodLp()]() in which all reward tokens are processed in the loop. And if `IS_PAIRED_LENDING_PAIR=true` then `PAIRED_LP_TOKEN` is deposited to Fraxlend pair, but contract does not check if `PAIRED_LP_TOKEN` deposit will violate the deposit limit, if it will then all functions will revert, leading to DoS of the contract.

### Root Cause

In the `AutoCompoundingPodLp.sol` contract, the `deposit()`, `mint()`, `withdraw()` and `redeem()` functions call `_processRewardsToPodLp()`:

```solidity
function deposit(uint256 _assets, address _receiver) external override returns (uint256 _shares) {
    _processRewardsToPodLp(0, block.timestamp);
    ...
}
function mint(uint256 _shares, address _receiver) external override returns (uint256 _assets) {
    _processRewardsToPodLp(0, block.timestamp);
    ...
}
function withdraw(uint256 _assets, address _receiver, address _owner) external override returns (uint256 _shares) {
    _processRewardsToPodLp(0, block.timestamp);
    ...
}
function redeem(uint256 _shares, address _receiver, address _owner) external override returns (uint256 _assets) {
    _processRewardsToPodLp(0, block.timestamp);
    ...
}

function _processRewardsToPodLp(uint256 _amountLpOutMin, uint256 _deadline) internal returns (uint256 _lpAmtOut) {
    ...
    address[] memory _tokens = ITokenRewards(IStakingPoolToken(_asset()).POOL_REWARDS()).getAllRewardsTokens();
    uint256 _len = _tokens.length + 1;
    for (uint256 _i; _i < _len; _i++) {
        ...
        uint256 _newLp = _tokenToPodLp(_token, _bal, 0, _deadline);
        _lpAmtOut += _newLp;
    }
    ...
}

function _tokenToPodLp(address _token, uint256 _amountIn, uint256 _amountLpOutMin, uint256 _deadline)
    internal
    returns (uint256 _lpAmtOut)
{
    uint256 _pairedOut = _tokenToPairedLpToken(_token, _amountIn);
    ...
}
```
Then in `_tokenToPairedLpToken()` if `IS_PAIRED_LENDING_PAIR=true` `PAIRED_LP_TOKEN` is deposited to Fraxlend pair:

```solidity
function _tokenToPairedLpToken(address _token, uint256 _amountIn) internal returns (uint256 _amountOut) {
    ...
    address _rewardsToken = pod.lpRewardsToken();
    if (_token != _rewardsToken) {
        _amountOut = _swap(_token, _swapOutputTkn, _amountIn, 0);
        if (IS_PAIRED_LENDING_PAIR) {
            //@audit deposit to fraxlend
            _amountOut = _depositIntoLendingPair(_pairedLpToken, _swapOutputTkn, _amountOut);
        }
        return _amountOut;
    }
    ...
    //@audit in all other cases try-catch block is used to swap, so function will not revert
    try DEX_ADAPTER.swapV3Single(
        _rewardsToken,
        _swapOutputTkn,
        REWARDS_POOL_FEE,
        _amountIn,
        0, // _amountOutMin can be 0 because this is nested inside of function with LP slippage provided
        address(this)
    ) returns (uint256 __amountOut) {
        _tokenToPairedSwapAmountInOverride[_rewardsToken][_swapOutputTkn] = 0;
        _amountOut = __amountOut;

        // if this is a self-lending pod, convert the received borrow token
        // into fTKN shares and use as the output since it's the pod paired LP token
        if (IS_PAIRED_LENDING_PAIR) {
            //@audit deposit to fraxlend
            _amountOut = _depositIntoLendingPair(_pairedLpToken, _swapOutputTkn, _amountOut);
        }
    } catch {
        _tokenToPairedSwapAmountInOverride[_rewardsToken][_swapOutputTkn] =
            _amountIn / 2 < _minSwap ? _minSwap : _amountIn / 2;
        IERC20(_rewardsToken).safeDecreaseAllowance(address(DEX_ADAPTER), _amountIn);
        emit TokenToPairedLpSwapError(_rewardsToken, _swapOutputTkn, _amountIn);
    }
}

function _depositIntoLendingPair(address _lendingPair, address _pairAsset, uint256 _depositAmt)
    internal
    returns (uint256 _shares)
{
    IERC20(_pairAsset).safeIncreaseAllowance(address(_lendingPair), _depositAmt);
    _shares = IFraxlendPair(_lendingPair).deposit(_depositAmt, address(this));
}
```

If it's not `IS_PAIRED_LENDING_PAIR` then contract is using try-catch block to swap, but in this case function will just revert leading to DoS.

[FraxlendPairCore.sol:deposit()](https://github.com/FraxFinance/fraxlend/blob/f1eae68d5a4a5877d379d61654a16e42d6591fe5/src/contracts/FraxlendPairCore.sol#L581C9-L581C87):
```solidity
function deposit(uint256 _amount, address _receiver) external nonReentrant returns (uint256 _sharesReceived) {
    if (_receiver == address(0)) revert InvalidReceiver();

    // Accrue interest if necessary
    _addInterest();

    // Pull from storage to save gas
    VaultAccount memory _totalAsset = totalAsset;

    // Check if this deposit will violate the deposit limit
    if (depositLimit < _totalAsset.amount + _amount) revert ExceedsDepositLimit();

    // Calculate the number of fTokens to mint
    _sharesReceived = _totalAsset.toShares(_amount, false);

    // Execute the deposit effects
    _deposit(_totalAsset, _amount.toUint128(), _sharesReceived.toUint128(), _receiver);
}
```

### Internal Pre-conditions

1. `IS_PAIRED_LENDING_PAIR = true`

### External Pre-conditions

1. Fraxlend pair reached deposit limit

### Attack Path

_No response_

### Impact

 DoS of `AutoCompoundingPodLp` if Fraxlend Pair exceeds deposit limit, impossible to deposit and withdraw tokens.

### PoC

_No response_

### Mitigation

Consider wrapping `_depositIntoLendingPair()` in a try-catch block to handle potential revert.