Keen Jetblack Deer

Medium

# FraxlendPair depositLimit should also check for LVA deposits


### Summary

In FraxlendPair, there is a `depositLimit` concept, which limits the maximum amount of assets within the FraxlendPair for risk handling. However, this limit only check against user's direct deposit into FraxlendPair, but does not check for LVA deposit. This means the limit can be easily bypassed, leading to increased risk.

### Root Cause

For regular user deposits to FraxlendPair, the depositLimit check is implemented in the `deposit()` function [here](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/fraxlend/src/contracts/FraxlendPairCore.sol#L634).

```solidity
    function deposit(uint256 _amount, address _receiver) external nonReentrant returns (uint256 _sharesReceived) {
        if (_receiver == address(0)) revert InvalidReceiver();

        // Accrue interest if necessary
        _addInterest();

        // Pull from storage to save gas
        VaultAccount memory _totalAsset = totalAsset;

        // Check if this deposit will violate the deposit limit
        // @audit-bug: This should also limit LVA deposits as well.
@>      if (depositLimit < _totalAsset.totalAmount(address(0)) + _amount) revert ExceedsDepositLimit();

        // Calculate the number of fTokens to mint
        _sharesReceived = _totalAsset.toShares(_amount, false);

        // Execute the deposit effects
        _deposit(_totalAsset, _amount.toUint128(), _sharesReceived.toUint128(), _receiver, true);
    }
```

However, this limit is not added for LVA deposits, specifically in `_depositFromVault()` function, and the internal `_deposit()` function.

```solidity
    function _depositFromVault(uint256 _amount) internal returns (uint256 _sharesReceived) {
        // Pull from storage to save gas
        VaultAccount memory _totalAsset = totalAsset;

        // Calculate the number of fTokens to mint
        _sharesReceived = _totalAsset.toShares(_amount, false);

        // Withdraw assets from external vault here
        externalAssetVault.whitelistWithdraw(_amount);

        // Execute the deposit effects
        _deposit(_totalAsset, _amount.toUint128(), _sharesReceived.toUint128(), address(externalAssetVault), false);
    }
```

A common use case is when users are borrowing assets, if the asset amount within FraxlendPair is not enough, it will use the assets from LVA. This may lead to deposit asset amount larger than depositLimit, which defeats the purpose of this limit, and increases risk.

```solidity
    function _borrowAsset(uint128 _borrowAmount, address _receiver) internal returns (uint256 _sharesAdded) {
        // Get borrow accounting from storage to save gas
        VaultAccount memory _totalBorrow = totalBorrow;

        // Check available capital (not strictly necessary because balance will underflow, but better revert message)
        uint256 _totalAssetsAvailable = _totalAssetAvailable(totalAsset, _totalBorrow, true);
        if (_totalAssetsAvailable < _borrowAmount) {
            revert InsufficientAssetsInContract(_totalAssetsAvailable, _borrowAmount);
        }
        uint256 _localAssetsAvailable = _totalAssetAvailable(totalAsset, _totalBorrow, false);
        if (_localAssetsAvailable < _borrowAmount) {
            uint256 _externalAmt = _borrowAmount - _localAssetsAvailable;
@>          _depositFromVault(_externalAmt);
        }
        ...
    }
```

### Internal pre-conditions

- Users borrow asset, or users redeem asset, and triggers an LVA deposit to FraxlendPair.

### External pre-conditions

N/A

### Attack Path

N/A

### Impact

`depositLimit` can be exceeded for a FraxlendPair. This defeats the purpose of this variable, which is used to limit risk of the lending market.

### PoC

N/A

### Mitigation

Move the check `if (depositLimit < _totalAsset.totalAmount(address(0)) + _amount) revert ExceedsDepositLimit();` into the internal `_deposit()` function.