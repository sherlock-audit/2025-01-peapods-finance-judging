Late Saffron Jaguar

High

# Borrowers will be unfairly liquidate due to the spTKNMinimalOracle returning incorrect prices.

### Summary

The spTKNMinimalOracle uses a couple of oracles to determine the price of the staked and podded version of a token. It has a function to account for the collateral-backing-ratio and another function to account for unwrap fees when fetching the price. In [_accountForCBRInPrice()](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/oracle/spTKNMinimalOracle.sol#L243C9-L243C68), it uses [convertToAssets()](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/WeightedIndex.sol#L128), which deducts the unwrap fees before returning the asset amount. This issue gets bigger with [_accountForUnwrapFeeInPrice()](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/oracle/spTKNMinimalOracle.sol#L252), which fetch the unwrap fee percentage from the pod, calculate and deduct it from the input amount. Both of these functions are used in [_checkAndHandleBaseTokenPodConfig()](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/oracle/spTKNMinimalOracle.sol#L213) and [_calculateBasePerPTkn()](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/oracle/spTKNMinimalOracle.sol#L170). And those functions are used in [_calculateSpTknPerBase()](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/oracle/spTKNMinimalOracle.sol#L145), which is used to fetch the price in [getPrice()](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/oracle/spTKNMinimalOracle.sol#L113). This will make the spTKN worth less than they actually are, which will be used in determining the health factor during liquidations in FraxLend, causing users to be wrongly liquidated. 

### Root Cause

In _accountForCBRInPrice(), convertToAssets is called, 

```solidity 
    function _accountForCBRInPrice(address _pod, address _underlying, uint256 _amtUnderlying)
        internal
        view
        returns (uint256)
    {
        require(IDecentralizedIndex(_pod).unlocked() == 1, "OU");
        if (_underlying == address(0)) {
            IDecentralizedIndex.IndexAssetInfo[] memory _assets = IDecentralizedIndex(_pod).getAllAssets();
            _underlying = _assets[0].token;
        }
        uint256 _pTknAmt =
            (_amtUnderlying * 10 ** IERC20Metadata(_pod).decimals()) / 10 ** IERC20Metadata(_underlying).decimals();
@>      return IDecentralizedIndex(_pod).convertToAssets(_pTknAmt);
    }
```

This function already accounts for the unwrap fees. In WeightedIndex.sol::convertToAssets(),

```solidity
    function convertToAssets(uint256 _shares) external view override returns (uint256 _assets) {
        bool _firstIn = _isFirstIn();
        uint256 _percSharesX96_2 = _firstIn ? 2 ** (96 / 2) : (_shares * 2 ** (96 / 2)) / _totalSupply;
        if (_firstIn) {
            _assets = (indexTokens[0].q1 * _percSharesX96_2) / FixedPoint96.Q96 / 2 ** (96 / 2);
        } else {
            _assets = (_totalAssets[indexTokens[0].token] * _percSharesX96_2) / 2 ** (96 / 2);
        }
@>      _assets -= ((_assets * _fees.debond) / DEN);
    }
```

The unwrap fee percentage is calculated and deducted again in _accountForUnwrapFeeInPrice(), 

```solidity 
    function _accountForUnwrapFeeInPrice(address _pod, uint256 _currentPrice)
        internal
        view
        returns (uint256 _newPrice)
    {
        uint16 _unwrapFee = IDecentralizedIndex(_pod).DEBOND_FEE();
        _newPrice = _currentPrice - (_currentPrice * _unwrapFee) / 10000;
    }
```
Both of the functions are used when fetching the base per pTKN price in _calculateBasePerPTkn()

```solidity
    function _calculateBasePerPTkn(uint256 _price18) internal view returns (uint256 _basePerPTkn18) {
        if (_price18 == 0) {
            bool _isBadData;
            (_isBadData, _price18) = _getDefaultPrice18();
            if (_isBadData) {
                return 0;
            }
        }
        _basePerPTkn18 = _accountForCBRInPrice(pod, underlyingTkn, _price18);
        _basePerPTkn18 = _accountForUnwrapFeeInPrice(pod, _basePerPTkn18);
    }
```
used to calculate the spTKN price in _calculateSpTknPerBase()

```solidity
    function _calculateSpTknPerBase(uint256 _price18) internal view returns (uint256 _spTknBasePrice18) {
        uint256 _priceBasePerPTkn18 = _calculateBasePerPTkn(_price18);
        address _pair = _getPair();

        (uint112 _reserve0, uint112 _reserve1) = V2_RESERVES.getReserves(_pair);
        uint256 _k = uint256(_reserve0) * _reserve1;
        uint256 _kDec = 10 ** IERC20Metadata(IUniswapV2Pair(_pair).token0()).decimals()
            * 10 ** IERC20Metadata(IUniswapV2Pair(_pair).token1()).decimals();
        uint256 _avgBaseAssetInLp18 = _sqrt((_priceBasePerPTkn18 * _k) / _kDec) * 10 ** (18 / 2);
        uint256 _basePerSpTkn18 =
            (2 * _avgBaseAssetInLp18 * 10 ** IERC20Metadata(_pair).decimals()) / IERC20(_pair).totalSupply();
        require(_basePerSpTkn18 > 0, "V2R");
        _spTknBasePrice18 = 10 ** (18 * 2) / _basePerSpTkn18;
        //issue is even bigger when the Base token is also a podded token
        if (BASE_IS_POD) {
            _spTknBasePrice18 = _checkAndHandleBaseTokenPodConfig(_spTknBasePrice18);
        } else if (BASE_IS_FRAX_PAIR) {
            _spTknBasePrice18 = IFraxlendPair(BASE_TOKEN).convertToAssets(_spTknBasePrice18);
        }
    }
```
used to fetch and calculate price in getPrices()

```solidity
    function getPrices()
        public
        view
        virtual
        override
        returns (bool _isBadData, uint256 _priceLow, uint256 _priceHigh)
    {
        uint256 _priceSpTKNBase = _calculateSpTknPerBase(0);
        ....

        uint256 _clPrice18 = _chainlinkBasePerPaired18();
        uint256 _clPriceBaseSpTKN = _calculateSpTknPerBase(_clPrice18);
```

which is used to calculate the exchange rate used in liquidations.

```solidity
  function _updateExchangeRate() internal returns (bool _isBorrowAllowed, uint256 _lowExchangeRate, uint256 _highExchangeRate) {
    // Pull from storage to save gas and set default return values
    ExchangeRateInfo memory _exchangeRateInfo = exchangeRateInfo;

    // Short circuit if already updated this block
    if (_exchangeRateInfo.lastTimestamp != block.timestamp) {
      // Get the latest exchange rate from the dual oracle
      bool _oneOracleBad;
      (_oneOracleBad, _lowExchangeRate, _highExchangeRate) = IDualOracle(_exchangeRateInfo.oracle).getPrices();
.....
```

### Internal Pre-conditions

Unwrap fee is non-zero and Exchange rate gets updated in FraxLend. 

### External Pre-conditions

None

### Attack Path

None. Logic flow error.

### Impact

This will make the collateral value of the borrowers worth less, since exchange rate is lower than the real exchange rate in fraxLend, causing liquidations even when their completely healthy. 

### PoC

_No response_

### Mitigation

Make the convertToAssets function not account for unwrap fees. 

```diff
    function convertToAssets(uint256 _shares) external view override returns (uint256 _assets) {
        bool _firstIn = _isFirstIn();
        uint256 _percSharesX96_2 = _firstIn ? 2 ** (96 / 2) : (_shares * 2 ** (96 / 2)) / _totalSupply;
        if (_firstIn) {
            _assets = (indexTokens[0].q1 * _percSharesX96_2) / FixedPoint96.Q96 / 2 ** (96 / 2);
        } else {
            _assets = (_totalAssets[indexTokens[0].token] * _percSharesX96_2) / 2 ** (96 / 2);
        }
--      _assets -= ((_assets * _fees.debond) / DEN);
    }
```