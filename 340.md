Bumpy Hemp Cuckoo

High

# Incorrect ratio calculation in `LendingAssetVault.sol` will cause asset loss for depositors


## Summary
Incorrect ratio calculation in `LendingAssetVault.sol` will cause asset loss for depositors as the tracked total assets become lower than actual value when CBR decreases

## Root Cause
In [`LendingAssetVault.sol:294`](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/LendingAssetVault.sol#L294) there is an incorrect calculation of vault asset ratio change when CBR decreases:
```solidity
    function _updateAssetMetadataFromVault(address _vault) internal {
        uint256 _prevVaultCbr = _vaultWhitelistCbr[_vault];
        _vaultWhitelistCbr[_vault] = IERC4626(_vault).convertToAssets(PRECISION);
        if (_prevVaultCbr == 0) {
            return;
        }
        uint256 _vaultAssetRatioChange = _prevVaultCbr > _vaultWhitelistCbr[_vault]
->          ? ((PRECISION * _prevVaultCbr) / _vaultWhitelistCbr[_vault]) - PRECISION
            : ((PRECISION * _vaultWhitelistCbr[_vault]) / _prevVaultCbr) - PRECISION;

        uint256 _currentAssetsUtilized = vaultUtilization[_vault];
        uint256 _changeUtilizedState = (_currentAssetsUtilized * _vaultAssetRatioChange) / PRECISION;
        vaultUtilization[_vault] = _prevVaultCbr > _vaultWhitelistCbr[_vault]
            ? _currentAssetsUtilized < _changeUtilizedState ? 0 : _currentAssetsUtilized - _changeUtilizedState
            : _currentAssetsUtilized + _changeUtilizedState;
        _totalAssetsUtilized = _totalAssetsUtilized - _currentAssetsUtilized + vaultUtilization[_vault];
        _totalAssets = _totalAssets - _currentAssetsUtilized + vaultUtilization[_vault];
        emit UpdateAssetMetadataFromVault(_vault, _totalAssets, _totalAssetsUtilized);
    }
```

When CBR decreases (previous > current), the formula `a/b - 1` is used instead of the correct `1 - b/a`. Since `1 - b/a` < `a/b - 1`, this results in a larger reduction of total assets than should occur.

## Internal pre-conditions
1. Vault utilization must be `1e18` 
2. Initial CBR must be `1e27`
3. CBR must decrease to `0.9e27`

## Impact
Depositors suffer asset value loss whenever CBR decreases. Using the pre-conditions above:
- `_vaultAssetRatioChange` becomes `1e27 * 1e27 / 0.9e27 - 1e27 = 0.11..1e27`
- Final total assets incorrectly reduce to `0.88..89e18` instead of proper value

## Mitigation
Update ratio calculation in affected functions:

```solidity
    function _updateAssetMetadataFromVault(address _vault) internal {
        uint256 _prevVaultCbr = _vaultWhitelistCbr[_vault];
        _vaultWhitelistCbr[_vault] = IERC4626(_vault).convertToAssets(PRECISION);
        if (_prevVaultCbr == 0) {
            return;
        }
        uint256 _vaultAssetRatioChange = _prevVaultCbr > _vaultWhitelistCbr[_vault]
-           ? ((PRECISION * _prevVaultCbr) / _vaultWhitelistCbr[_vault]) - PRECISION
+           ? PRECISION - ((PRECISION * _vaultWhitelistCbr[_vault]) / _prevVaultCbr)
            : ((PRECISION * _vaultWhitelistCbr[_vault]) / _prevVaultCbr) - PRECISION;

        uint256 _currentAssetsUtilized = vaultUtilization[_vault];
        uint256 _changeUtilizedState = (_currentAssetsUtilized * _vaultAssetRatioChange) / PRECISION;
        vaultUtilization[_vault] = _prevVaultCbr > _vaultWhitelistCbr[_vault]
            ? _currentAssetsUtilized < _changeUtilizedState ? 0 : _currentAssetsUtilized - _changeUtilizedState
            : _currentAssetsUtilized + _changeUtilizedState;
        _totalAssetsUtilized = _totalAssetsUtilized - _currentAssetsUtilized + vaultUtilization[_vault];
        _totalAssets = _totalAssets - _currentAssetsUtilized + vaultUtilization[_vault];
        emit UpdateAssetMetadataFromVault(_vault, _totalAssets, _totalAssetsUtilized);
    }
```

And similarly for `_previewAddInterestAndMdInAllVaults()`:

```solidity
    function _previewAddInterestAndMdInAllVaults() internal view returns (uint256 _previewTotalAssets) {
        _previewTotalAssets = _totalAssets;
        uint256 _l = _vaultWhitelistAry.length;
        for (uint256 _i; _i < _l; _i++) {
            address _vault = _vaultWhitelistAry[_i];
            uint256 _prevVaultCbr = _vaultWhitelistCbr[_vault];
            if (_prevVaultCbr == 0) {
                continue;
            }

            // the following effectively simulates addInterest + convertToAssets
            (,,,, VaultAccount memory _totalAsset,) = IFraxlendPair(_vault).previewAddInterest();
            uint256 _newVaultCbr = _totalAsset.toAmount(PRECISION, false);

            uint256 _vaultAssetRatioChange = _prevVaultCbr > _newVaultCbr
-               ? ((PRECISION * _prevVaultCbr) / _newVaultCbr) - PRECISION
+               ? PRECISION - ((PRECISION * _newVaultCbr) / _prevVaultCbr)
                : ((PRECISION * _newVaultCbr) / _prevVaultCbr) - PRECISION;
            uint256 _currentAssetsUtilized = vaultUtilization[_vault];
            uint256 _changeUtilizedState = (_currentAssetsUtilized * _vaultAssetRatioChange) / PRECISION;
            uint256 _newAssetsUtilized = _prevVaultCbr > _newVaultCbr
                ? _currentAssetsUtilized < _changeUtilizedState ? 0 : _currentAssetsUtilized - _changeUtilizedState
                : _currentAssetsUtilized + _changeUtilizedState;
            _previewTotalAssets = _previewTotalAssets - _currentAssetsUtilized + _newAssetsUtilized;
        }
    }
```
