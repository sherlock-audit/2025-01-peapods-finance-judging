Silly Maroon Shell

Medium

# Recursive decreasing of `_amountOutMin` will cause loss of funds.

### Summary

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/Zapper.sol#L177

The recursive decreasing of `_amountOutMin` will cause weak slippage check as an attacker can sandwich and steal funds.


### Root Cause

- In Zapper.sol:177, there is wrong calculation of `amountOutMin`.
```solidity
    function _swapV3Single(address _in, uint24 _fee, address _out, uint256 _amountIn, uint256 _amountOutMin)
        internal
        returns (uint256)
    {
        address _v3Pool;
        try DEX_ADAPTER.getV3Pool(_in, _out, uint24(10000)) returns (address __v3Pool) {
            _v3Pool = __v3Pool;
        } catch {
            _v3Pool = DEX_ADAPTER.getV3Pool(_in, _out, int24(200));
        }
@>      if (_amountOutMin == 0) {
            address _token0 = _in < _out ? _in : _out;
            uint256 _poolPriceX96 =
                V3_TWAP_UTILS.priceX96FromSqrtPriceX96(V3_TWAP_UTILS.sqrtPriceX96FromPoolAndInterval(_v3Pool));
@>          _amountOutMin = _in == _token0
                ? (_poolPriceX96 * _amountIn) / FixedPoint96.Q96
                : (_amountIn * FixedPoint96.Q96) / _poolPriceX96;
        }

        uint256 _outBefore = IERC20(_out).balanceOf(address(this));
        uint256 _finalSlip = _slippage[_v3Pool] > 0 ? _slippage[_v3Pool] : _defaultSlippage;
        IERC20(_in).safeIncreaseAllowance(address(DEX_ADAPTER), _amountIn);
        DEX_ADAPTER.swapV3Single(
@>          _in, _out, _fee, _amountIn, (_amountOutMin * (1000 - _finalSlip)) / 1000, address(this)
        );
        return IERC20(_out).balanceOf(address(this)) - _outBefore;
    }
```
As we can see above, `amountOutMin` is always decreased by `_finalSlip` even if the user passed valid `_amountOutMin > 0`.

### Internal Pre-conditions

-

### External Pre-conditions

-

### Attack Path

-

### Impact

An attacker can sandwich and steal funds because of weak slippage check.


### PoC

_No response_

### Mitigation

Modify `Zapper.sol#_swapV3Single()` function as follows.
```solidity
    function _swapV3Single(address _in, uint24 _fee, address _out, uint256 _amountIn, uint256 _amountOutMin)
        internal
        returns (uint256)
    {
        address _v3Pool;
        try DEX_ADAPTER.getV3Pool(_in, _out, uint24(10000)) returns (address __v3Pool) {
            _v3Pool = __v3Pool;
        } catch {
            _v3Pool = DEX_ADAPTER.getV3Pool(_in, _out, int24(200));
        }
++      uint256 _finalSlip = _slippage[_v3Pool] > 0 ? _slippage[_v3Pool] : _defaultSlippage;
        if (_amountOutMin == 0) {
            address _token0 = _in < _out ? _in : _out;
            uint256 _poolPriceX96 =
                V3_TWAP_UTILS.priceX96FromSqrtPriceX96(V3_TWAP_UTILS.sqrtPriceX96FromPoolAndInterval(_v3Pool));
            _amountOutMin = _in == _token0
                ? (_poolPriceX96 * _amountIn) / FixedPoint96.Q96
                : (_amountIn * FixedPoint96.Q96) / _poolPriceX96;
        }
++      else{
++          _finalSlip = 0;
++      }

        uint256 _outBefore = IERC20(_out).balanceOf(address(this));
--      uint256 _finalSlip = _slippage[_v3Pool] > 0 ? _slippage[_v3Pool] : _defaultSlippage;
        IERC20(_in).safeIncreaseAllowance(address(DEX_ADAPTER), _amountIn);
        DEX_ADAPTER.swapV3Single(
            _in, _out, _fee, _amountIn, (_amountOutMin * (1000 - _finalSlip)) / 1000, address(this)
        );
        return IERC20(_out).balanceOf(address(this)) - _outBefore;
    }
```