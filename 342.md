Bumpy Hemp Cuckoo

High

# Yield Conversion will Fail in `AutoCompoundingPodLp`


## Summary
Incorrect balance consideration in `_pairedSwapAmt` calculation will cause a reversion of `indexUtils.addLPAndStake` through slippage protection as the protocol fails to properly convert yield to staking pool assets in `AutoCompoundingPodLp`.

## Root Cause
In [`AutoCompoundingPodLp.sol:L313`](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/AutoCompoundingPodLp.sol#L313) the calculation of `_pairedSwapAmt` fails to account for existing pod balances, leading to incorrect swap amounts:

```solidity
    function _pairedLpTokenToPodLp(uint256 _amountIn, uint256 _deadline) internal returns (uint256 _amountOut) {
        address _pairedLpToken = pod.PAIRED_LP_TOKEN();
313->   uint256 _pairedSwapAmt = _getSwapAmt(_pairedLpToken, address(pod), _pairedLpToken, _amountIn);
        uint256 _pairedRemaining = _amountIn - _pairedSwapAmt;
        uint256 _minPtknOut;
        if (address(podOracle) != address(0)) {
            // calculate the min out with 5% slippage
            _minPtknOut = (
                podOracle.getPodPerBasePrice() * _pairedSwapAmt * 10 ** IERC20Metadata(address(pod)).decimals() * 95
            ) / 10 ** IERC20Metadata(_pairedLpToken).decimals() / 10 ** 18 / 100;
        }
        IERC20(_pairedLpToken).safeIncreaseAllowance(address(DEX_ADAPTER), _pairedSwapAmt);
323->      try DEX_ADAPTER.swapV2Single(_pairedLpToken, address(pod), _pairedSwapAmt, _minPtknOut, address(this)) returns (
            uint256 _podAmountOut
        ) {
            // reset here to local balances to accommodate any residual leftover from previous runs
327->          _podAmountOut = pod.balanceOf(address(this));
            _pairedRemaining = IERC20(_pairedLpToken).balanceOf(address(this)) - _protocolFees;
            IERC20(pod).safeIncreaseAllowance(address(indexUtils), _podAmountOut);
            IERC20(_pairedLpToken).safeIncreaseAllowance(address(indexUtils), _pairedRemaining);
            try indexUtils.addLPAndStake(
332->              pod, _podAmountOut, _pairedLpToken, _pairedRemaining, _pairedRemaining, lpSlippage, _deadline
            ) returns (uint256 _lpTknOut) {
                _amountOut = _lpTknOut;
            } catch {
                IERC20(pod).safeDecreaseAllowance(address(indexUtils), _podAmountOut);
                IERC20(_pairedLpToken).safeDecreaseAllowance(address(indexUtils), _pairedRemaining);
                emit AddLpAndStakeError(address(pod), _amountIn);
            }
        } catch {
            IERC20(_pairedLpToken).safeDecreaseAllowance(address(DEX_ADAPTER), _pairedSwapAmt);
            emit AddLpAndStakeV2SwapError(_pairedLpToken, address(pod), _pairedRemaining);
        }
    }
```

## Internal pre-conditions
1. Contract must have residual `pod` token balance from previous operations
2. New yield must be accrued in `pairedLpToken`

## Attack Path
1. Contract accrues new yield in `pairedLpToken`
2. `_pairedLpTokenToPodLp` is called to convert yield
3. `_getSwapAmt` calculates swap amount without considering existing pod balance
4. After swap, the ratio between pod and pairedLpToken becomes imbalanced
5. `indexUtils.addLPAndStake` reverts due to slippage protection

## Impact
The protocol fails to convert accrued yields into stakeable assets, resulting in lost rewards for depositors.

## PoC
Given the following scenario:
1. Contract has 1e18 pod tokens initially
2. Pool ratio is 1:1 (pod:pairedLpToken)
3. 2e18 pairedLpToken accrues as yield
4. Swap calculation returns 1e18 as swap amount
5. Final balances: 2e18 pod tokens and 1e18 pairedLpToken
6. Transaction reverts due to 2:1 ratio mismatch with pool's 1:1 ratio

## Mitigation
Modify `_getSwapAmt` calculation to include existing pod balance when determining optimal swap amounts.

