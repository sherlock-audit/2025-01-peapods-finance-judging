Silly Maroon Shell

High

# Wrong calculation of `_vaultAssetRatioChange` will cause loss of assets to users.

### Summary

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/LendingAssetVault.sol#L294

Wrong calculation of `_vaultAssetRatioChange` will cause loss of assets to depositors as tracked total assets are less than real.


### Root Cause

- In LendingAssetVault.sol:294, there is wrong calculation of `_vaultAssetRatioChange` in case of decreased cbr.
```solidity
    function _updateAssetMetadataFromVault(address _vault) internal {
        uint256 _prevVaultCbr = _vaultWhitelistCbr[_vault];
        _vaultWhitelistCbr[_vault] = IERC4626(_vault).convertToAssets(PRECISION);
        if (_prevVaultCbr == 0) {
            return;
        }
        uint256 _vaultAssetRatioChange = _prevVaultCbr > _vaultWhitelistCbr[_vault]
@>          ? ((PRECISION * _prevVaultCbr) / _vaultWhitelistCbr[_vault]) - PRECISION  // @audit: this is wrong
            : ((PRECISION * _vaultWhitelistCbr[_vault]) / _prevVaultCbr) - PRECISION;

        uint256 _currentAssetsUtilized = vaultUtilization[_vault];
        uint256 _changeUtilizedState = (_currentAssetsUtilized * _vaultAssetRatioChange) / PRECISION;
        vaultUtilization[_vault] = _prevVaultCbr > _vaultWhitelistCbr[_vault]
            ? _currentAssetsUtilized < _changeUtilizedState ? 0 : _currentAssetsUtilized - _changeUtilizedState
            : _currentAssetsUtilized + _changeUtilizedState;
        _totalAssetsUtilized = _totalAssetsUtilized - _currentAssetsUtilized + vaultUtilization[_vault];
        _totalAssets = _totalAssets - _currentAssetsUtilized + vaultUtilization[_vault];
        emit UpdateAssetMetadataFromVault(_vault, _totalAssets, _totalAssetsUtilized);
    }
```
We say that `_prevVaultCbr = a` and current vault cbr is `b`.
In case of increased cbr, `a < b` and `_vaultAssetRatioChange` becomes `b / a - 1`.   
But in case of decreased cbr, `a > b` and `_vaultAssetRatioChange` is calculated wrongly with `a / b - 1`.   
The correct calculation is `1 - b / a` and `1 - b / a` < `a / b - 1`.   
So totalAssets are decreased more than real.

### Internal Pre-conditions

-

### External Pre-conditions

-

### Attack Path

- We say that a vault's utilization is 1e18 and `totalUtilized = totalAssets = 1e18` when `cbr = 1e27`.   
- Then, cbr is changed to `0.9e27` because of bad debt.   
- Then, `_vaultAssetRatioChange` becomes `1e27 * 1e27 / 0.9e27  - 1e27` = 0.11..1e27. 
So `_changeUtilizedState` becomes 1e18 * 0.11..1e27 / 1e27 = 0.11...1e18.   
Therefore, result `totalAssets` become 1e18 - 0.11...1e18 = 0.88...89e18.

In fact, real result `totalAssets` is 1e18 * 0.9e27 / 1e27 = 0.9e18.   
So loss of assets is `0.9e18 - 0.88...89e18 = 0.011...1e18`.   
Therefore, depositors lose 1% of assets about 10% of cbr decreasing.   
This fact means that depositors lose 10% of assets more than real decreasing.

### Impact

The depositors will lose funds when cbr is decreased.


### PoC

- We say that a vault's utilization is 1e18 and `totalUtilized = totalAssets = 1e18` when `cbr = 1e27`.   
- Then, cbr is changed to `0.9e27` because of bad debt.   
- Then, `_vaultAssetRatioChange` becomes `1e27 * 1e27 / 0.9e27  - 1e27` = 0.11..1e27. 
So `_changeUtilizedState` becomes 1e18 * 0.11..1e27 / 1e27 = 0.11...1e18.   
Therefore, result `totalAssets` become 1e18 - 0.11...1e18 = 0.88...89e18.

In fact, real result `totalAssets` is 1e18 * 0.9e27 / 1e27 = 0.9e18.   
So loss of assets is `0.9e18 - 0.88...89e18 = 0.011...1e18`.   
Therefore, depositors lose 1% of assets about 10% of cbr decreasing.   
This fact means that depositors lose 10% of assets more than real decreasing.

### Mitigation

1. Modify `LendingAssetVault.sol#_updateAssetMetadataFromVault()` function as follows.
```solidity
    function _updateAssetMetadataFromVault(address _vault) internal {
        uint256 _prevVaultCbr = _vaultWhitelistCbr[_vault];
        _vaultWhitelistCbr[_vault] = IERC4626(_vault).convertToAssets(PRECISION);
        if (_prevVaultCbr == 0) {
            return;
        }
        uint256 _vaultAssetRatioChange = _prevVaultCbr > _vaultWhitelistCbr[_vault]
--          ? ((PRECISION * _prevVaultCbr) / _vaultWhitelistCbr[_vault]) - PRECISION
++          ? PRECISION - ((PRECISION * _vaultWhitelistCbr[_vault]) / _prevVaultCbr)
            : ((PRECISION * _vaultWhitelistCbr[_vault]) / _prevVaultCbr) - PRECISION;

        uint256 _currentAssetsUtilized = vaultUtilization[_vault];
        uint256 _changeUtilizedState = (_currentAssetsUtilized * _vaultAssetRatioChange) / PRECISION;
        vaultUtilization[_vault] = _prevVaultCbr > _vaultWhitelistCbr[_vault]
            ? _currentAssetsUtilized < _changeUtilizedState ? 0 : _currentAssetsUtilized - _changeUtilizedState
            : _currentAssetsUtilized + _changeUtilizedState;
        _totalAssetsUtilized = _totalAssetsUtilized - _currentAssetsUtilized + vaultUtilization[_vault];
        _totalAssets = _totalAssets - _currentAssetsUtilized + vaultUtilization[_vault];
        emit UpdateAssetMetadataFromVault(_vault, _totalAssets, _totalAssetsUtilized);
    }
```
2. Modify `LendingAssetVault.sol#_previewAddInterestAndMdInAllVaults()` function as follows.
```solidity
    function _previewAddInterestAndMdInAllVaults() internal view returns (uint256 _previewTotalAssets) {
        _previewTotalAssets = _totalAssets;
        uint256 _l = _vaultWhitelistAry.length;
        for (uint256 _i; _i < _l; _i++) {
            address _vault = _vaultWhitelistAry[_i];
            uint256 _prevVaultCbr = _vaultWhitelistCbr[_vault];
            if (_prevVaultCbr == 0) {
                continue;
            }

            // the following effectively simulates addInterest + convertToAssets
            (,,,, VaultAccount memory _totalAsset,) = IFraxlendPair(_vault).previewAddInterest();
            uint256 _newVaultCbr = _totalAsset.toAmount(PRECISION, false);

            uint256 _vaultAssetRatioChange = _prevVaultCbr > _newVaultCbr
--              ? ((PRECISION * _prevVaultCbr) / _newVaultCbr) - PRECISION
++              ? PRECISION - ((PRECISION * _newVaultCbr) / _prevVaultCbr)
                : ((PRECISION * _newVaultCbr) / _prevVaultCbr) - PRECISION;
            uint256 _currentAssetsUtilized = vaultUtilization[_vault];
            uint256 _changeUtilizedState = (_currentAssetsUtilized * _vaultAssetRatioChange) / PRECISION;
            uint256 _newAssetsUtilized = _prevVaultCbr > _newVaultCbr
                ? _currentAssetsUtilized < _changeUtilizedState ? 0 : _currentAssetsUtilized - _changeUtilizedState
                : _currentAssetsUtilized + _changeUtilizedState;
            _previewTotalAssets = _previewTotalAssets - _currentAssetsUtilized + _newAssetsUtilized;
        }
    }
```