Keen Jetblack Deer

Medium

# LVF feature won't work if `stakeUserRestriction` was set for StakingPoolToken.


### Summary

LVF feature won't work if `stakeUserRestriction` was set for StakingPoolToken.

### Root Cause

In StakingPoolToken.sol, there is a `stakeUserRestriction` feature that restricts only a specific user can stake LP tokens to receive spTKN.

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/StakingPoolToken.sol#L70

```solidity
    function stake(address _user, uint256 _amount) external override {
        require(stakingToken != address(0), "I");
        if (stakeUserRestriction != address(0)) {
@>          require(_user == stakeUserRestriction, "U");
        }
        _mint(_user, _amount);
        IERC20(stakingToken).safeTransferFrom(_msgSender(), address(this), _amount);
        emit Stake(_msgSender(), _user, _amount);
    }
```

In the LVF feature, the AutoCompoundingPodLP automatically swap rewards to pairedLpTKN and pTKN, then stake for spTKN and recompound it. However, when staking for spTKN, it uses `IndexUtils.sol#addLPAndStake()` function which internally calls `_stakeLPForUserHandlingLeftoverCheck()` function. Inside this function, it either stakes to the `IndexUtils.sol` contract itself, or stakes to `AutoCompoundingPodLp.sol` contract, which would both fail if `stakeUserRestriction` is set.

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/AutoCompoundingPodLp.sol#L331

```solidity
@>  try indexUtils.addLPAndStake(
        pod, _podAmountOut, _pairedLpToken, _pairedRemaining, _pairedRemaining, lpSlippage, _deadline
    ) returns (uint256 _lpTknOut) {
        _amountOut = _lpTknOut;
    } catch {
        IERC20(pod).safeDecreaseAllowance(address(indexUtils), _podAmountOut);
        IERC20(_pairedLpToken).safeDecreaseAllowance(address(indexUtils), _pairedRemaining);
        emit AddLpAndStakeError(address(pod), _amountIn);
    }
```

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/IndexUtils.sol#L154

```solidity
    function addLPAndStake(
        IDecentralizedIndex _indexFund,
        uint256 _amountIdxTokens,
        address _pairedLpTokenProvided,
        uint256 _amtPairedLpTokenProvided,
        uint256 _amountPairedLpTokenMin,
        uint256 _slippage,
        uint256 _deadline
    ) external payable override returns (uint256 _amountOut) {
        ...
@>      _amountOut = _stakeLPForUserHandlingLeftoverCheck(_indexFund.lpStakingPool(), _msgSender(), _amountOut);
        ...
    }

    function _stakeLPForUserHandlingLeftoverCheck(address _stakingPool, address _receiver, uint256 _stakeAmount)
        internal
        returns (uint256 _finalAmountOut)
    {
        _finalAmountOut = _stakeAmount;
        if (IERC20(_stakingPool).balanceOf(address(this)) > 0) {
            IStakingPoolToken(_stakingPool).stake(_receiver, _stakeAmount);
            return _finalAmountOut;
        }

@>      IStakingPoolToken(_stakingPool).stake(address(this), _stakeAmount);
        // leave 1 wei in the CA for future gas savings
        _finalAmountOut = IERC20(_stakingPool).balanceOf(address(this)) - 1;
        IERC20(_stakingPool).safeTransfer(_receiver, _finalAmountOut);
    }
```

### Internal pre-conditions

- `stakeUserRestriction` is set

### External pre-conditions

N/A

### Attack Path

N/A

### Impact

If `stakeUserRestriction` is set, the LVF feature would not work at all.

This may or may not be by design, but it was at least not documented.

### PoC

N/A

### Mitigation

Allow to stake to the above two contracts even if `stakeUserRestriction` is set.
