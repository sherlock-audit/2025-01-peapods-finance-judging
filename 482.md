Keen Jetblack Deer

Medium

# LeverageManager removeLeverage feature will often fail due to cached addInterest mechanism for FraxlendPair.


### Summary

LeverageManager removeLeverage feature will often fail due to cached addInterest mechanism for FraxlendPair.

### Root Cause

First we need to understand the workflow of removeLeverage in LeverageManager.

1. Borrow `_borrowAssetAmt` underlying token from flashloan source.
2. Repay these tokens to FraxlendPair.
3. Withdraw collateral, and convert the collateral to underlying token to repay flashloan.

The issue here is in step 2. In Fraxlend, when repaying asset, we need to specify the borrowed share amount instead of the borrowed asset amount. So we need to calculate the amount of shares that `_borrowAssetAmt` corresponds to.

We can see that in `removeLeverage()` function, it first calls the external `FraxlendPair.addInterest()` function to update interest, then use `IFraxlendPair(_lendingPair).totalBorrow().toShares(_borrowAssetAmt, false)` to calculate shares.

The bug here is, the external `FraxlendPair.addInterest()` has a cache mechanism, if the utilization does not change above a threshold, it would not trigger the internal addInterest function. This mechanism is designed to save gas in LendingAssetVault, but it introduces a new bug here. Because the interest is not updated, the cbr would be smaller than expected, and the repay shares would be larger than expected (shares = asset / cbr), so when repaying shares, it would lead to a DoS because we don't have enough asset tokens.

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/lvf/LeverageManager.sol#L1

```solidity
    function removeLeverage(
        uint256 _positionId,
        uint256 _borrowAssetAmt,
        uint256 _collateralAssetRemoveAmt,
        uint256 _podAmtMin,
        uint256 _pairedAssetAmtMin,
        uint256 _podSwapAmtOutMin,
        uint256 _userProvidedDebtAmtMax
    ) external override workflow(true) {

        ...
        address _lendingPair = positionProps[_positionId].lendingPair;
@>      IFraxlendPair(_lendingPair).addInterest(false);

        ...
        // needed to repay flash loaned asset in lending pair
        // before removing collateral and unwinding
        IERC20(_borrowTkn).safeIncreaseAllowance(_lendingPair, _borrowAssetAmt);

        ...
        bytes memory _additionalInfo = abi.encode(
@>          IFraxlendPair(_lendingPair).totalBorrow().toShares(_borrowAssetAmt, false),
            _collateralAssetRemoveAmt,
            _podAmtMin,
            _pairedAssetAmtMin,
            _podSwapAmtOutMin,
            _userProvidedDebtAmtMax
        );
        IFlashLoanSource(_getFlashSource(_positionId)).flash(
            _borrowTkn, _borrowAssetAmt, address(this), abi.encode(_props, _additionalInfo)
        );
    }

    function _removeLeveragePostCallback(bytes memory _userData)
        internal
        returns (uint256 _podAmtRemaining, uint256 _borrowAmtRemaining)
    {
        IFlashLoanSource.FlashData memory _d = abi.decode(_userData, (IFlashLoanSource.FlashData));
        (LeverageFlashProps memory _props, bytes memory _additionalInfo) =
            abi.decode(_d.data, (LeverageFlashProps, bytes));
        (
            uint256 _borrowSharesToRepay,
            uint256 _collateralAssetRemoveAmt,
            uint256 _podAmtMin,
            uint256 _pairedAssetAmtMin,
            uint256 _podSwapAmtOutMin,
            uint256 _userProvidedDebtAmtMax
        ) = abi.decode(_additionalInfo, (uint256, uint256, uint256, uint256, uint256, uint256));

        LeveragePositionProps memory _posProps = positionProps[_props.positionId];

        // allowance increases for _borrowAssetAmt prior to flash loaning asset

        // @audit-bug: This would fail if interest fails to update.
@>      IFraxlendPair(_posProps.lendingPair).repayAsset(_borrowSharesToRepay, _posProps.custodian);
        LeveragePositionCustodian(_posProps.custodian).removeCollateral(
            _posProps.lendingPair, _collateralAssetRemoveAmt, address(this)
        );
        ...
    }
```

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/fraxlend/src/contracts/FraxlendPairCore.sol

```solidity
    function addInterest(bool _returnAccounting)
        external
        nonReentrant
        returns (
            uint256 _interestEarned,
            uint256 _feesAmount,
            uint256 _feesShare,
            CurrentRateInfo memory _currentRateInfo,
            VaultAccount memory _totalAsset,
            VaultAccount memory _totalBorrow
        )
    {
        _currentRateInfo = currentRateInfo;
        // the following checks whether the current utilization rate against the new utilization rate
        // (including external assets available) exceeds a threshold and only updates interest if so.
        // With this enabled, it's obviously possible for there to be some level of "unfair" interest
        // paid by borrowers and/or earned by suppliers, but the idea is this unfairness
        // should theoretically be negligible within some level of error and therefore it won't matter.
        // This is in place to support lower gas & more arbitrage volume through the pair since arbitrage
        // would many times only occur with small changes in asset supply or borrowed positions.
        uint256 _currentUtilizationRate = _prevUtilizationRate;
        uint256 _totalAssetsAvailable = totalAsset.totalAmount(address(externalAssetVault));
        uint256 _newUtilizationRate =
            _totalAssetsAvailable == 0 ? 0 : (UTIL_PREC * totalBorrow.amount) / _totalAssetsAvailable;
        uint256 _rateChange = _newUtilizationRate > _currentUtilizationRate
            ? _newUtilizationRate - _currentUtilizationRate
            : _currentUtilizationRate - _newUtilizationRate;
@>      if (
            _currentUtilizationRate != 0
                && _rateChange < _currentUtilizationRate * minURChangeForExternalAddInterest / UTIL_PREC
        ) {
            emit SkipAddingInterest(_rateChange);
        } else {
            (, _interestEarned, _feesAmount, _feesShare, _currentRateInfo) = _addInterest();
        }
        if (_returnAccounting) {
            _totalAsset = totalAsset;
            _totalBorrow = totalBorrow;
        }
    }
```

### Internal pre-conditions

- When user triggers removeLeverage, the FraxlendPair `addInterest()` is cached and does not actually updates the latest interest.

### External pre-conditions

N/A

### Attack Path

N/A

### Impact

removeLeverage feature will often fail due to not enough repay asset tokens.

### PoC

N/A

### Mitigation

Use `FraxlendPair.convertToShares()` to calculate shares. This function supports previewing interest.