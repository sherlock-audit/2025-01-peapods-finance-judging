Keen Jetblack Deer

Medium

# AutoCompoundingPodLp may not always process pairedLpTKN, leading to stuck rewards.


### Summary

AutoCompoundingPodLp may not always process pairedLpTKN, leading to stuck rewards.

### Root Cause

When autocompounding rewards in AutoCompoundingPodLp.sol, it checks all the tokens return by `getAllRewardsTokens()`. However, this function may not contain pairedLpTKN for the pod.

In TokenRewards, if `LEAVE_AS_PAIRED_LP_TOKEN` was set to false, then pairedLpTKN would not be a valid reward token, so it would not be in the token array returned by `getAllRewardsTokens()`.

It is important to always process pairedLpTKN (go thorugh the `_tokenToPodLp()` function), and by looking at the autocompounding process, this is why:

The autocompounding process is: 1) swap rewardTKN -> pairedLpTKN, 2) swap a portion (roughly half) of pairedLpTKN -> pTKN, 3) add pairedLpTKN, pTKN to UniV2 LP, 4) stake LP token to spTKN.

Now, if step 2 or step 3 failed due to slippage, pairedLpTKN would be leftover in the contract. This should be processed in the next autocompounding phase starting from rewardTKN=pairedLpTKN again. However, if we skip rewardTKN=pairdLpTKN, these pairdLpTKN would be forever stuck in the contract, leading to loss of rewards.

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/AutoCompoundingPodLp.sol#L213

```solidity
    function _processRewardsToPodLp(uint256 _amountLpOutMin, uint256 _deadline) internal returns (uint256 _lpAmtOut) {
        if (!yieldConvEnabled) {
            return _lpAmtOut;
        }
        address[] memory _tokens = ITokenRewards(IStakingPoolToken(_asset()).POOL_REWARDS()).getAllRewardsTokens();
        uint256 _len = _tokens.length + 1;
        for (uint256 _i; _i < _len; _i++) {
            address _token = _i == _tokens.length ? pod.lpRewardsToken() : _tokens[_i];

            // @audit-bug: Anyone can trigger this by donating 1 wei.
@>          uint256 _bal =
                IERC20(_token).balanceOf(address(this)) - (_token == pod.PAIRED_LP_TOKEN() ? _protocolFees : 0);
            if (_bal == 0) {
                continue;
            }
@>          uint256 _newLp = _tokenToPodLp(_token, _bal, 0, _deadline);
            _lpAmtOut += _newLp;
        }
        _totalAssets += _lpAmtOut;
        require(_lpAmtOut >= _amountLpOutMin, "M");
    }

    function _tokenToPodLp(address _token, uint256 _amountIn, uint256 _amountLpOutMin, uint256 _deadline)
        internal
        returns (uint256 _lpAmtOut)
    {
        uint256 _pairedOut = _tokenToPairedLpToken(_token, _amountIn);
        if (_pairedOut > 0) {
            uint256 _pairedFee = (_pairedOut * protocolFee) / 1000;
            if (_pairedFee > 0) {
                _protocolFees += _pairedFee;
                _pairedOut -= _pairedFee;
            }
            _lpAmtOut = _pairedLpTokenToPodLp(_pairedOut, _deadline);
            require(_lpAmtOut >= _amountLpOutMin, "M");
        }
    }

    function _pairedLpTokenToPodLp(uint256 _amountIn, uint256 _deadline) internal returns (uint256 _amountOut) {
        address _pairedLpToken = pod.PAIRED_LP_TOKEN();
        uint256 _pairedSwapAmt = _getSwapAmt(_pairedLpToken, address(pod), _pairedLpToken, _amountIn);
        uint256 _pairedRemaining = _amountIn - _pairedSwapAmt;
        uint256 _minPtknOut;
        if (address(podOracle) != address(0)) {
            // calculate the min out with 5% slippage
            _minPtknOut = (
                podOracle.getPodPerBasePrice() * _pairedSwapAmt * 10 ** IERC20Metadata(address(pod)).decimals() * 95
            ) / 10 ** IERC20Metadata(_pairedLpToken).decimals() / 10 ** 18 / 100;
        }
        IERC20(_pairedLpToken).safeIncreaseAllowance(address(DEX_ADAPTER), _pairedSwapAmt);

        // @audit-note: This may fail due to slippage.
@>      try DEX_ADAPTER.swapV2Single(_pairedLpToken, address(pod), _pairedSwapAmt, _minPtknOut, address(this)) returns (
            uint256 _podAmountOut
        ) {
            // reset here to local balances to accommodate any residual leftover from previous runs
            _podAmountOut = pod.balanceOf(address(this));
            _pairedRemaining = IERC20(_pairedLpToken).balanceOf(address(this)) - _protocolFees;
            IERC20(pod).safeIncreaseAllowance(address(indexUtils), _podAmountOut);
            IERC20(_pairedLpToken).safeIncreaseAllowance(address(indexUtils), _pairedRemaining);

            // @audit-note: This may fail due to slippage.
            try indexUtils.addLPAndStake(
@>              pod, _podAmountOut, _pairedLpToken, _pairedRemaining, _pairedRemaining, lpSlippage, _deadline
            ) returns (uint256 _lpTknOut) {
                _amountOut = _lpTknOut;
            } catch {
                IERC20(pod).safeDecreaseAllowance(address(indexUtils), _podAmountOut);
                IERC20(_pairedLpToken).safeDecreaseAllowance(address(indexUtils), _pairedRemaining);
                emit AddLpAndStakeError(address(pod), _amountIn);
            }
        } catch {
            IERC20(_pairedLpToken).safeDecreaseAllowance(address(DEX_ADAPTER), _pairedSwapAmt);
            emit AddLpAndStakeV2SwapError(_pairedLpToken, address(pod), _pairedRemaining);
        }
    }
```

Note that one may think a "bypass mechanism" is to always add pairedLpTKN to reward whitelist. However, this will introduce more problems. Because pairedLpTKN was added for a lot of pods, attackers can bloat up the `_allRewardsTokens` array of a TokenRewards.sol by depositing 1 wei of all pairedLpTKN, which will cause out-of-gas DoS for reward processing and autocompounding.

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/TokenRewards.sol#L206

```solidity
    function _isValidRewardsToken(address _token) internal view returns (bool) {
        return _token == rewardsToken || REWARDS_WHITELISTER.whitelist(_token);
    }

    function depositRewards(address _token, uint256 _amount) external override {
        _depositRewardsFromToken(_msgSender(), _token, _amount, true);
    }

    function _depositRewardsFromToken(address _user, address _token, uint256 _amount, bool _shouldTransfer) internal {
        require(_amount > 0, "A");
@>      require(_isValidRewardsToken(_token), "V");
        ...
        _depositRewards(_token, _finalAmt);
    }

    function _depositRewards(address _token, uint256 _amountTotal) internal {
        if (_amountTotal == 0) {
            return;
        }
        if (!_depositedRewardsToken[_token]) {
            _depositedRewardsToken[_token] = true;
@>          _allRewardsTokens.push(_token);
        }
        ...
    }
    function getAllRewardsTokens() external view override returns (address[] memory) {
        return _allRewardsTokens;
    }
```


### Internal pre-conditions

- A pod sets `LEAVE_AS_PAIRED_LP_TOKEN=false`.

### External pre-conditions

- Autocompounding fails once due to slippage, leaving pairedLpTKN inside AutoCompoundingPodLP contract.

### Attack Path

No attack path required. Explained above.

### Impact

pairedLpTKN would be stuck in AutoCompoundingPodLP contract, leading to loss of rewards.

### PoC

N/A

### Mitigation

Make sure to always process pairedLpTKN in `_processRewardsToPodLp()` function.
