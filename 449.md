Keen Jetblack Deer

Medium

# FraxlendPair should call `_addInterest()` when admin fees are withdrawed


### Summary

In FraxlendPair, `_addInterest()` should be called before admin fees are withdrawed. The current implementation results in admins collecting fewer fees.

### Root Cause

Admin uses the function `withdrawFees()` to withdraw admin fees, which is in the form of fTKN shares. The number of shares `_shares` is specified. However, since `_addInterest()` is not called, the amount of assets the shares corresponds would be fewer than expected, because the latest interest is not accumulated yet.

- https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/fraxlend/src/contracts/FraxlendPair.sol#L452-L469

```solidity
    function withdrawFees(uint128 _shares, address _recipient) external onlyOwner returns (uint256 _amountToTransfer) {
        if (_recipient == address(0)) revert InvalidReceiver();

        // Grab some data from state to save gas
        VaultAccount memory _totalAsset = totalAsset;

        // Take all available if 0 value passed
        if (_shares == 0) _shares = balanceOf(address(this)).toUint128();

        // We must calculate this before we subtract from _totalAsset or invoke _burn
        _amountToTransfer = _totalAsset.toAmount(_shares, true);

        _approve(address(this), msg.sender, _shares);
        _redeem(_totalAsset, _amountToTransfer.toUint128(), _shares, _recipient, address(this), false);
        uint256 _collateralAmount = userCollateralBalance[address(this)];
        _removeCollateral(_collateralAmount, _recipient, address(this));
        emit WithdrawFees(_shares, _recipient, _amountToTransfer, _collateralAmount);
    }
```

The correct implementation is as the regular `withdraw()` function in [FraxlendPairCore.sol](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/fraxlend/src/contracts/FraxlendPairCore.sol#L823).

```solidity
    function withdraw(uint256 _amount, address _receiver, address _owner)
        external
        nonReentrant
        returns (uint256 _sharesToBurn)
    {
        if (_receiver == address(0)) revert InvalidReceiver();

        // Check if withdraw is paused and revert if necessary
        if (isWithdrawPaused) revert WithdrawPaused();

        // Accrue interest if necessary
@>      _addInterest();

        // Pull from storage to save gas
        VaultAccount memory _totalAsset = totalAsset;

        // Calculate the number of shares to burn based on the amount to withdraw
        _sharesToBurn = _totalAsset.toShares(_amount, true);

        // Execute the withdraw effects
        _redeem(_totalAsset, _amount.toUint128(), _sharesToBurn.toUint128(), _receiver, _owner, false);
    }


```


### Internal pre-conditions

- Admin withdraws fees with function `withdrawFees()`.

### External pre-conditions

N/A

### Attack Path

N/A

### Impact

Admin would end up with less asset token.

### PoC

N/A

### Mitigation

Call `_addInterest()`.
