Silly Maroon Shell

Medium

# An attacker can manipulate token rates by bonding dust amount to `WeightedIndex` when it is configured for multi-assets.

### Summary

Missing check of minimum amount of minted supply will cause manipulated rates of assets to be different from weights which are set by owner as an attacker will manipulate asset rates per unit supply.

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/WeightedIndex.sol#L150



### Root Cause

- In WeightedIndex.sol#bond() function, minimum amount of minted supply is only checked by value which is passed by caller.
```solidity
    function _bond(address _token, uint256 _amount, uint256 _amountMintMin, address _user) internal {
        require(_isTokenInIndex[_token], "IT");
        uint256 _tokenIdx = _fundTokenIdx[_token];

        bool _firstIn = _isFirstIn();
        uint256 _tokenAmtSupplyRatioX96 =
            _firstIn ? FixedPoint96.Q96 : (_amount * FixedPoint96.Q96) / _totalAssets[_token];
        uint256 _tokensMinted;
        if (_firstIn) {
            _tokensMinted = (_amount * FixedPoint96.Q96 * 10 ** decimals()) / indexTokens[_tokenIdx].q1;
        } else {
            _tokensMinted = (_totalSupply * _tokenAmtSupplyRatioX96) / FixedPoint96.Q96;
        }
        uint256 _feeTokens = _canWrapFeeFree(_user) ? 0 : (_tokensMinted * _fees.bond) / DEN;
        require(_tokensMinted - _feeTokens >= _amountMintMin, "M");
        _totalSupply += _tokensMinted;
        _mint(_user, _tokensMinted - _feeTokens);
        if (_feeTokens > 0) {
            _mint(address(this), _feeTokens);
            _processBurnFee(_feeTokens);
        }
        uint256 _il = indexTokens.length;
        for (uint256 _i; _i < _il; _i++) {
            uint256 _transferAmt = _firstIn
                ? getInitialAmount(_token, _amount, indexTokens[_i].token)
                : (_totalAssets[indexTokens[_i].token] * _tokenAmtSupplyRatioX96) / FixedPoint96.Q96;
            require(_transferAmt > 0, "T0");
            _totalAssets[indexTokens[_i].token] += _transferAmt;
            _transferFromAndValidate(IERC20(indexTokens[_i].token), _user, _transferAmt);
        }
        _internalBond();
        emit Bond(_user, _token, _amount, _tokensMinted);
    }
```
So an attacker can manipulate asset rates by bonding dust amount.   

### Internal Pre-conditions

-

### External Pre-conditions

-

### Attack Path

- We say that `WeightedIndex` is configured for two tokens - token1, token2.   
And they all have 18 decimals and their weights are [1, 3].   
Then, index tokens' `q1`s are [1 * Q96 * 1e18, 3 * Q96 * 1e18] by following code snippet.
```solidity
    uint256 _xX96 = (FixedPoint96.Q96 * _totalWeights) / _weights[0]; @audit: _xX96 = Q96 * 4 / 1 = 4 * Q96
    for (uint256 _i; _i < _tl; _i++) {
        indexTokens[_i].q1 = (_weights[_i] * _xX96 * 10 ** IERC20Metadata(_tokens[_i]).decimals()) / _totalWeights;
    }
```
- An attacker bonds 4 wei of token2.   
Then, minted supply is 1 wei by following code.
```solidity
    if (_firstIn) {
@>      _tokensMinted = (_amount * FixedPoint96.Q96 * 10 ** decimals()) / indexTokens[_tokenIdx].q1; @ audit: _tokenIdx = 1 and _tokensMinted = 4 * Q96 * 1e18 / (3 * Q96 * 1e18) = 1.
    } else {
```
And transfered amount of token1 is 1 wei by following code, too.
```solidity
    uint256 _sourceTokenIdx = _fundTokenIdx[_sourceToken]; // @audit: _sourceTokenIds = 1
    uint256 _targetTokenIdx = _fundTokenIdx[_targetToken]; // @audit: _targetTokenIdx = 0
    return (_sourceAmount * indexTokens[_targetTokenIdx].weighting * 10 ** IERC20Metadata(_targetToken).decimals())
        / indexTokens[_sourceTokenIdx].weighting / 10 ** IERC20Metadata(_sourceToken).decimals(); // @audit: 4 * 1 * 1e18 / 3 / 1e18 = 1
```
- So bonded assets are [1 wei, 1 wei] and totalSupply becomes 1 wei.
- After that, users must bond with wrong asset rates(1:1).
```solidity
    function _bond(address _token, uint256 _amount, uint256 _amountMintMin, address _user) internal {
        require(_isTokenInIndex[_token], "IT");
        uint256 _tokenIdx = _fundTokenIdx[_token];

        bool _firstIn = _isFirstIn();
        uint256 _tokenAmtSupplyRatioX96 =
@>          _firstIn ? FixedPoint96.Q96 : (_amount * FixedPoint96.Q96) / _totalAssets[_token]; @audit: _totalAssets[token] = 1 wei and _tokenAmtSupplyRatioX96 = _amount * Q96.
        uint256 _tokensMinted;
        if (_firstIn) {
            _tokensMinted = (_amount * FixedPoint96.Q96 * 10 ** decimals()) / indexTokens[_tokenIdx].q1;
        } else {
@>          _tokensMinted = (_totalSupply * _tokenAmtSupplyRatioX96) / FixedPoint96.Q96;  @audit: 1 * _amount * Q96 / Q96 = _amount
        }
        uint256 _feeTokens = _canWrapFeeFree(_user) ? 0 : (_tokensMinted * _fees.bond) / DEN;
        require(_tokensMinted - _feeTokens >= _amountMintMin, "M");
        _totalSupply += _tokensMinted;
        _mint(_user, _tokensMinted - _feeTokens);
        if (_feeTokens > 0) {
            _mint(address(this), _feeTokens);
            _processBurnFee(_feeTokens);
        }
        uint256 _il = indexTokens.length;
        for (uint256 _i; _i < _il; _i++) {
            uint256 _transferAmt = _firstIn
                ? getInitialAmount(_token, _amount, indexTokens[_i].token)
@>              : (_totalAssets[indexTokens[_i].token] * _tokenAmtSupplyRatioX96) / FixedPoint96.Q96; // @audit: 1 * _amount * Q96 / Q96 = _amount
            require(_transferAmt > 0, "T0");
            _totalAssets[indexTokens[_i].token] += _transferAmt;
            _transferFromAndValidate(IERC20(indexTokens[_i].token), _user, _transferAmt);
        }
        _internalBond();
        emit Bond(_user, _token, _amount, _tokensMinted);
    }
```
As we can see above, from second bonding users have to bond by wrong asset rates.   
At this time, `_amountMintMin` for slippage check does not interact in case of bonding first asset because its weight is `1`.

### Impact

Protocol and users suffer from wrong asset rates manipulated by attacker.


### PoC

_No response_

### Mitigation

Add a check of minimum total supply so that an attacker cannot manipulate asset rates by rounding error.   
For example, we can do it as follows.
```solidity
++  const uint256 MINIMUM_SUPPLY = 1e9;
    function _bond(address _token, uint256 _amount, uint256 _amountMintMin, address _user) internal {
        require(_isTokenInIndex[_token], "IT");
        ...
        _totalSupply += _tokensMinted;
        ...
        _internalBond();

++      require(_totalSupply >= MINIMUM_SUPPLY);

        emit Bond(_user, _token, _amount, _tokensMinted);
    }

    /// @notice The ```debond``` function unwraps a user out of a pod and burns pTKN
    /// @param _amount Number of pTKN to burn
    function debond(uint256 _amount, address[] memory, uint8[] memory) external override lock noSwapOrFee {
        ...
        _totalSupply -= _amountAfterFee;
        ...

++      require(_totalSupply == 0 || _totalSupply >= MINIMUM_SUPPLY);

        emit Debond(_msgSender(), _amount);
    }
```