Nutty Steel Sealion

Medium

# Users may lose their rewards when a reward token is paused

### Summary

When a wallet's shares are updated, the `excluded` value for all tokens is reset. However, rewards for paused tokens are skipped during this process. As a result, if a token is paused when a wallet's shares are updated, users may lose rewards, as their `excluded` value will be updated without actually receiving the rewards for the paused token.

### Root Cause

The issue occurs during the update of a wallet's shares in [`TokenRewards.sol`](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/TokenRewards.sol#L236-L263).

When shares are updated, the process first distributes rewards, if needed, and then resets the `excluded` values for all tokens.

```solidity
function _addShares(address _wallet, uint256 _amount) internal {
    if (shares[_wallet] > 0) {
        _distributeReward(_wallet);
    }
    totalShares += _amount;
    shares[_wallet] += _amount;
    _resetExcluded(_wallet);
}

function _removeShares(address _wallet, uint256 _amount) internal {
    require(shares[_wallet] > 0 && _amount <= shares[_wallet], "RE");
    _distributeReward(_wallet);
    totalShares -= _amount;
    shares[_wallet] -= _amount;
    _resetExcluded(_wallet);
}
```

However, when distributing rewards, paused tokens are skipped, meaning no rewards are given for those tokens. 

```solidity
function _distributeReward(address _wallet) internal {
    if (shares[_wallet] == 0) return;

    for (uint256 _i; _i < _allRewardsTokens.length; _i++) {
        address _token = _allRewardsTokens[_i];
        
        if (REWARDS_WHITELISTER.paused(_token)) {
            continue;
        }
        ...
    }
}
```

On the other hand, the `excluded` values are updated for all tokens, including paused ones.

```solidity
function _resetExcluded(address _wallet) internal {
    for (uint256 _i; _i < _allRewardsTokens.length; _i++) {
        address _token = _allRewardsTokens[_i];
        rewards[_token][_wallet].excluded = _cumulativeRewards(_token, shares[_wallet], true);
    }
}
```

### Internal Pre-conditions

A reward token is paused.

### External Pre-conditions

N/A

### Attack Path

1. Users stake LP tokens and receive reward shares.
2. Rewards are distributed to stakers.
3. The reward token is paused.
4. A userâ€™s balance is updated for any reason, such as:
   - Staking, unstaking, or transferring tokens.
   - A malicious actor triggering a zero-value transfer of staked tokens to a victim address (zero transfers are allowed but will still trigger a share update).
5. When the reward token is unpaused, users whose balances were updated during the paused state will lose their rewards.

### Impact

This issue leads to users losing rewards for paused tokens. When a token is paused, its rewards are skipped, but the `excluded` value is still updated. As a result, users do not receive the correct rewards once the token is unpaused, and the previously accumulated rewards for that token are effectively lost.

### PoC

Add a test to `TokenRewards.t.sol`:

```solidity
function test_RewardLostWhenTokenPaused() public {
    rewardsWhitelister.setWhitelist(address(rewardsToken), true);

    vm.prank(address(trackingToken));
    tokenRewards.setShares(user1, 10e18, false);

    tokenRewards.depositRewards(address(rewardsToken), 100e18);

    rewardsWhitelister.setPaused(true);

    vm.prank(address(trackingToken));
    tokenRewards.setShares(user1, 0, false);

    rewardsWhitelister.setPaused(false);

    tokenRewards.claimReward(user1);
    assertEq( rewardsToken.balanceOf(user1), 0);
}
```

### Mitigation

Store unpaid rewards as debt for users when a token is paused, and distribute them once the token is unpaused. This way, the `excluded` values can still be updated when shares change.