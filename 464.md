Keen Jetblack Deer

Medium

# pairedLpTKN as pTKN with non-zero transferTax is not supported in in multiple contracts.


### Summary

pairedLpTKN as pTKN with non-zero transferTax is not supported in multiple contracts: 1) DecentralizedIndex.sol, 2) FraxlendPair.sol, 3) LeverageManager.sol

### Root Cause

First we should know that setting pTKN as pairedLpTKN is a valid use case. We can find on https://peapods.finance/app that pPEAS sets pOHM as it's pairedLpTKN.

pTKN may have transfer tax enabled, which makes it a fee-on-transfer token. Though fee-on-transfer tokens aren't supported for underlying tokens for pods, the pod itself (pTKN) should always be handled for fee-on-transfer cases, since this is by design.

```solidity
    function _update(address _from, address _to, uint256 _amount) internal override {
        ...
        uint256 _fee;
        if (_swapping == 0 && _swapAndFeeOn == 1) {
            ...
@>          } else if (!_buy && !_sell && _config.hasTransferTax) {
                _fee = _amount / 10000; // 0.01%
                _fee = _fee == 0 && _amount > 0 ? 1 : _fee;
                super._update(_from, address(this), _fee);
            }
        }
        ...
    }
```

There are multiple areas that does not correctly handle pairedLpTKN as a fee-on-transfer token.

1. DecentralizedIndex.sol

In the `addLiquidityV2()` function, the same amount `_pairedLPTokens` is transferred from user to DecentralizedIndex.sol contract, and from DecentralizedIndex.sol contract to DEX_HANDLER, then to UniswapRouter. The bug here is simple, for a fee-on-transfer pTKN as pairedLpTKN, this would always fail.

Now, as for why `DecentralizedIndex.sol#addLiquidityV2` must be used to add liquidity. This is because this is the only function that disables swap fees when adding liquidity (only disables the fee for main pTKN, not for the pairedLpTKN that is also pTKN). This function is also used by LVF feature in AutoCompoundingPodLp where reward token is used to convert to LP tokens and spTKN.

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/DecentralizedIndex.sol#L332

```solidity
    function addLiquidityV2(
        uint256 _pTKNLPTokens,
        uint256 _pairedLPTokens,
        uint256 _slippage, // 100 == 10%, 1000 == 100%
        uint256 _deadline
    ) external override lock noSwapOrFee returns (uint256) {
        uint256 _idxTokensBefore = balanceOf(address(this));
        uint256 _pairedBefore = IERC20(PAIRED_LP_TOKEN).balanceOf(address(this));

        super._update(_msgSender(), address(this), _pTKNLPTokens);
        _approve(address(this), address(DEX_HANDLER), _pTKNLPTokens);

@>      IERC20(PAIRED_LP_TOKEN).safeTransferFrom(_msgSender(), address(this), _pairedLPTokens);
@>      IERC20(PAIRED_LP_TOKEN).safeIncreaseAllowance(address(DEX_HANDLER), _pairedLPTokens);

        uint256 _poolBalBefore = IERC20(DEX_HANDLER.getV2Pool(address(this), PAIRED_LP_TOKEN)).balanceOf(_msgSender());
        DEX_HANDLER.addLiquidity(
            address(this),
            PAIRED_LP_TOKEN,
            _pTKNLPTokens,
@>          _pairedLPTokens,
            (_pTKNLPTokens * (1000 - _slippage)) / 1000,
            (_pairedLPTokens * (1000 - _slippage)) / 1000,
            _msgSender(),
            _deadline
        );
        IERC20(PAIRED_LP_TOKEN).safeIncreaseAllowance(address(DEX_HANDLER), 0);

        // check & refund excess tokens from LPing
        if (balanceOf(address(this)) > _idxTokensBefore) {
            super._update(address(this), _msgSender(), balanceOf(address(this)) - _idxTokensBefore);
        }
        if (IERC20(PAIRED_LP_TOKEN).balanceOf(address(this)) > _pairedBefore) {
            IERC20(PAIRED_LP_TOKEN).safeTransfer(
                _msgSender(), IERC20(PAIRED_LP_TOKEN).balanceOf(address(this)) - _pairedBefore
            );
        }
        emit AddLiquidity(_msgSender(), _pTKNLPTokens, _pairedLPTokens);
        return IERC20(DEX_HANDLER.getV2Pool(address(this), PAIRED_LP_TOKEN)).balanceOf(_msgSender()) - _poolBalBefore;
    }
```

2. LeverageManager.sol

If pairedLpTKN is a pTKN (e.g. pOHM), it will be flashloaned whenever increasing or decreasing leverage. However, in flashloans, it always expect the exact amount of tokens to be transferred, which is not the case for fee-on-transfer tokens.

3. FraxlendPair.sol

In LVF feature, if pairedLpTKN is a pTKN (e.g. pOHM), it would have it's own lending market. However, FraxlendPair does not support fee-on-transfer for basic deposit/withdraws.

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/fraxlend/src/contracts/FraxlendPairCore.sol#L578

```solidity
    function _deposit(
        VaultAccount memory _totalAsset,
        uint128 _amount,
        uint128 _shares,
        address _receiver,
        bool _shouldTransfer
    ) internal {
        // Effects: bookkeeping
@>      _totalAsset.amount += _amount;
        _totalAsset.shares += _shares;

        // Effects: write back to storage
        _mint(_receiver, _shares);
        totalAsset = _totalAsset;

        // Interactions
        if (_shouldTransfer) {
@>          assetContract.safeTransferFrom(msg.sender, address(this), _amount);
            ..
        }
        ...
    }
```

### Internal pre-conditions

Pod uses a fee-on-transfer pTKN as pairedLpTKN.

### External pre-conditions

N/A

### Attack Path

N/A

### Impact

1. Liquidity cannot be added by `addLiquidity()`, and the LVF feature would break as well because AutoCompoundingPodLp will fail.
2. LeverageManager and Fraxlend won't work correctly.

### PoC

N/A

### Mitigation

1. Support FoT for the above places.
2. Consider disabling transfer tax.
