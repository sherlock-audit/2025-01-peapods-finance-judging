Nutty Steel Sealion

High

# Pods DoS if no protocol fees are set

### Summary

If the protocol renounces fees, it causes `TokenRewards::depositFromPairedLpToken` to revert for Pods with the `LEAVE_AS_PAIRED_LP_TOKEN` option enabled. The `depositFromPairedLpToken` function is triggered during the Pod swap fee process, affecting nearly all essential operations such as debonding, transfers, and providing liquidity.  

### Root Cause

In [`TokenRewards.sol:286`](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/TokenRewards.sol#L286), we see that there may be no protocol fees, which is why the `_getYieldFees` function first checks `protocolFees` for the zero address before retrieving the fee values. In addition to admin and burn fee values, [`ProtocolFees.sol:8`](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/ProtocolFees.sol#L8) has the `DEN` view function representing fee precision. In most cases, a call to this function is made after ensuring the fee amount it not zero, indicating that `protocolFees` is properly set.

```solidity
function _getAdminFeeFromAmount(uint256 _amount) internal view returns (uint256) {
    (uint256 _yieldAdminFee,) = _getYieldFees();
    if (_yieldAdminFee == 0) {
        return 0;
    }
    return (_amount * _yieldAdminFee) / PROTOCOL_FEE_ROUTER.protocolFees().DEN();
}
```
However, when the `LEAVE_AS_PAIRED_LP_TOKEN` option is enabled, the check is [missing](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/TokenRewards.sol#L154), and the function directly calls `PROTOCOL_FEE_ROUTER.protocolFees().DEN()` without ensuring that the `protocolFees` is set.

```solidity
if (LEAVE_AS_PAIRED_LP_TOKEN) {
    (, uint256 _yieldBurnFee) = _getYieldFees();
    uint256 _burnAmount = (_amountTkn * _yieldBurnFee) / PROTOCOL_FEE_ROUTER.protocolFees().DEN();
    ...
}
```

If the protocol renounces fees and sets the `protocolFee` address to `address(0)`, this call will revert.


### Internal Pre-conditions

- Protocol fees are not set.
- The `LEAVE_AS_PAIRED_LP_TOKEN` option is enabled.


### External Pre-conditions

N/A

### Attack Path

1. A Pod with the `LEAVE_AS_PAIRED_LP_TOKEN` option is created.  
2. Users bond their funds.  
3. The protocol decides to renounce fees, sets the `protocolFee` address to `0`, and renounces ownership of the `ProtocolFeeRouter` contract.  
4. The Pod experiences a DoS, and users are unable to debond their funds.

### Impact

This issue causes a DoS for all Pods with the `LEAVE_AS_PAIRED_LP_TOKEN` option enabled. If fee renouncement is accompanied by the renouncement of ownership of the `ProtocolFeeRouter` contract, the funds become irrecoverable, resulting in users losing their funds.

### PoC

1. Add imports to `WeightedIndex.t.sol`:

```solidity
import {ProtocolFeeRouter} from "../contracts/ProtocolFeeRouter.sol";
import {IProtocolFees} from "../contracts/interfaces/IProtocolFees.sol";
```

2. Add a test to `WeightedIndex.t.sol`:

```solidity
function test_ProtocolFeeRenouncement() public {
    address protocolFeeRouter = 0x7d544DD34ABbE24C8832db27820Ff53C151e949b;

    bytes memory immutables = abi.encode(
        dai,
        address(peas),
        0x6B175474E89094C44Da98b954EedeAC495271d0F,
        protocolFeeRouter,
        rewardsWhitelist,
        0x024ff47D552cB222b265D68C7aeB26E586D5229D,
        dexAdapter
    );

    IDecentralizedIndex.Config memory _c;
    IDecentralizedIndex.Fees memory _f;
    _f.bond = fee;
    _f.debond = fee;
    address[] memory _t = new address[](1);
    _t[0] = address(peas);
    uint256[] memory _w = new uint256[](1);
    _w[0] = 100;
    address _pod = _createPod(
        "Test",
        "pTEST",
        _c,
        _f,
        _t,
        _w,
        address(0),
        true,
        immutables
    );
    pod = WeightedIndex(payable(_pod));

    vm.startPrank(alice);
    peas.approve(address(pod), type(uint256).max);
    pod.bond(address(peas), bondAmt, 0);
    vm.stopPrank();

    vm.startPrank(bob);
    peas.approve(address(pod), type(uint256).max);
    pod.bond(address(peas), bondAmt, 0);
    vm.stopPrank();

    uint256 podTokensToAdd = 1e18;
    uint256 pairedTokensToAdd = 1e18;
    uint256 slippage = 50;

    deal(pod.PAIRED_LP_TOKEN(), alice, pairedTokensToAdd);
    vm.startPrank(alice);
    IERC20(pod.PAIRED_LP_TOKEN()).approve(address(pod), pairedTokensToAdd);
    uint256 lpTokensReceived = pod.addLiquidityV2(podTokensToAdd, pairedTokensToAdd, slippage, block.timestamp);

    address protocolFeeRouterOwner = ProtocolFeeRouter(protocolFeeRouter).owner();

    vm.startPrank(protocolFeeRouterOwner);
    ProtocolFeeRouter(protocolFeeRouter).setProtocolFees(IProtocolFees(address(0)));
    ProtocolFeeRouter(protocolFeeRouter).renounceOwnership();
    vm.stopPrank();

    vm.startPrank(bob);
    address[] memory _n1;
    uint8[] memory _n2;
    vm.expectRevert(bytes(""));
    pod.debond(bondAmtAfterFee, _n1, _n2);
    vm.stopPrank();
}
```

3. Run the test.

### Mitigation

```diff
         if (LEAVE_AS_PAIRED_LP_TOKEN) {
             (, uint256 _yieldBurnFee) = _getYieldFees();
-            uint256 _burnAmount = (_amountTkn * _yieldBurnFee) / PROTOCOL_FEE_ROUTER.protocolFees().DEN();
+            uint256 _burnAmount = 0;
+            if (_yieldBurnFee > 0) {
+                (_amountTkn * _yieldBurnFee) / PROTOCOL_FEE_ROUTER.protocolFees().DEN();
+            }
             _adminAmt += _burnAmount;
             _amountTkn -= _burnAmount;
             if (_adminAmt > 0) {
```