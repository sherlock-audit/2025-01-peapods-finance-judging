Keen Jetblack Deer

Medium

# FraxlendPair external `addInterest()` update logic is flawed, leading to inaccurate interest for LVA.


### Summary

FraxlendPair external `addInterest()` update logic is flawed, leading to inaccurate interest for LVA.

### Root Cause

First we need to understand how LVA triggers FraxlendPair's external addInterest function.

Inside FraxlendPairCore, a `_prevUtilizationRate` variable is used to maintain the utilization rate (borrowedAsset/totalAsset ratio). When LVA triggers an external `addInterest()`, it first checks the whether the last utilization rate has changed against the new utilization rate. If the change is large enough, the internal `_addInterest()` is trigged.

A typical callstack is `LendingAssetVault#withdraw` -> `LendingAssetVault#_updateInterestAndMdInAllVaults` -> `FraxlendPair#addInterest` -> `FraxlendPair#_addInterest`.

The bug here is the utilization rate diff does not work if there was no interaction with FraxlendPair/LVA for a period of time.

For example:

1. At timestamp T0, Fraxlend `_addInterest` is triggered.
2. At timestamp T1, Fraxlend `_addInterest` is triggered again. `_prevUtilizationRate` is now updated to utilization at T0.
3. For a long time, there is no interaction to FraxlendPair and LVA.
4. At timestamp T2, a user calls `LendingAssetVault#withdraw()` to withdraw shares.

Since the time period from T1->T2 is long, the interest accrued is also large, so when withdrawing shares, user should benefit from the interest between period T1->T2. However, in `addInterest()`, the new utilization `_newUtilizationRate` calculated is still the same as `_prevUtilizationRate`, because interest was not updated at all (also because there has been no interaction with LVA, the external asset also didn't change). This means the interest is not updated, and the user would not get the interest, effectively resulting in loss of funds.

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/LendingAssetVault.sol

```solidity
    function withdraw(uint256 _assets, address _receiver, address _owner) external override returns (uint256 _shares) {
        _updateInterestAndMdInAllVaults(address(0));
        _shares = convertToShares(_assets);
        _withdraw(_shares, _assets, _owner, _msgSender(), _receiver);
    }

    function _updateInterestAndMdInAllVaults(address _vaultToExclude) internal {
        uint256 _l = _vaultWhitelistAry.length;
        for (uint256 _i; _i < _l; _i++) {
            address _vault = _vaultWhitelistAry[_i];
            if (_vault == _vaultToExclude) {
                continue;
            }
            (uint256 _interestEarned,,,,,) = IFraxlendPair(_vault).addInterest(false);
            if (_interestEarned > 0) {
                _updateAssetMetadataFromVault(_vault);
            }
        }
    }
```

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/fraxlend/src/contracts/FraxlendPairCore.sol#L286

```solidity
    function addInterest(bool _returnAccounting)
        external
        nonReentrant
        returns (
            uint256 _interestEarned,
            uint256 _feesAmount,
            uint256 _feesShare,
            CurrentRateInfo memory _currentRateInfo,
            VaultAccount memory _totalAsset,
            VaultAccount memory _totalBorrow
        )
    {
        uint256 _currentUtilizationRate = _prevUtilizationRate;
        uint256 _totalAssetsAvailable = totalAsset.totalAmount(address(externalAssetVault));
@>      uint256 _newUtilizationRate =
            _totalAssetsAvailable == 0 ? 0 : (UTIL_PREC * totalBorrow.amount) / _totalAssetsAvailable;
        uint256 _rateChange = _newUtilizationRate > _currentUtilizationRate
            ? _newUtilizationRate - _currentUtilizationRate
            : _currentUtilizationRate - _newUtilizationRate;
        if (
            _currentUtilizationRate != 0
                && _rateChange < _currentUtilizationRate * minURChangeForExternalAddInterest / UTIL_PREC
        ) {
            emit SkipAddingInterest(_rateChange);
        } else {
            (, _interestEarned, _feesAmount, _feesShare, _currentRateInfo) = _addInterest();
        }
        if (_returnAccounting) {
            _totalAsset = totalAsset;
            _totalBorrow = totalBorrow;
        }
        ...
    }

    function _addInterest()
        internal
        returns (
            bool _isInterestUpdated,
            uint256 _interestEarned,
            uint256 _feesAmount,
            uint256 _feesShare,
            CurrentRateInfo memory _currentRateInfo
        )
    {
        // Pull from storage and set default return values
        _currentRateInfo = currentRateInfo;

        // store the current utilization rate as previous for next check
        uint256 _totalAssetsAvailable = _totalAssetAvailable(totalAsset, totalBorrow, true);
@>      _prevUtilizationRate = _totalAssetsAvailable == 0 ? 0 : (UTIL_PREC * totalBorrow.amount) / _totalAssetsAvailable;
        ...
    }
```

### Internal pre-conditions

- No interaction with FraxlendPair and LVA for an enough period of time.

### External pre-conditions

N/A

### Attack Path

N/A

### Impact

During the period where no interactions was made to FraxlendPair and LVA, if users deposit/withdraw to LVA, the interest in Fraxlend would not be updated. This may lead to:

1. When users are withdrawing shares, he would lose interest.
2. When users are depositing assets, he can gain more shares since asset/share ratio is not updated.

### PoC

N/A

### Mitigation

Add a time-based update mechanism in FraxlendPair's external `addInterest()`, e.g. update every 1 minute, regardless of how utilization rate changes.