Bumpy Hemp Cuckoo

High

# `DecentralizedIndex.sol#_update()` recursive calls will cause excessive fee application


## Summary
A recursive call pattern in the `_update()` function will cause excessive fee charges for users as the protocol will recursively apply fees during token transfers, buys and sells.

## Root Cause
In [`DecentralizedIndex.sol:L180`](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/DecentralizedIndex.sol#L180), the `_update()` function triggers recursive calls through the fee processing flow:

```solidity
    function _update(address _from, address _to, uint256 _amount) internal override {
        require(!_blacklist[_to], "BK");
        bool _buy = _from == V2_POOL && _to != V2_ROUTER;
        bool _sell = _to == V2_POOL;
        uint256 _fee;
        if (_swapping == 0 && _swapAndFeeOn == 1) {
            if (_from != V2_POOL) {
                _processPreSwapFeesAndSwap();
            }
            if (_buy && _fees.buy > 0) {
                _fee = (_amount * _fees.buy) / DEN;
                super._update(_from, address(this), _fee);
            } else if (_sell && _fees.sell > 0) {
                _fee = (_amount * _fees.sell) / DEN;
                super._update(_from, address(this), _fee);
            } else if (!_buy && !_sell && _config.hasTransferTax) {
                _fee = _amount / 10000; // 0.01%
->              _fee = _fee == 0 && _amount > 0 ? 1 : _fee;
                super._update(_from, address(this), _fee);
            }
        }
180->      _processBurnFee(_fee);
        super._update(_from, _to, _amount - _fee);
    }
```

The recursion occurs through `_processBurnFee()`:

```solidity
    function _processBurnFee(uint256 _amtToProcess) internal {
        if (_amtToProcess == 0 || _fees.burn == 0) {
            return;
        }
        uint256 _burnAmt = (_amtToProcess * _fees.burn) / DEN;
        _totalSupply -= _burnAmt;
->      _burn(address(this), _burnAmt); // reentrant to _update()
    }
```

## Internal pre-conditions
1. `_swapping` must be `0`
2. `_swapAndFeeOn` must be `1` 
3. Fee must be greater than `0`

## Impact
Users will pay significantly higher fees than intended due to recursive fee application. In transfer tax scenarios, transactions may consistently revert due to the minimum fee logic preventing fee from reaching zero, creating an infinite recursion.

## Mitigation
Add reentrancy protection to `_update()`:

```solidity
    function _update(address _from, address _to, uint256 _amount) internal override {
        require(!_blacklist[_to], "BK");
        bool _buy = _from == V2_POOL && _to != V2_ROUTER;
        bool _sell = _to == V2_POOL;
        uint256 _fee;
-       if (_swapping == 0 && _swapAndFeeOn == 1) {
+       if (_swapping == 0 && _swapAndFeeOn == 1 && !update_entered) {
            if (_from != V2_POOL) {
                _processPreSwapFeesAndSwap();
            }
            if (_buy && _fees.buy > 0) {
                _fee = (_amount * _fees.buy) / DEN;
                super._update(_from, address(this), _fee);
            } else if (_sell && _fees.sell > 0) {
                _fee = (_amount * _fees.sell) / DEN;
                super._update(_from, address(this), _fee);
            } else if (!_buy && !_sell && _config.hasTransferTax) {
                _fee = _amount / 10000; // 0.01%
                _fee = _fee == 0 && _amount > 0 ? 1 : _fee;
                super._update(_from, address(this), _fee);
            }
        }
+       update_entered = true;
        _processBurnFee(_fee);
+       update_entered = false;
        super._update(_from, _to, _amount - _fee);
    }
```
