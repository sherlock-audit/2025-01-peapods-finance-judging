Brilliant Fiery Sheep

Medium

# `_processRewardsToPodLp` will be DOSed when the `lpRewardsToken` is added to the `_allRewardsTokens` array

### Summary

The `AutoCompoundingPodLp._processRewardsToPodLp` has a check that will process the `lpRewardsToken` after all other reward tokens have been looped through. This however ignores the fact that the `lpRewardsToken` could also be part of the `_allRewardsTokens` array which would leading to an attempt at processing the `lpRewardsToken` twice leading to a denial of service caused by the function reverting.

### Root Cause

The `AutoCompoundingPodLp._processRewardsToPodLp` has a check that will process the `lpRewardsToken` after all other reward tokens have been looped through:

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/AutoCompoundingPodLp.sol#L213-L231

```solidity
    function _processRewardsToPodLp(uint256 _amountLpOutMin, uint256 _deadline) internal returns (uint256 _lpAmtOut) {
        if (!yieldConvEnabled) {
            return _lpAmtOut;
        }
        address[] memory _tokens = ITokenRewards(IStakingPoolToken(_asset()).POOL_REWARDS()).getAllRewardsTokens();
        uint256 _len = _tokens.length + 1;
        for (uint256 _i; _i < _len; _i++) {
            address _token = _i == _tokens.length ? pod.lpRewardsToken() : _tokens[_i];
            uint256 _bal =
                IERC20(_token).balanceOf(address(this)) - (_token == pod.PAIRED_LP_TOKEN() ? _protocolFees : 0);
            if (_bal == 0) {
                continue;
            }
            uint256 _newLp = _tokenToPodLp(_token, _bal, 0, _deadline);
            _lpAmtOut += _newLp;
        }
        _totalAssets += _lpAmtOut;
        require(_lpAmtOut >= _amountLpOutMin, "M");
    }
```

As can be seen there is an assumption that the `lpRewardsToken` will only be processed after all other tokens are looped through:

```solidity
 address _token = _i == _tokens.length ? pod.lpRewardsToken() : _tokens[_i];
```

The issue is that anyone can add `lpRewardsToken` to the `_allRewardsTokens` array through the `TokenRewards.depositRewards` function which is permissionless:

```solidity
    function depositRewards(address _token, uint256 _amount) external override {
        _depositRewardsFromToken(_msgSender(), _token, _amount, true);
    }
```

This will cause it to be added to the array:

```solidity
    function _depositRewardsFromToken(address _user, address _token, uint256 _amount, bool _shouldTransfer) internal {
        require(_amount > 0, "A");
        require(_isValidRewardsToken(_token), "V");
        uint256 _finalAmt = _amount;
        if (_shouldTransfer) {
            uint256 _balBefore = IERC20(_token).balanceOf(address(this));
            IERC20(_token).safeTransferFrom(_user, address(this), _finalAmt);
            _finalAmt = IERC20(_token).balanceOf(address(this)) - _balBefore;
        }
        uint256 _adminAmt = _getAdminFeeFromAmount(_finalAmt);
        if (_adminAmt > 0) {
            IERC20(_token).safeTransfer(OwnableUpgradeable(address(V3_TWAP_UTILS)).owner(), _adminAmt);
            _finalAmt -= _adminAmt;
        }
        _depositRewards(_token, _finalAmt);
    }


    function _depositRewards(address _token, uint256 _amountTotal) internal {
        if (_amountTotal == 0) {
            return;
        }
        if (!_depositedRewardsToken[_token]) {
            _depositedRewardsToken[_token] = true;
            _allRewardsTokens.push(_token);
        }
```

Note that the check `_isValidRewardsToken` will not prevent it being added:

```solidity
    function _isValidRewardsToken(address _token) internal view returns (bool) {
        return _token == rewardsToken || REWARDS_WHITELISTER.whitelist(_token);
    }
```

This will lead to the `lpRewardsToken` attempting to be swapped out twice in `_tokenToPairedLpToken` as part of the `_processRewardsToPodLp` process leading to reverts due to inadequate balance.

### Internal Pre-conditions

1. Someone deposits a small amount of `lpRewardsToken`

### External Pre-conditions

None

### Attack Path

1. Someone deposits a small amount of `lpRewardsToken`
2. `lpRewardsToken` is added to the `_allRewardsTokens` array as part of the deposit process
3. `_processRewardsToPodLp` is called at some point e.g when a deposit or withdrawal is being done
4. The function reverts because the `lpRewardsToken` will be processed twice leading to a revert when the token is attempted to be swapped the second time.

### Impact

Multiple functions are DOSed including deposits and withdrawals.

### PoC

_No response_

### Mitigation

A check can be added in `AutoCompoundingPodLp._processRewardsToPodLp` to skip the `lpRewardsToken` if it is part of the `_allRewardsTokens` array.