Fast Khaki Raccoon

High

# `receiveFlashLoan` does not save the balancer fee, which would cause the TX to revert

### Summary

`receiveFlashLoan` does not save the balancer fee, causing the TX to revert and making the whole balancer flash module unusable.

### Root Cause

`receiveFlashLoan` will save the fee locally (i.e. only in this function), but would not override `_userData`  when passing the data to `IFlashLoanRecipient` with `callback`, which means that the actual parameter `_fData.fee` will be 0.

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/flash/BalancerFlashSource.sol#L47-L57
```solidity
    function receiveFlashLoan(IERC20[] memory, uint256[] memory, uint256[] memory _feeAmounts, bytes memory _userData)
        external
        override
        workflow(false)
    {
        require(_msgSender() == source, "CBV");
        FlashData memory _fData = abi.decode(_userData, (FlashData));
        _fData.fee = _feeAmounts[0];

        IERC20(_fData.token).safeTransfer(_fData.recipient, _fData.amount);
        IFlashLoanRecipient(_fData.recipient).callback(_userData);
    }
```

Since we know that `_d.fee` would be 0 we would also find that `_flashPaybackAmt` won't be sufficient to pay the whole flash loan (as it's missing the fee), which would cause the TX to revert.

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/lvf/LeverageManager.sol#L328-L342
```solidity
        uint256 _flashPaybackAmt = _d.amount + _d.fee;
        uint256 _borrowAmt = _overrideBorrowAmt > _flashPaybackAmt 
            ? _overrideBorrowAmt 
            : _flashPaybackAmt;

        // IFraxlendPair(positionProps[_positionId].lendingPair).collateralContract()
        address _aspTkn = _getAspTkn(_props.positionId);
        IERC20(_aspTkn).safeTransfer(positionProps[_props.positionId].custodian, _aspTknCollateralBal);

        LeveragePositionCustodian(positionProps[_props.positionId].custodian).borrowAsset(
            positionProps[_props.positionId].lendingPair, _borrowAmt, _aspTknCollateralBal, address(this)
        );

        // pay back flash loan and send remaining to borrower
        IERC20(_d.token).safeTransfer(IFlashLoanSource(_getFlashSource(_props.positionId)).source(), _flashPaybackAmt);
        uint256 _remaining = IERC20(_d.token).balanceOf(address(this));
```

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

There is no attack path, just the balancer flash module being unusable.

### Impact

Not storing the fee and continuing the call will result in it not being paid, which would revert the whole TX, thus making the balancer flash module unusable.

### PoC

The bellow code can be ran in remix to test the functionality of the bug
```solidity
// SPDX-License-Identifier: GPL-3.0

pragma solidity >=0.8 <0.9.0;

import "hardhat/console.sol";

contract ShadowFactory {
    struct FlashData {
        address some;
        uint256 fee;
        uint256[] arr;
    }
    function encoder() public pure returns (bytes memory) {
        uint256[] memory arr = new uint256[](2);
        arr[0] = 3;
        arr[1] = 8;
        FlashData memory data = FlashData(address(32), 3, arr);
        return abi.encode(data);
    }

    function tryItOut(bytes memory userData) external pure {
         FlashData memory flash = abi.decode(userData, (FlashData));
         flash.fee = 8;
         someOther(userData);
    }

    function someOther(bytes memory data) public pure {
        FlashData memory flash = abi.decode(data, (FlashData));
        console.log(flash.fee);
    }
}
```

### Mitigation

Make sure the fee is saved, example of how the problem can be solved:


```diff
        require(_msgSender() == source, "CBV");
        FlashData memory _fData = abi.decode(_userData, (FlashData));

        _fData.fee = _feeAmounts[0];
+       _userData = abi.encode(_fData);

        IERC20(_fData.token).safeTransfer(_fData.recipient, _fData.amount);
        IFlashLoanRecipient(_fData.recipient).callback(_userData);
```