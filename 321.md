Acidic Marmalade Robin

Medium

# LeverageManager may not return remaining borrow amount to user in removeLeverage()

### Summary

`LeverageManager.sol` contract does not include the `_podSwapAmtOutMin` value in the `_borrowAmtRemaining` [calculation](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/lvf/LeverageManager.sol#L400), which causes the contract to not return the remaining borrow tokens to user if  `_repayAmount - _pairedAmtReceived < _podSwapAmtOutMin`.

### Root Cause

In [LeverageManager.sol:_removeLeveragePostCallback()](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/lvf/LeverageManager.sol#L346) if user provided `_podSwapAmtOutMin` then contract will try to get `amountOut = _podSwapAmtOutMin` from `swapV2SingleExactOut` but will not account this value in `_borrowAmtRemaining` calculation:

```solidity
function _removeLeveragePostCallback(bytes memory _userData)
    internal
    returns (uint256 _podAmtRemaining, uint256 _borrowAmtRemaining)
{
    IFlashLoanSource.FlashData memory _d = abi.decode(_userData, (IFlashLoanSource.FlashData));
    (LeverageFlashProps memory _props, bytes memory _additionalInfo) =
        abi.decode(_d.data, (LeverageFlashProps, bytes));
    (
        ...
        uint256 _podSwapAmtOutMin, //@audit user provided this value
        ...
    ) = abi.decode(_additionalInfo, (uint256, uint256, uint256, uint256, uint256, uint256));
    ...
    (uint256 _podAmtReceived, uint256 _pairedAmtReceived) = _unstakeAndRemoveLP(
        _props.positionId, _posProps.pod, _collateralAssetRemoveAmt, _podAmtMin, _pairedAssetAmtMin
    );
    _podAmtRemaining = _podAmtReceived;
    ...
    // pay back flash loan and send remaining to borrower
    uint256 _repayAmount = _d.amount + _d.fee;
    //@audit check if _pairedAmtReceived not enough to repay for flash loan
    if (_pairedAmtReceived < _repayAmount) {
        _podAmtRemaining = _acquireBorrowTokenForRepayment(
            _props,
            _posProps.pod,
            _d.token,
            _repayAmount - _pairedAmtReceived, //@audit _borrowNeeded
            _podAmtReceived,
            _podSwapAmtOutMin, //@audit if this value > 0 then amountOut equal this value, not _borrowNeeded
            _userProvidedDebtAmtMax
        );
    }
    IERC20(_d.token).safeTransfer(IFlashLoanSource(_getFlashSource(_props.positionId)).source(), _repayAmount);
    //@audit if _podSwapAmtOutMin > _borrowNeeded borrowToken will be left on the contract and will not be returned to user
    _borrowAmtRemaining = _pairedAmtReceived > _repayAmount ? _pairedAmtReceived - _repayAmount : 0;
    emit RemoveLeverage(_props.positionId, _props.owner, _collateralAssetRemoveAmt);
}

function _acquireBorrowTokenForRepayment(
    LeverageFlashProps memory _props,
    address _pod,
    address _borrowToken,
    uint256 _borrowNeeded,
    uint256 _podAmtReceived,
    uint256 _podSwapAmtOutMin,
    uint256 _userProvidedDebtAmtMax
) internal returns (uint256 _podAmtRemaining) {
    _podAmtRemaining = _podAmtReceived;
    //@audit _repayAmount - _pairedAmtReceived
    uint256 _borrowAmtNeededToSwap = _borrowNeeded;
    ...
    if (_borrowAmtNeededToSwap > 0) {
        if (_isPodSelfLending(_props.positionId)) {
            ...
        } else {
            _podAmtRemaining = _swapPodForBorrowToken(
                _pod, _borrowToken, _podAmtReceived, _borrowAmtNeededToSwap, _podSwapAmtOutMin
            );
        }
    }
}

function _swapPodForBorrowToken(
    address _pod,
    address _targetToken,
    uint256 _podAmt,
    uint256 _targetNeededAmt,
    uint256 _podSwapAmtOutMin
) internal returns (uint256 _podRemainingAmt) {
    ...
    //@audit if user provided _podSwapAmtOutMin then try to get this amount from swap
    _dexAdapter.swapV2SingleExactOut(
        _pod, _targetToken, _podAmt, _podSwapAmtOutMin == 0 ? _targetNeededAmt : _podSwapAmtOutMin, address(this)
    );
    _podRemainingAmt = _podAmt - (_balBefore - IERC20(_pod).balanceOf(address(this)));
}
```

### Internal Pre-conditions

1. `_pairedAmtReceived < _repayAmount`

### External Pre-conditions

1. User provided `_podSwapAmtOutMin` > `_borrowNeeded` (`_repayAmount - _pairedAmtReceived`)
2. Swap was successful with `amountOut = _podSwapAmtOutMin`

### Attack Path

*No response*

### Impact

The protocol fails to return correct remaining borrow amount to user if he provided correct `_podSwapAmtOutMin` and this value is more than `_borrowNeeded`.

### PoC

**Take for example:**
`_repayAmount = 110`
`_pairedAmtReceived = 100`
`_podSwapAmtOutMin = 20`
`borrowToken.balanceOf(LeverageManager) = _pairedAmtReceived = 100`

**Contract wants to swap pod tokens for borrowToken to repay for flashloan:**
`_borrowAmtNeededToSwap = _repayAmount - _pairedAmtReceived = 110 - 100 = 10`
`amountOut = _podSwapAmtOutMin == 0 ? _borrowAmtNeededToSwap : _podSwapAmtOutMin = 20 == 0 -> false -> 20`
*Swap was successful*
`borrowToken.balanceOf(LeverageManager) = _pairedAmtReceived + amountOut = 100 + 20 = 120`

**Return**
1. Repay to flash source:
`borrowToken.balanceOf(LeverageManager) = borrowToken.balanceOf(LeverageManager) - _repayAmount = 120 - 110 = 10`
2. Calculate `_borrowAmtRemaining` to return to user:
`_borrowAmtRemaining = _pairedAmtReceived > _repayAmount ? _pairedAmtReceived - _repayAmount : 0 = 100 > 110 -> false -> 0`

Now `borrowToken.balanceOf(LeverageManager) = 10` and user did not get his tokens back.

### Mitigation

Change `_borrowAmtRemaining` calculation:
```solidity
if (_pairedAmtReceived < _repayAmount) {
    if (_podSwapAmtOutMin > _repayAmount - _pairedAmtReceived) {
        _borrowAmtRemaining = _pairedAmtReceived + _podSwapAmtOutMin - _repayAmount;
    } else {
        _borrowAmtRemaining = 0;
    }
} else {
    _borrowAmtRemaining = _pairedAmtReceived - _repayAmount
}
```