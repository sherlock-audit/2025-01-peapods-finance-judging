Keen Jetblack Deer

Medium

# Potential vault initial deposit attack for AutoCompoundingPodLP and FraxlendPair


### Summary

Potential vault initial deposit attack for AutoCompoundingPodLP and FraxlendPair

### Root Cause

AutoCompoundingPodLP and FraxlendPair are both ERC4626 vaults. Both contracts are susceptible to the vault initial deposit attack.

Even though both contracts use internal tracking for asset balance, which prevents token transfer donation, however, there are still ways to inflate the asset/share ratio:

1. For AutoCompoundingPodLP, attackers can inflate asset/share ratio by depositing a bunch of rewards, which will then compound to spTKN assets, effectively "donating" assets.
2. For FraxlendPair, it's a bit complicated. The idea is to borrow token to first increase asset/share ratio to >1, then inflate the asset/share ratio by continuous depositing/withdrawing asset. Asset/share ratio can be increased exponentially. Details and PoC can be found in [Pashov audit report](https://sherlock-files.ams3.digitaloceanspaces.com/additional_resources/peapods_lvf_Pashov_report.pdf) H-04.

Both issues are mitigated by adding a minimumDeposit in the factory contract. However, the mitigation is not incorrect, because the factory contract returns the deposited shares to the deployer. If the attacker was the deployer of the contract (consider the Peapods system is decentralized, so anyone can create a pod), he can simply redeem the shares, and the vaults would be in initial status again.

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/AutoCompoundingPodLpFactory.sol#L41

```solidity
    function create(
        string memory _name,
        string memory _symbol,
        bool _isSelfLendingPod,
        IDecentralizedIndex _pod,
        IDexAdapter _dexAdapter,
        IIndexUtils _indexUtils,
        uint96 _salt
    ) external returns (address _aspAddy) {
        _aspAddy =
            _deploy(getBytecode(_name, _symbol, _isSelfLendingPod, _pod, _dexAdapter, _indexUtils), _getFullSalt(_salt));
@>      if (address(_pod) != address(0) && minimumDepositAtCreation > 0) {
            _depositMin(_aspAddy, _pod);
        }
        AutoCompoundingPodLp(_aspAddy).transferOwnership(owner());
        emit Create(_aspAddy);
    }

    function _depositMin(address _aspAddy, IDecentralizedIndex _pod) internal {
        address _lpToken = _pod.lpStakingPool();
        IERC20(_lpToken).safeTransferFrom(_msgSender(), address(this), minimumDepositAtCreation);
        IERC20(_lpToken).safeIncreaseAllowance(_aspAddy, minimumDepositAtCreation);
        AutoCompoundingPodLp(_aspAddy).deposit(minimumDepositAtCreation, _msgSender());
    }
```

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/fraxlend/src/contracts/FraxlendPairDeployer.sol#L274

```solidity
    if (defaultDepositAmt > 0) {
        IERC20(_fraxlendPair.asset()).safeTransferFrom(msg.sender, address(this), defaultDepositAmt);
        IERC20(_fraxlendPair.asset()).approve(address(_fraxlendPair), defaultDepositAmt);
        _fraxlendPair.deposit(defaultDepositAmt, msg.sender);
    }
```

Additionally, for AutoCompoundingPodLP, if it was created as a self-lending pod in LeverageFactory, it would not set the `_pod` parameter, so no initial deposit would be created. This means it is by default susceptible to initial deposit attack.

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/lvf/LeverageFactory.sol#L109

```solidity
    function createSelfLendingPodAndAddLvf(
        address _borrowTkn,
        address _dexAdapter,
        address _indexUtils,
        bytes memory _podConstructorArgs,
        bytes memory _aspTknOracleRequiredImmutables,
        bytes memory _aspTknOracleOptionalImmutables,
        bytes memory _fraxlendPairConfigData
    ) external returns (address _newPod, address _aspTkn, address _aspTknOracle, address _fraxlendPair) {
        ...
@>      _aspTkn = _getOrCreateAspTkn(_podConstructorArgs, "", "", address(0), _dexAdapter, _indexUtils, true, false);
        ...
    }
```

### Internal pre-conditions

N/A

### External pre-conditions

- Attacker is deployer of the vault contracts, or a self-lending pod is created.

### Attack Path

Mentioned above.

### Impact

AutoCompoundingPodLP and FraxlendPair are still vulnerable to vault initial deposit attack.

### PoC

N/A

### Mitigation

Don't send the initial shares to deployer, but to a address(0) contract.