Faithful Wooden Elephant

High

# Incorrect `_updateAssetMetadataFromVault` function


### Summary
The calculation of `vaultUtilization` in the `LendingAssetVault::_updateAssetMetadataFromVault` function is incorrect.

### Root Cause
The issue arises from the calculation of vaultAssetRatioChange, which is defined as either:
    - `vaultAssetRatioChange = prevVaultCbr / currVaultCbr - 1`  
    - `vaultAssetRatioChange = currVaultCbr / prevVaultCbr - 1`
However, on line 298, the calculation always uses: 
    - `changeUtilizedState = currentAssetsUtilized * vaultAssetRatioChange`.

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/LendingAssetVault.sol#L298
```solidity
    function _updateAssetMetadataFromVault(address _vault) internal {
        uint256 _prevVaultCbr = _vaultWhitelistCbr[_vault];
        _vaultWhitelistCbr[_vault] = IERC4626(_vault).convertToAssets(PRECISION);
        if (_prevVaultCbr == 0) {
            return;
        }
293:    uint256 _vaultAssetRatioChange = _prevVaultCbr > _vaultWhitelistCbr[_vault]
            ? ((PRECISION * _prevVaultCbr) / _vaultWhitelistCbr[_vault]) - PRECISION
            : ((PRECISION * _vaultWhitelistCbr[_vault]) / _prevVaultCbr) - PRECISION;

        uint256 _currentAssetsUtilized = vaultUtilization[_vault];
298:    uint256 _changeUtilizedState = (_currentAssetsUtilized * _vaultAssetRatioChange) / PRECISION;
        vaultUtilization[_vault] = _prevVaultCbr > _vaultWhitelistCbr[_vault]
            ? _currentAssetsUtilized < _changeUtilizedState ? 0 : _currentAssetsUtilized - _changeUtilizedState
            : _currentAssetsUtilized + _changeUtilizedState;
        _totalAssetsUtilized = _totalAssetsUtilized - _currentAssetsUtilized + vaultUtilization[_vault];
        _totalAssets = _totalAssets - _currentAssetsUtilized + vaultUtilization[_vault];
        emit UpdateAssetMetadataFromVault(_vault, _totalAssets, _totalAssetsUtilized);
    }
```

### Internal pre-conditions
N/A

### External pre-conditions
N/A

### Attack Path
N/A

### PoC
Let's consider the following senario.
`prevVaultCbr = 1.1e27`, `currVaultCbr = 1e27`, `prevVaultUtilization = 110e18`
At this point, `currVaultUtilization` should be 100e18. However:
    In L293, `_vaultAssetRatioChange = 1e27 * 1.1 / 1 - 1e27 = 0.1e27`
    In L298, `_changeUtilizedState = 110e18 * 0.1e27 / 1e27 = 11e18`
    In L300, currVaultUtilization = 110e18 - 11e18 = 99e18.
As a result, the `vaultUtilization` is less than it should.

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/LendingAssetVault.sol#L250
```solidity
        vaultUtilization[_vault] -= _assetAmt;
```
https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/LendingAssetVault.sol#L331
```solidity
        vaultUtilization[_vault] -= _redeemAmt;
```
Due to the underflow, the `LAV` could not receive funds exceeding `vaultUtilization`.
The `CBR` of `FraxlendPairVault` can decrease, particularly in cases of bad debt.

### Impact
Loss of funds for the providers of `LAV`.

### Mitigation
```diff
    function _updateAssetMetadataFromVault(address _vault) internal {
        uint256 _prevVaultCbr = _vaultWhitelistCbr[_vault];
        _vaultWhitelistCbr[_vault] = IERC4626(_vault).convertToAssets(PRECISION);
        if (_prevVaultCbr == 0) {
            return;
        }
-293:    uint256 _vaultAssetRatioChange = _prevVaultCbr > _vaultWhitelistCbr[_vault]
-            ? ((PRECISION * _prevVaultCbr) / _vaultWhitelistCbr[_vault]) - PRECISION
-            : ((PRECISION * _vaultWhitelistCbr[_vault]) / _prevVaultCbr) - PRECISION;

-        uint256 _currentAssetsUtilized = vaultUtilization[_vault];
-298:    uint256 _changeUtilizedState = (_currentAssetsUtilized * _vaultAssetRatioChange) / PRECISION;
-        vaultUtilization[_vault] = _prevVaultCbr > _vaultWhitelistCbr[_vault]
-            ? _currentAssetsUtilized < _changeUtilizedState ? 0 : _currentAssetsUtilized - _changeUtilizedState
-            : _currentAssetsUtilized + _changeUtilizedState;
-        _totalAssetsUtilized = _totalAssetsUtilized - _currentAssetsUtilized + vaultUtilization[_vault];
-        _totalAssets = _totalAssets - _currentAssetsUtilized + vaultUtilization[_vault];
+        uint256 _prevAssetsUtilized = vaultUtilization[_vault];
+        vaultUtilization[_vault] = _prevAssetsUtilized * _vaultWhitelistCbr[_vault] / _prevVaultCbr;
+        _totalAssetsUtilized = _totalAssetsUtilized - _prevAssetsUtilized + vaultUtilization[_vault];
+        _totalAssets = _totalAssets - _prevAssetsUtilized + vaultUtilization[_vault];
        emit UpdateAssetMetadataFromVault(_vault, _totalAssets, _totalAssetsUtilized);
    }
```