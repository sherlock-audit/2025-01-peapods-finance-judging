Boxy Charcoal Perch

Medium

# Any user can front-run rescueERC20 to claim tokens from the contract under certain conditions

### Summary

`Zapper::rescueERC20` allows the owner to rescue stuck tokens or donations to the contract , however if the token to be rescued is an intermediate token in any uniswapV2 swap path then a malicious user can front-run the transaction to claim the tokens.


### Root Cause

`Zapper::rescueERC20` can be called by the admin to rescue tokens from the contract as we see here:
```solidity
    function rescueERC20(IERC20 _token) external onlyOwner {
        require(_token.balanceOf(address(this)) > 0);
        _token.safeTransfer(owner(), _token.balanceOf(address(this)));
    }
```

However, in `Zapper::_swapV2` if swap path is `twoHop` we notice that the contract balance of the intermediate token is swapped for the output token , here:

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/Zapper.sol#L206-L218
```solidity
    function _swapV2(address[] memory _path, uint256 _amountIn, uint256 _amountOutMin) internal returns (uint256) {
        bool _twoHops = _path.length == 3;
        address _out = _twoHops ? _path[2] : _path[1];
        uint256 _outBefore = IERC20(_out).balanceOf(address(this));
        IERC20(_path[0]).safeIncreaseAllowance(address(DEX_ADAPTER), _amountIn);
        DEX_ADAPTER.swapV2Single(_path[0], _path[1], _amountIn, _twoHops ? 0 : _amountOutMin, address(this));
        if (_twoHops) {
            uint256 _intermediateBal = IERC20(_path[1]).balanceOf(address(this)); //<@
            IERC20(_path[1]).safeIncreaseAllowance(address(DEX_ADAPTER), _intermediateBal);
            DEX_ADAPTER.swapV2Single(_path[1], _path[2], _intermediateBal, _amountOutMin, address(this));
        }
        return IERC20(_out).balanceOf(address(this)) - _outBefore;
    }
```

This means that if/when the token to be rescued is an intermediate token in any `twoHop` swap path ( i.e. swap involving 2 pools) , then an attacker can front-run the rescueERC20 transaction with a call to `addLpAndStake` such that the `_pairedLpTokenProvided` is the input token token in the `twoHop` swap path.
By doing so, they're able swap the contracts balance of the intermediate token to the output token add and stake pod Lp, effectively claiming the tokens which they can then unstake and withdraw anytime.


### Internal Pre-conditions

reascue token is also an intermediate token in any v2 `twoHop` swap path


### External Pre-conditions

NONE

### Attack Path

- Admin calls `Zapper::rescueERC20`
- User front-runs the rescueERC20 call and calls `IndexUtils::addLpAndStake` with `_pairedLpTokenProvided` = input token in the swap path
  The call path to `_swapV2` is: `IndexUtils::addLpAndStake` -> `Zapper::_zap` (when `pairedTokenProvided != pairedToken` of the pod) -> `Zapper::_swapV2`.


### Impact

Medium - loss of funds


### PoC

_No response_

### Mitigation

Use the amount out for the intermediate swap, example:

```diff
    function _swapV2(address[] memory _path, uint256 _amountIn, uint256 _amountOutMin) internal returns (uint256) {
        bool _twoHops = _path.length == 3;
        address _out = _twoHops ? _path[2] : _path[1];
        uint256 _outBefore = IERC20(_out).balanceOf(address(this));
        IERC20(_path[0]).safeIncreaseAllowance(address(DEX_ADAPTER), _amountIn);
-       DEX_ADAPTER.swapV2Single(_path[0], _path[1], _amountIn, _twoHops ? 0 : _amountOutMin, address(this));
+       uint _intermediateBal = DEX_ADAPTER.swapV2Single(_path[0], _path[1], _amountIn, _twoHops ? 0 : _amountOutMin, address(this));
        if (_twoHops) {
-           uint256 _intermediateBal = IERC20(_path[1]).balanceOf(address(this));
            IERC20(_path[1]).safeIncreaseAllowance(address(DEX_ADAPTER), _intermediateBal);
            DEX_ADAPTER.swapV2Single(_path[1], _path[2], _intermediateBal, _amountOutMin, address(this));
        }
        return IERC20(_out).balanceOf(address(this)) - _outBefore;
    }
```