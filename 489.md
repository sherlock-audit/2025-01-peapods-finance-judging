Keen Jetblack Deer

Medium

# `LeverageManager` ownership is set to `LeverageFactory`, which cannot call `setFlashSource()`.


### Summary

`LeverageManager` ownership is set to `LeverageFactory`, which cannot call `setFlashSource()`.

### Root Cause

`LeverageManager` inherits `LeverageManagerAccessControl`, which only the owner can set `lendingPairs[]` and `flashSource[]`. The issue here is, obviously the owner of `LeverageManager` is `LeverageFactory`, this can be inferred by `LeverageFactory` always call `setLendingPair()` when setting up a self-lending pod. However, `LeverageFactory` does not have any method to call `setFlashSource()`, nor transfer the ownership of `LeverageManager`.

This means once `LeverageFactory` owns `LeverageManager`, no new flashloan sources can be added. This makes it impossible to support new underlying tokens.

The fix here should be in `LeverageManagerAccessControl`, where the owner can set a `leverageFactory` contract address, and allow it to update `lendingPair[]`, instead of transferring the ownership to `LeverageFactory`.

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/lvf/LeverageManagerAccessControl.sol#L1

```solidity
contract LeverageManagerAccessControl is Ownable, ILeverageManagerAccessControl {
    // pod => pair
    mapping(address => address) public override lendingPairs;
    // borrow asset (USDC, DAI, pOHM, etc.) => flash source
    mapping(address => address) public override flashSource;

    constructor() Ownable(_msgSender()) {}

@>  function setLendingPair(address _pod, address _pair) external override onlyOwner {
        if (_pair != address(0)) {
            require(IFraxlendPair(_pair).collateralContract() != address(0), "LPS");
        }
        lendingPairs[_pod] = _pair;
    }

@>  function setFlashSource(address _borrowAsset, address _flashSource) external override onlyOwner {
        if (_flashSource != address(0)) {
            require(IFlashLoanSource(_flashSource).source() != address(0), "AFS");
        }
        flashSource[_borrowAsset] = _flashSource;
    }
}
```

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/lvf/LeverageFactory.sol

```solidity
    function addLvfSupportForPod(
        address _pod,
        address _dexAdapter,
        address _indexUtils,
        bytes memory _aspTknOracleRequiredImmutables,
        bytes memory _aspTknOracleOptionalImmutables,
        bytes memory _fraxlendPairConfigData
    ) external onlyOwner returns (address _aspTkn, address _aspTknOracle, address _fraxlendPair) {
        address _borrowTkn = IDecentralizedIndex(_pod).PAIRED_LP_TOKEN();
        require(ILeverageManagerAccessControl(leverageManager).flashSource(_borrowTkn) != address(0), "FS2");
        uint256 _aspMinDep = IAspTknFactory(aspTknFactory).minimumDepositAtCreation();
        if (_aspMinDep > 0) {
            address _spTkn = IDecentralizedIndex(_pod).lpStakingPool();
            IERC20(_spTkn).safeTransferFrom(_msgSender(), address(this), _aspMinDep);
            IERC20(_spTkn).safeIncreaseAllowance(aspTknFactory, _aspMinDep);
        }
        _aspTkn = _getOrCreateAspTkn(
            "", IERC20Metadata(_pod).name(), IERC20Metadata(_pod).symbol(), _pod, _dexAdapter, _indexUtils, false, false
        );
        _aspTknOracle = IAspTknOracleFactory(aspTknOracleFactory).create(
            _aspTkn, _aspTknOracleRequiredImmutables, _aspTknOracleOptionalImmutables, 0
        );
        _fraxlendPair = _createFraxlendPair(_borrowTkn, _aspTkn, _aspTknOracle, _fraxlendPairConfigData);

        // this effectively is what "turns on" LVF for the pair
        ILeverageManagerAccessControl(leverageManager).setLendingPair(_pod, _fraxlendPair);

        emit AddLvfSupportForPod(_pod, _aspTkn, _aspTknOracle, _fraxlendPair);
    }
```

### Internal pre-conditions

N/A

### External pre-conditions

N/A

### Attack Path

N/A

### Impact

Admin cannot add new flashloan source once the ownership is transferred to LeverageFactory.

### PoC

N/A

### Mitigation

The fix here should be in `LeverageManagerAccessControl`, where the owner can set a `leverageFactory` contract address, and allow it to update `lendingPair[]`, instead of transferring the ownership to `LeverageFactory`.