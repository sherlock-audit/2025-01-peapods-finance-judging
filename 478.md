Keen Jetblack Deer

Medium

# AutoCompoundingPodLP does not use correct oracle price as slippage for pairedLpTKn -> pTKN swap.


### Summary

AutoCompoundingPodLP does not use correct oracle price as slippage for pairedLpTKn -> pTKN swap.

### Root Cause

When doing the pairedLpTKN -> pTKN swap in AutoCompoundingPodLP, it checks the oracle for the expected output, and uses it as slippage. However, the oracle does not return expected result.

There bug here is: the `getPodPerBasePrice()` function returns baseTKN/pTKN price, instead of pairedLpTKN/pTKN price. The difference is for podded token or fraxlend pair token as pairedLpTKN, e.g. pOHM or fUSDC. The price differs by a constant factor of asset/share ratio of the pod or the fraxlend pair token.

This underestimates the oracle price, which loosens the slippage constraint. This may lead to sandwich opportunities for attackers.

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/AutoCompoundingPodLp.sol#L1

```solidity
    function _pairedLpTokenToPodLp(uint256 _amountIn, uint256 _deadline) internal returns (uint256 _amountOut) {
        address _pairedLpToken = pod.PAIRED_LP_TOKEN();
        uint256 _pairedSwapAmt = _getSwapAmt(_pairedLpToken, address(pod), _pairedLpToken, _amountIn);
        uint256 _pairedRemaining = _amountIn - _pairedSwapAmt;
        uint256 _minPtknOut;
        if (address(podOracle) != address(0)) {
            // calculate the min out with 5% slippage
            // @audit-bug: This does not return the correct oracle price.
            _minPtknOut = (
@>              podOracle.getPodPerBasePrice() * _pairedSwapAmt * 10 ** IERC20Metadata(address(pod)).decimals() * 95
            ) / 10 ** IERC20Metadata(_pairedLpToken).decimals() / 10 ** 18 / 100;
        }
        IERC20(_pairedLpToken).safeIncreaseAllowance(address(DEX_ADAPTER), _pairedSwapAmt);
        try DEX_ADAPTER.swapV2Single(_pairedLpToken, address(pod), _pairedSwapAmt, _minPtknOut, address(this)) returns (
            uint256 _podAmountOut
        ) {
            ...
        }
    }
```

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/oracle/spTKNMinimalOracle.sol

```solidity
    function getPodPerBasePrice() external view override returns (uint256 _pricePTknPerBase18) {
        _pricePTknPerBase18 = 10 ** (18 * 2) / _calculateBasePerPTkn(0);
    }

    function _calculateBasePerPTkn(uint256 _price18) internal view returns (uint256 _basePerPTkn18) {
        // pull from UniV3 TWAP if passed as 0
        if (_price18 == 0) {
            bool _isBadData;
            (_isBadData, _price18) = _getDefaultPrice18();
            if (_isBadData) {
                return 0;
            }
        }
        _basePerPTkn18 = _accountForCBRInPrice(pod, underlyingTkn, _price18);

        // adjust current price for spTKN pod unwrap fee, which will end up making the end price
        // (spTKN per base) higher, meaning it will take more spTKN to equal the value
        // of base token. This will more accurately ensure healthy LTVs when lending since
        // a liquidation path will need to account for unwrap fees
        _basePerPTkn18 = _accountForUnwrapFeeInPrice(pod, _basePerPTkn18);
    }
```


### Internal pre-conditions

N/A

### External pre-conditions

N/A

### Attack Path

N/A

### Impact

Oracle for pairedLpTKN -> pTKN swap slippage is incorrect. The slippage result is underestimated. This may lead to sandwich opportunities for attackers.

### PoC

N/A

### Mitigation

Also multiply the asset/share ratio for podded/fraxlend pair tokens as pairedLpTKN.
