Brilliant Fiery Sheep

Medium

# The amount of shares needed for redemption of borrow tokens is underquoted during the removal of leverage process leading to reverting.

### Summary

When removing leverage, some additional borrow amount is acquired in the `LeverageManager._acquireBorrowTokenForRepayment` function. The amount needed is however underquoted due to the rounding down nature of the `convertToShares` function. This means not enough amount will be acquired leading to the removing of leverage reverting.

### Root Cause

In `LeverageManager._acquireBorrowTokenForRepayment`, `_borrowAmtNeededToSwap` is acquired to provide enough tokens to repay the flash loan:

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/lvf/LeverageManager.sol#L431-L446


```solidity
        // sell pod token into LP for enough borrow token to get enough to repay
        // if self-lending swap for lending pair then redeem for borrow token
        if (_borrowAmtNeededToSwap > 0) {
            if (_isPodSelfLending(_props.positionId)) {
                _podAmtRemaining = _swapPodForBorrowToken(
                    _pod,
                    positionProps[_props.positionId].lendingPair,
                    _podAmtReceived,
                    IFraxlendPair(positionProps[_props.positionId].lendingPair).convertToShares(_borrowAmtNeededToSwap),
                    _podSwapAmtOutMin
                );
                IFraxlendPair(positionProps[_props.positionId].lendingPair).redeem(
                    IERC20(positionProps[_props.positionId].lendingPair).balanceOf(address(this)),
                    address(this),
                    address(this)
                );
```

As can be seen `convertToShares` is used to determine the equivalent number of shares that should be acquired for redemption into the borrow token. 

`convertToShares` is a rounding down function:

```solidity
    function convertToShares(uint256 _assets) external view returns (uint256 _shares) {
        _shares = toAssetShares(_assets, false, true);
    }
```

```solidity
    function toAssetShares(uint256 _amount, bool _roundUp, bool _previewInterest)
        public
        view
        returns (uint256 _shares)
    {
        if (_previewInterest) {
            (,,,, VaultAccount memory _totalAsset,) = previewAddInterest();
            _shares = _totalAsset.toShares(_amount, _roundUp);
        } else {
            _shares = totalAsset.toShares(_amount, _roundUp);
        }
    }
```

This means that the shares that will be used for the redemption of the borrow tokens will be underquoted.

The redemption process will also lead to a further rounding down compounding the issue:

```solidity
                IFraxlendPair(positionProps[_props.positionId].lendingPair).redeem(
                    IERC20(positionProps[_props.positionId].lendingPair).balanceOf(address(this)),
                    address(this),
                    address(this)
                );
```

```solidity
    function redeem(uint256 _shares, address _receiver, address _owner)
        external
        nonReentrant
        returns (uint256 _amountToReturn)
    {
        if (_receiver == address(0)) revert InvalidReceiver();

        // Check if withdraw is paused and revert if necessary
        if (isWithdrawPaused) revert WithdrawPaused();

        // Accrue interest if necessary
        _addInterest();

        // Pull from storage to save gas
        VaultAccount memory _totalAsset = totalAsset;

        // Calculate the number of assets to transfer based on the shares to burn
        _amountToReturn = _totalAsset.toAmount(_shares, false);
```

```solidity
    function toAmount(VaultAccount memory total, uint256 shares, bool roundUp) internal pure returns (uint256 amount) {
        if (total.shares == 0) {
            amount = shares;
        } else {
            amount = (shares * total.amount) / total.shares;
            if (roundUp && (amount * total.shares) / total.amount < shares) {
                amount = amount + 1;
            }
        }
    }
```

In the end the amount of borrow tokens acquired will be less than the required amount leading to reverting when trying to repay the flash loan.

```solidity
        // pay back flash loan and send remaining to borrower
        uint256 _repayAmount = _d.amount + _d.fee;
        if (_pairedAmtReceived < _repayAmount) {
            _podAmtRemaining = _acquireBorrowTokenForRepayment(
                _props,
                _posProps.pod,
                _d.token,
                _repayAmount - _pairedAmtReceived,
                _podAmtReceived,
                _podSwapAmtOutMin,
                _userProvidedDebtAmtMax
            );
        }
        IERC20(_d.token).safeTransfer(IFlashLoanSource(_getFlashSource(_props.positionId)).source(), _repayAmount);
```

### Internal Pre-conditions

None

### External Pre-conditions

None

### Attack Path

1. User tries to remove leverage.
2. The process reverts due to the error in determining the borrow tokens needed.

### Impact

The remove leverage process reverts leading to a denial of service.

### PoC

_No response_

### Mitigation

Round up the shares needed to be redeemed for the borrow tokens. This is safe because any surplus will later be refunded to the user.