Perfect Porcelain Snail

Medium

# Unset `props.sender` in `removeLeverage()` causes Flashloan repayment failure

### Summary

An unset `props.sender` in the `removeLeverage()` function leads to a situation where, if a user supplies a non-zero `_userProvidedDebtAmtMax` (to repay flashloan amount), the subsequent call `IERC20(_borrowToken).safeTransferFrom(_props.sender, address(this), _borrowAmtFromUser);` is made from the zero address. This causes the `transferFrom()` to revert, preventing the user from successfully repaying the flashloan via their provided funds and forcing them tosell their pTKN.

### Root Cause

In [`removeLeverage()`](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/lvf/LeverageManager.sol#L147), while preparing the flashloan repayment parameters, the `LeverageFlashProps` struct is instantiated without setting its `sender` field.

When the flashloan mechanism determines that additional funds are needed (i.e. when `_pairedAmtReceived < _repayAmount`), the function [`_acquireBorrowTokenForRepayment()`](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/lvf/LeverageManager.sol#L388C13-L388C46) executes.  

Inside `_acquireBorrowTokenForRepayment()`, the following line attempts to transfer tokens using `props.sender`:
 
  ```solidity
  IERC20(_borrowToken).safeTransferFrom(_props.sender, address(this), _borrowAmtFromUser);
  ```
  
  Since `_props.sender` was never initialized (and thus defaults to the zero address), the call results in a revert (e.g., `"Dai/insufficient-allowance"`).


### Internal Pre-conditions

1. A leveraged position must have been opened via `addLeverage()`.
2. During leverage removal via `removeLeverage()`, the condition `_pairedAmtReceived < _repayAmount` must be met so that the system relies on the user-provided debt amount (`_userProvidedDebtAmtMax`).

### External Pre-conditions

N/A

### Attack Path

1. A user with an existing leveraged position calls `removeLeverage()` and provides a non-zero value for `_userProvidedDebtAmtMax`.
2. The flashloan repayment logic detects that the borrowed token amount is insufficient (i.e. `_pairedAmtReceived < _repayAmount`) and proceeds to call `_acquireBorrowTokenForRepayment()`.
3. Inside this function, the contract attempts to execute:
   ```solidity
   IERC20(_borrowToken).safeTransferFrom(_props.sender, address(this), _borrowAmtFromUser);
   ```
4. Since `_props.sender` was never set, it defaults to the zero address, leading to a revert in the `transferFrom()` call.

### Impact

Affected users will be unable to supply additional funds for the flashloan repayment. As a result, even if they have the necessary tokens, the transaction will revert forcing users to sell their pTKN holdings.


### PoC

The following PoC (derived from [LivePOC.t.sol](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/test/helpers/LivePOC.t.sol)) demonstrates the issue:

`forge test -vvvv --match-path test/lvf/poc_remove_leverage_revert_transfer_from.t.sol --fork-url https://rpc.mevblocker.io`

[poc_remove_leverage_revert_transfer_from.t.sol.txt](https://github.com/user-attachments/files/18785000/poc_remove_leverage_revert_transfer_from.t.sol.txt)

```Solidity
function test_POCRevertTransferFrom() public {

        // Set a LP postion
        uint256 positionId;
        positionId = addLeverage();

        vm.startPrank(ALICE);

        uint256 borrowAssetAmt = 50 * 1e18 ;

        (,,address custodian,,) = leverageManager.positionProps(positionId);
        uint256 collateralAssetRemoveAmt = pair.userCollateralBalance(custodian); 
        borrowAssetAmt = pair.userBorrowShares(custodian);

        // Symbolic value
        uint256 userProvidedDebtAmtMax = 1;


        // From : https://etherscan.io/token/0x6b175474e89094c44da98b954eedeac495271d0f#code
        vm.expectRevert(bytes("Dai/insufficient-allowance"));
        // From forge test -vvvv --match-path test/lvf/POCRevertTransferFrom.t.sol --fork-url https://rpc.flashbots.net/fast
        // ├─ [5371] Dai::transferFrom(0x0000000000000000000000000000000000000000, LeverageManager: [0x3D7Ebc40AF7092E3F1C81F2e996cbA5Cae2090d7], 1)
        // │   └─ ← [Revert] revert: Dai/insufficient-allowance
        leverageManager.removeLeverage(positionId, borrowAssetAmt, collateralAssetRemoveAmt, 0, 0, 0, userProvidedDebtAmtMax);

        vm.stopPrank();
    }

function addLeverage() public returns (uint256) {
        uint256 daiAmt = 100 * 1e18;
        uint256 pairedLpDesired = 50 * 1e18;
        bytes memory config = abi.encode(0, 1000, block.timestamp + 1 hours);

        deal(address(DAI), ALICE, daiAmt);

        vm.startPrank(ALICE);

        // wrap into the pod
        IERC20(address(DAI)).approve(address(pod), type(uint256).max);
        pod.bond(address(DAI), daiAmt, 0);
        uint256 pTknAmt = IERC20(address(pod)).balanceOf(ALICE);

        uint256 positionId = leverageManager.initializePosition(address(pod), ALICE, address(0), false);

        IERC20(address(pod)).approve(address(leverageManager), type(uint256).max);
        leverageManager.addLeverage(positionId, address(pod), pTknAmt, pairedLpDesired, 0, false, config);

        vm.stopPrank();

        return positionId;
    }
```

### Mitigation

Before initiating the flashloan in `removeLeverage()`, ensure the `LeverageFlashProps` struct sets a proper `sender` value  by using `_msgSender()` (or the caller's address) :

```diff
function removeLeverage(
    uint256 _positionId,
    uint256 _borrowAssetAmt,
    uint256 _collateralAssetRemoveAmt,
    uint256 _podAmtMin,
    uint256 _pairedAssetAmtMin,
    uint256 _podSwapAmtOutMin,
    uint256 _userProvidedDebtAmtMax
) external override workflow(true) {
    address _sender = _msgSender();
    address _owner = positionNFT.ownerOf(_positionId);
    require(
        _owner == _sender ||
        positionNFT.getApproved(_positionId) == _sender ||
        positionNFT.isApprovedForAll(_owner, _sender),
        "A1"
    );
    address _lendingPair = positionProps[_positionId].lendingPair;
    IFraxlendPair(_lendingPair).addInterest(false);
    _processExtraFlashLoanPayment(_positionId, _sender);
    address _borrowTkn = _getBorrowTknForPod(_positionId);
    IERC20(_borrowTkn).safeIncreaseAllowance(_lendingPair, _borrowAssetAmt);
    LeverageFlashProps memory _props;
    _props.method = FlashCallbackMethod.REMOVE;
    _props.positionId = _positionId;
    _props.owner = _owner;
+   _props.sender = _sender;
    bytes memory _additionalInfo = abi.encode(
        IFraxlendPair(_lendingPair).totalBorrow().toShares(_borrowAssetAmt, false),
        _collateralAssetRemoveAmt,
        _podAmtMin,
        _pairedAssetAmtMin,
        _podSwapAmtOutMin,
        _userProvidedDebtAmtMax
    );
    IFlashLoanSource(_getFlashSource(_positionId)).flash(
        _borrowTkn, _borrowAssetAmt, address(this), abi.encode(_props, _additionalInfo)
    );
}
```

By setting `_props.sender` correctly, the contract will correctly perform the `transferFrom()` using the caller's address, allowing the user to supply the necessary tokens and avoiding the revert.