Bumpy Hemp Cuckoo

High

# Fee-on-transfer pod configuration will cause `PodUnwrapLocker.sol#debondAndLock()` to revert


## Summary
An incorrect handling of fee-on-transfer tokens will cause transaction reverts in the locking process for users as the `PodUnwrapLocker` contract fails to account for transfer fees during debonding operations.

## Root Cause
In [`PodUnwrapLocker.sol#debondAndLock()`](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/PodUnwrapLocker.sol#L60-L97), the contract does not account for the discrepancy between the transferred amount and received amount when the pod implements a fee-on-transfer mechanism.

```solidity
    function debondAndLock(address _pod, uint256 _amount) external nonReentrant {
        require(_amount > 0, "D1");
        require(_pod != address(0), "D2");

->      IERC20(_pod).safeTransferFrom(_msgSender(), address(this), _amount);

        IDecentralizedIndex _podContract = IDecentralizedIndex(_pod);
        IDecentralizedIndex.IndexAssetInfo[] memory _podTokens = _podContract.getAllAssets();
        address[] memory _tokens = new address[](_podTokens.length);
        uint256[] memory _balancesBefore = new uint256[](_tokens.length);

        // Get token addresses and balances before debonding
        for (uint256 i = 0; i < _tokens.length; i++) {
            _tokens[i] = _podTokens[i].token;
            _balancesBefore[i] = IERC20(_tokens[i]).balanceOf(address(this));
        }
->      _podContract.debond(_amount, new address[](0), new uint8[](0));

        uint256[] memory _receivedAmounts = new uint256[](_tokens.length);
        for (uint256 i = 0; i < _tokens.length; i++) {
            _receivedAmounts[i] = IERC20(_tokens[i]).balanceOf(address(this)) - _balancesBefore[i];
        }

        IDecentralizedIndex.Config memory _podConfig = _podContract.config();
        uint256 _lockId = currentLockId++;
        locks[_lockId] = LockInfo({
            user: _msgSender(),
            pod: _pod,
            tokens: _tokens,
            amounts: _receivedAmounts,
            unlockTime: block.timestamp + _podConfig.debondCooldown,
            withdrawn: false
        });

        emit LockCreated(
            _lockId, _msgSender(), _pod, _tokens, _receivedAmounts, block.timestamp + _podConfig.debondCooldown
        );
    }
```

The pod's transfer fee mechanism is implemented in `DecentralizedIndex.sol#_update()`:

```solidity
    function _update(address _from, address _to, uint256 _amount) internal override {
        require(!_blacklist[_to], "BK");
        bool _buy = _from == V2_POOL && _to != V2_ROUTER;
        bool _sell = _to == V2_POOL;
        uint256 _fee;
        if (_swapping == 0 && _swapAndFeeOn == 1) {
            if (_from != V2_POOL) {
                _processPreSwapFeesAndSwap();
            }
            if (_buy && _fees.buy > 0) {
                _fee = (_amount * _fees.buy) / DEN;
                super._update(_from, address(this), _fee);
            } else if (_sell && _fees.sell > 0) {
                _fee = (_amount * _fees.sell) / DEN;
                super._update(_from, address(this), _fee);
->          } else if (!_buy && !_sell && _config.hasTransferTax) {
->              _fee = _amount / 10000; // 0.01%
->              _fee = _fee == 0 && _amount > 0 ? 1 : _fee;
->              super._update(_from, address(this), _fee);
            }
        }
        _processBurnFee(_fee);
->      super._update(_from, _to, _amount - _fee);
    }
```

## Internal Pre-conditions
1. Pod contract needs to have `hasTransferTax` enabled in its configuration
2. User needs to attempt debonding and locking operation with fee-on-transfer enabled pod

## Impact
Users cannot utilize the debond and lock functionality when the pod is configured with transfer fees, effectively blocking a core protocol feature.

## Mitigation
Update `PodUnwrapLocker.sol#debondAndLock()` to account for received amount after transfer:

```solidity
    function debondAndLock(address _pod, uint256 _amount) external nonReentrant {
        require(_amount > 0, "D1");
        require(_pod != address(0), "D2");

+       uint256 balanceBefore = IERC20(_pod).balanceOf(address(this));
        IERC20(_pod).safeTransferFrom(_msgSender(), address(this), _amount);
+       _amount = IERC20(_pod).balanceOf(address(this)) - balanceBefore;

        ....................................
    }
```
