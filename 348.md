Bumpy Hemp Cuckoo

Medium

# Invalid slippage handling will cause loss of funds for users


## Summary
The incorrect slippage calculation in `Zapper.sol` will cause loss of funds for users as attackers can perform sandwich attacks due to weakened slippage protection.

## Root Cause
In [`Zapper.sol:L177`](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/Zapper.sol#L177) there is incorrect slippage handling where `_amountOutMin` is always reduced by `_finalSlip` regardless of whether user provided a valid minimum amount:

```solidity
    function _swapV3Single(address _in, uint24 _fee, address _out, uint256 _amountIn, uint256 _amountOutMin)
        internal
        returns (uint256)
    {
        address _v3Pool;
        try DEX_ADAPTER.getV3Pool(_in, _out, uint24(10000)) returns (address __v3Pool) {
            _v3Pool = __v3Pool;
        } catch {
            _v3Pool = DEX_ADAPTER.getV3Pool(_in, _out, int24(200));
        }
->      if (_amountOutMin == 0) {
            address _token0 = _in < _out ? _in : _out;
            uint256 _poolPriceX96 =
                V3_TWAP_UTILS.priceX96FromSqrtPriceX96(V3_TWAP_UTILS.sqrtPriceX96FromPoolAndInterval(_v3Pool));
->          _amountOutMin = _in == _token0
                ? (_poolPriceX96 * _amountIn) / FixedPoint96.Q96
                : (_amountIn * FixedPoint96.Q96) / _poolPriceX96;
        }

        uint256 _outBefore = IERC20(_out).balanceOf(address(this));
        uint256 _finalSlip = _slippage[_v3Pool] > 0 ? _slippage[_v3Pool] : _defaultSlippage;
        IERC20(_in).safeIncreaseAllowance(address(DEX_ADAPTER), _amountIn);
        DEX_ADAPTER.swapV3Single(
->          _in, _out, _fee, _amountIn, (_amountOutMin * (1000 - _finalSlip)) / 1000, address(this)
        );
        return IERC20(_out).balanceOf(address(this)) - _outBefore;
    }
```

## Attack Path
1. User calls swap function with desired minimum output amount
2. Contract incorrectly reduces this amount by the slippage percentage
3. Attacker sandwiches the transaction, taking advantage of weakened slippage protection
4. User receives less tokens than their specified minimum

## Impact
Users can suffer significant losses due to sandwich attacks enabled by incorrect slippage handling. The attacker profits from the price manipulation.

## Mitigation
Modify `Zapper.sol#_swapV3Single()` to only apply slippage reduction when no minimum amount is specified:

```solidity
    function _swapV3Single(address _in, uint24 _fee, address _out, uint256 _amountIn, uint256 _amountOutMin)
        internal
        returns (uint256)
    {
        address _v3Pool;
        try DEX_ADAPTER.getV3Pool(_in, _out, uint24(10000)) returns (address __v3Pool) {
            _v3Pool = __v3Pool;
        } catch {
            _v3Pool = DEX_ADAPTER.getV3Pool(_in, _out, int24(200));
        }
+       uint256 _finalSlip = _slippage[_v3Pool] > 0 ? _slippage[_v3Pool] : _defaultSlippage;
        if (_amountOutMin == 0) {
            address _token0 = _in < _out ? _in : _out;
            uint256 _poolPriceX96 =
                V3_TWAP_UTILS.priceX96FromSqrtPriceX96(V3_TWAP_UTILS.sqrtPriceX96FromPoolAndInterval(_v3Pool));
            _amountOutMin = _in == _token0
                ? (_poolPriceX96 * _amountIn) / FixedPoint96.Q96
                : (_amountIn * FixedPoint96.Q96) / _poolPriceX96;
        }
+       else {
+           _finalSlip = 0;
+       }

        uint256 _outBefore = IERC20(_out).balanceOf(address(this));
-       uint256 _finalSlip = _slippage[_v3Pool] > 0 ? _slippage[_v3Pool] : _defaultSlippage;
        IERC20(_in).safeIncreaseAllowance(address(DEX_ADAPTER), _amountIn);
        DEX_ADAPTER.swapV3Single(
            _in, _out, _fee, _amountIn, (_amountOutMin * (1000 - _finalSlip)) / 1000, address(this)
        );
        return IERC20(_out).balanceOf(address(this)) - _outBefore;
    }
```