Atomic Syrup Leopard

Medium

# Users lose their borrow assets during removing leverage because of the incorrect calculation of remaining borrow amount.

### Summary

When users remove their leverage from LVF, they can provide `_podSwapAmtOutMin` to override the output amount of swapping pod token into borrow asset. And the remaining borrow amount is calculated after paying flash loan and is sent back to the user.

However, the remaining borrow amount is calculated incorrectly, thus causing loss to users.

### Root Cause

The root cause is in [`_removeLeveragePostCallback`](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/d28eb19f4b39d3db7997477460f9f9c76839cb0c/contracts/contracts/lvf/LeverageManager.sol#L400) function.

It returns zero as  `_borrowAmtRemaining` when `_pairedAmtReceived < _repayAmount`.

When `_podSwapAmtOutMin` is zero, the logic is correct, but when `_podSwapAmtOutMin` is set, it's usually `_podSwapAmtOutMin > _pairedAmtReceived - _repayAmount`, thus there is always the remaining amount to be returned.

### Internal Pre-conditions

N/A

### External Pre-conditions

N/A

### Attack Path

- Alice has a leverage position in LVF and going to remove leverage.
- Assume, 1000 DAI needs to be repaid and 800 DAI is returned by removing leverage and unstaking.
- The amount to swap is 200 DAI, but say Alice has provided 300 DAI as `_podSwapAmtOutMin`.
- Some of her pod tokens are swapped into 300 DAI.
- After paying 1000 DAI flash loan, there's 100 DAI left in the contract.
- However, the contract does not return 100 DAI to Alice.
- As a result, Alice loses 100 DAI.

### Impact

Users lose their borrow assets during removing leverage, as long as they provide `_podSwapAmtOutMin`.

### PoC

_No response_

### Mitigation

To make it simple, the remaining borrow amount to be returned would be the contract's balance. Or to make it precise, `_acquireBorrowTokenForRepayment` function should return the remaining amount along with `_podAmtRemaining`.

```diff
    function _acquireBorrowTokenForRepayment(
        LeverageFlashProps memory _props,
        address _pod,
        address _borrowToken,
        uint256 _borrowNeeded,
        uint256 _podAmtReceived,
        uint256 _podSwapAmtOutMin,
        uint256 _userProvidedDebtAmtMax
-   ) internal returns (uint256 _podAmtRemaining) {
+   ) internal returns (uint256 _podAmtRemaining, uint256 _borrowRemainingAmt) {
        _podAmtRemaining = _podAmtReceived;
        uint256 _borrowAmtNeededToSwap = _borrowNeeded;
        if (_userProvidedDebtAmtMax > 0) {
            uint256 _borrowAmtFromUser =
                _userProvidedDebtAmtMax >= _borrowNeeded ? _borrowNeeded : _userProvidedDebtAmtMax;
            _borrowAmtNeededToSwap -= _borrowAmtFromUser;
            IERC20(_borrowToken).safeTransferFrom(_props.sender, address(this), _borrowAmtFromUser);
        }
        // sell pod token into LP for enough borrow token to get enough to repay
        // if self-lending swap for lending pair then redeem for borrow token
        if (_borrowAmtNeededToSwap > 0) {
            if (_isPodSelfLending(_props.positionId)) {
-               _podAmtRemaining = _swapPodForBorrowToken(
+               (_podAmtRemaining, _borrowRemainingAmt) = _swapPodForBorrowToken(
                    _pod,
                    positionProps[_props.positionId].lendingPair,
                    _podAmtReceived,
                    IFraxlendPair(positionProps[_props.positionId].lendingPair).convertToShares(_borrowAmtNeededToSwap),
                    _podSwapAmtOutMin
                );
                IFraxlendPair(positionProps[_props.positionId].lendingPair).redeem(
                    IERC20(positionProps[_props.positionId].lendingPair).balanceOf(address(this)),
                    address(this),
                    address(this)
                );
            } else {
-               _podAmtRemaining = _swapPodForBorrowToken(
+               (_podAmtRemaining, _borrowRemainingAmt) = _swapPodForBorrowToken(
                    _pod, _borrowToken, _podAmtReceived, _borrowAmtNeededToSwap, _podSwapAmtOutMin
                );
            }
        }
    }

    function _swapPodForBorrowToken(
        address _pod,
        address _targetToken,
        uint256 _podAmt,
        uint256 _targetNeededAmt,
        uint256 _podSwapAmtOutMin
-   ) internal returns (uint256 _podRemainingAmt) {
+   ) internal returns (uint256 _podRemainingAmt, uint256 _borrowRemainingAmt) {
        IDexAdapter _dexAdapter = IDecentralizedIndex(_pod).DEX_HANDLER();
        uint256 _balBefore = IERC20(_pod).balanceOf(address(this));
        IERC20(_pod).safeIncreaseAllowance(address(_dexAdapter), _podAmt);
        _dexAdapter.swapV2SingleExactOut(
            _pod, _targetToken, _podAmt, _podSwapAmtOutMin == 0 ? _targetNeededAmt : _podSwapAmtOutMin, address(this)
        );
        _podRemainingAmt = _podAmt - (_balBefore - IERC20(_pod).balanceOf(address(this)));
+       _borrowRemainingAmt = _podSwapAmtOutMin == 0 ? 0 : _podSwapAmtOutMin - _targetNeededAmt;
    }
```