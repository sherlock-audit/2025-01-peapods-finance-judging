Boxy Charcoal Perch

Medium

# `FraxLendPairCore::_addInterest` incorrectly calculates utilization rate

### Summary

`_addInterest` incorrectly calculates and sets the `_prevUtiliationRate` state variable and as a result breaks the `minimum utilization change` functionality


### Root Cause

The Utilization rate is calculated as total_borrows/total_assets in the contract, however,
In `_addInterest` the utilization rate is calculated as total_borrows/total_assets - total_borrows as seen here:

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/fraxlend/src/contracts/FraxlendPairCore.sol#L442-L458
```solidity
    function _addInterest()
        internal
        returns (
            bool _isInterestUpdated,
            uint256 _interestEarned,
            uint256 _feesAmount,
            uint256 _feesShare,
            CurrentRateInfo memory _currentRateInfo
        )
    {
        // Pull from storage and set default return values
        _currentRateInfo = currentRateInfo;


        // store the current utilization rate as previous for next check
        uint256 _totalAssetsAvailable = _totalAssetAvailable(totalAsset, totalBorrow, true);// <@
        _prevUtilizationRate = _totalAssetsAvailable == 0 ? 0 : (UTIL_PREC * totalBorrow.amount) / _totalAssetsAvailable;
       //...SNIP...

}


    function _totalAssetAvailable(VaultAccount memory _totalAsset, VaultAccount memory _totalBorrow, bool _includeVault)
        internal
        view
        returns (uint256)
    {
        if (_includeVault) {
            return _totalAsset.totalAmount(address(externalAssetVault)) - _totalBorrow.amount;// <@
        }
        return _totalAsset.amount - _totalBorrow.amount;
    }
```



the `totalAssetsAvailble` function returns the total asset in contract plus assets in external vault minus the total borrows

this means that the `_prevUtilizationRate` recorded in `_addInterest` will be significantly larger than the actual utilization rate that will be calculated by the external `addInterest` function as we see here:

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/fraxlend/src/contracts/FraxlendPairCore.sol#L286-L309
```solidity
    function addInterest(bool _returnAccounting)
        external
        nonReentrant
        returns (
            uint256 _interestEarned,
            uint256 _feesAmount,
            uint256 _feesShare,
            CurrentRateInfo memory _currentRateInfo,
            VaultAccount memory _totalAsset,
            VaultAccount memory _totalBorrow
        )
    {
        _currentRateInfo = currentRateInfo;
        // the following checks whether the current utilization rate against the new utilization rate
        // (including external assets available) exceeds a threshold and only updates interest if so.
        // With this enabled, it's obviously possible for there to be some level of "unfair" interest
        // paid by borrowers and/or earned by suppliers, but the idea is this unfairness
        // should theoretically be negligible within some level of error and therefore it won't matter.
        // This is in place to support lower gas & more arbitrage volume through the pair since arbitrage
        // would many times only occur with small changes in asset supply or borrowed positions.
        uint256 _currentUtilizationRate = _prevUtilizationRate;
        uint256 _totalAssetsAvailable = totalAsset.totalAmount(address(externalAssetVault));// <@
        uint256 _newUtilizationRate =
            _totalAssetsAvailable == 0 ? 0 : (UTIL_PREC * totalBorrow.amount) / _totalAssetsAvailable;
       //...SNIP...
}
```

since the total borrows is not removed from the denominator and as a result the `minimum utilization change` functionality is basically broken because interest will always be calculated/updated whenever the addInterest function is called.


### Internal Pre-conditions

Admin sets a reasonable `minURChangeForExternalAddInterest`


### External Pre-conditions

NONE

### Attack Path

NONE

### Impact

Medium - Broken functionality, `addInterest` will always calculate new interest rates


### PoC

_No response_

### Mitigation

```diff
    function _addInterest()
        internal
        returns (
            bool _isInterestUpdated,
            uint256 _interestEarned,
            uint256 _feesAmount,
            uint256 _feesShare,
            CurrentRateInfo memory _currentRateInfo
        )
    {
        // Pull from storage and set default return values
        _currentRateInfo = currentRateInfo;

        // store the current utilization rate as previous for next check
-       uint256 _totalAssetsAvailable = _totalAssetAvailable(totalAsset, totalBorrow, true);
+       uint256 _totalAssetsAvailable = totalAsset.totalAmount(address(externalAssetVault));
        _prevUtilizationRate = _totalAssetsAvailable == 0 ? 0 : (UTIL_PREC * totalBorrow.amount) / _totalAssetsAvailable;

        // ...SNIP...
    }
```