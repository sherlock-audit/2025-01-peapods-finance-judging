Perfect Porcelain Snail

High

# Double extraction of debond Fee in `_calculateBasePerPTkn()` leading to incorrect price base per pTKN

### Summary

The debond fee is subtracted twice during the price conversion in [`_calculateBasePerPTkn()`](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/oracle/spTKNMinimalOracle.sol#L170). This double fee extraction results in an under calculation of the base per pTKN price.

### Root Cause

The fee is applied in two places:

1. **First Extraction**  
   Within the conversion logic of [`_accountForCBRInPrice()`](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/oracle/spTKNMinimalOracle.sol#L231), which is used indirectly during the execution of `convertToAssets()` in [WeightedIndex.sol](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/WeightedIndex.sol#L120).

  ```Solidity
    /// @notice The ```convertToAssets``` function returns the number of TKN returned based on burning _shares pTKN excluding fees
    /// @param _shares Number of pTKN to burn
    /// @return _assets Number of TKN[0] to be returned to user from pod
    function convertToAssets(uint256 _shares) external view override returns (uint256 _assets) {
        bool _firstIn = _isFirstIn();
        uint256 _percSharesX96_2 = _firstIn ? 2 ** (96 / 2) : (_shares * 2 ** (96 / 2)) / _totalSupply;
        if (_firstIn) {
            _assets = (indexTokens[0].q1 * _percSharesX96_2) / FixedPoint96.Q96 / 2 ** (96 / 2);
        } else {
            _assets = (_totalAssets[indexTokens[0].token] * _percSharesX96_2) / 2 ** (96 / 2);
        }
        _assets -= ((_assets * _fees.debond) / DEN);
    }
  ```

2. **Second Extraction**  
   Explicitly in [`_accountForUnwrapFeeInPrice()`](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/oracle/spTKNMinimalOracle.sol#L246):

   ```solidity
   function _accountForUnwrapFeeInPrice(address _pod, uint256 _currentPrice)
       internal
       view
       returns (uint256 _newPrice)
   {
       uint16 _unwrapFee = IDecentralizedIndex(_pod).DEBOND_FEE();
       _newPrice = _currentPrice - (_currentPrice * _unwrapFee) / 10000;
   }
   ```

Together, these lead to the debond fee being deducted twice, thus lowering the calculated `_basePerPTkn18` value this directly affects the outputs of functions like [`getPodPerBasePrice()`](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/oracle/spTKNMinimalOracle.sol#L104C14-L104C32) and [`getPrices()`](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/oracle/spTKNMinimalOracle.sol#L145), resulting in incorrect asset valuations.

### Internal Pre-conditions

1. The podâ€™s debond fee  is non-zero.

### External Pre-conditions

N/A

### Attack Path

1. A user (or attacker) call a function that will query the conversion rate  such as `getPodPerBasePrice()` or `getPrices()`.
2. Internally, the system calls `_calculateBasePerPTkn()`:
   - First, the conversion applies the debond fee via `_accountForCBRInPrice()` during the native conversion logic.
   - Next, the same fee is subtracted again in `_accountForUnwrapFeeInPrice()`.
3. This double fee deduction leads to an under calculated conversion price.

### Impact

The following functions are using  `_calculateBasePerPTkn()`:

- The function  `getPrices` is used to calculate the spTKN collateral value, which plays a critical role in determining the borrowing capacity in Frax Lend pairs. Due to the double extraction of the debond fee, this function return an undervalued conversion rate. Consequently,  spTKN collateral is undervalued, meaning they are allowed to borrow less than they would if the fees were applied correctly.

- The function `getPodPerBasePrice` is used in `AutoCompoundingPodLp.sol` in order to swap pairedAsset to Pod. A wrong price calculation will lead to an incorrect swap amount.

### PoC

The following PoC (derived from [test_getPodPerBasePrice_PEASDAI()](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/test/oracle/spTKNMinimalOracle.t.sol#L71C14-L71C45) demonstrates the issue:


```sh
forge test -vvvv --match-path test/oracle/poc_extract_double_time_fees.t.sol --fork-url https://rpc.mevblocker.io
```

[poc_extract_double_time_fees.t.sol.txt](https://github.com/user-attachments/files/18787095/poc_extract_double_time_fees.t.sol.txt)
[spTKNMinimalOracleSuggested.sol.txt](https://github.com/user-attachments/files/18787096/spTKNMinimalOracleSuggested.sol.txt)

**Context**:
- Use the same setup as test_getPodPerBasePrice_PEASDAI()
- spTKNMinimalOracleSuggested removed `_accountForUnwrapFeeInPrice()`

```Solidity
function test_extractDebondFeeTwoTimes() public {


        spTKNMinimalOracleSuggested oraclePEASDAISuggested = new spTKNMinimalOracleSuggested(
            abi.encode(
                address(_clOracle),
                address(_uniOracle),
                address(_diaOracle),
                0x6B175474E89094C44Da98b954EedeAC495271d0F, // DAI
                false,
                false,
                IDecentralizedIndex(_newPod).lpStakingPool(),
                0xAe750560b09aD1F5246f3b279b3767AfD1D79160 // UniV3: PEAS / DAI
            ),
            abi.encode(address(0), address(0), address(0), address(0), address(0), address(_v2Res))
        );

        uint256 _currentPrice18 = oraclePEASDAI.getPodPerBasePrice();
        uint256 _suggestedPrice18 = oraclePEASDAISuggested.getPodPerBasePrice();

        
        
        // Calculate the result of _accountForCBRInPrice() manually in order to check the difference 

        // From the report of Guardian audits
        // (_amtUnderlying * IERC20(_underlying).balanceOf(_pod) * 10 * IERC20Metadata(_pod).decimals()) /
        //   IERC20(_pod).totalSupply() / 10 * IERC20Metadata(_underlying).decimals();
        
        (, uint256 _amtUnderlying) = IMinimalSinglePriceOracle(oraclePEASDAI.UNISWAP_V3_SINGLE_PRICE_ORACLE()).getPriceUSD18(
            oraclePEASDAI.BASE_CONVERSION_CHAINLINK_FEED(), oraclePEASDAI.underlyingTkn(), oraclePEASDAI.UNDERLYING_TKN_CL_POOL(), 10 minutes
        );
    
        IDecentralizedIndex.IndexAssetInfo[] memory _assets = IDecentralizedIndex(_newPod).getAllAssets();
        address underlyingTkn = _assets[0].token;
        
        uint256 _manualAccountForCBRInPrice;
        uint256 totalSupply = IDecentralizedIndex(_newPod).totalSupply();
        uint256 totalAssets = IDecentralizedIndex(_newPod).totalAssets();
        {
        _manualAccountForCBRInPrice = (_amtUnderlying * totalAssets * 10 ** IERC20Metadata(_newPod).decimals()) /
           totalSupply / 10 ** IERC20Metadata(underlyingTkn).decimals();
        }

        uint16 _unwrapFee = IDecentralizedIndex(_newPod).DEBOND_FEE();
        _manualAccountForCBRInPrice = _manualAccountForCBRInPrice - (_manualAccountForCBRInPrice * _unwrapFee) / 10000;
        uint256 _manualPricePTknPerBase18 = 10 ** (18 * 2) / _manualAccountForCBRInPrice;


        console.log("_currentPrice18 : %s", _currentPrice18); // console::log("_currentPrice18 : %s", 178425338811729054 [1.784e17])
        console.log("_suggestedPrice18 : %s", _suggestedPrice18); // console::log("_suggestedPrice18 : %s", 175748958729553118 [1.757e17])
        console.log("_manualPricePTknPerBase18 : %s", _manualPricePTknPerBase18); //  console::log("_manualPricePTknPerBase18 : %s", 175748958729241579 [1.757e17]) 

        uint256 diffCurrentSuggested = _currentPrice18 - _suggestedPrice18;
        console.log("diffCurrentSuggested : %s", diffCurrentSuggested); // console::log("diffCurrentSuggested : %s", 2676380082175936 [2.676e15])

        assertGt(_currentPrice18, _suggestedPrice18);

        uint256 diffSuggestedManual = _suggestedPrice18 - _manualPricePTknPerBase18;
        console.log("diffSuggestedManual : %s", diffSuggestedManual); //  console::log("diffSuggestedManual : %s", 311539 [3.115e5]) 

        // Check that the manual and suggested has the same result ==> Theoric and new implementation matched
        assertApproxEqAbs(_suggestedPrice18, _manualPricePTknPerBase18 , 1e6);
        // Check that manual + diffCurrentSuggested = _currentPrice18 ==> Theoric + excess unwrap fee = current implementation
        assertApproxEqAbs(_currentPrice18, _manualPricePTknPerBase18 + diffCurrentSuggested, 1e6);
    }
```


### Mitigation

Remove the redundant fee extraction by eliminating the call to `_accountForUnwrapFeeInPrice()` in `_calculateBasePerPTkn()`. This ensures that the debond fee is only applied once during the price conversion process, yielding a correct base per pTKN conversion rate.

