Keen Jetblack Deer

Medium

# TokenRewards.sol potential overflow when calculating cumulative rewards, which may DoS the entire reward logic.


### Summary

TokenRewards.sol potential overflow when calculating cumulative rewards, which may DoS the entire reward logic.

### Root Cause

`_rewardsPerShare[]` accumulates the sum of `(PRECISION * _depositAmount) / totalShares` for deposited rewards.

During spTKN transfer, `_resetExcluded()` is always called, which always calculate a `_r = (_share * _rewardsPerShare[_token]) / PRECISION;` when calculating `rewards[_token][_wallet].excluded`.

Here PRECISION=1e27, share decimals is always 18, and reward token decimals is 18. In worst case scenario, totalShares can be 1 wei (e.g. during the beginning of a pod lifecycle, or a rare case during the pod lifecycle). 

u256.max is around 1e77. Now we already have 18+18+27=63. If depositAmount and sharesAmount (which is UniV2 LP amount) multiplies up to 1e14, we can construct an overflow scenario.

This would be acheivable if either rewardToken and pTKN/pairedLpTKN LP was cheap.

One example is the setting pairedLpTKN to be pMODE, this is currently available on Mode chain (see on https://peapods.finance/app). pMODE currently has a fair price of 0.0126. Now if the pTKN was also very cheap (e.g. pPEPE, which is priced at $0.00001, and also existing on Peapods mainnet right now), the pTKN/pairedLpTKN LP would be cheap. Note that reward token can also be pairedLpTKN (e.g. if `LEAVE_AS_PAIRED_LP_TOKEN=true` was set in TokenRewards.sol). So to acheive overflow for pTKN=pPEPE and pairedLpTKN=rewardToken=pMode, only around `0.012*1e7*2 ~ 2e5` is required, which is reasonable.

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/TokenRewards.sol#L20

```solidity
    uint256 constant PRECISION = 10 ** 27;

    function _depositRewards(address _token, uint256 _amountTotal) internal {
        ...
        rewardsDeposited[_token] += _depositAmount;
@>      _rewardsPerShare[_token] += (PRECISION * _depositAmount) / totalShares;
        emit DepositRewards(_msgSender(), _token, _depositAmount);
    }

    function _addShares(address _wallet, uint256 _amount) internal {
        if (shares[_wallet] > 0) {
            _distributeReward(_wallet);
        }
        uint256 sharesBefore = shares[_wallet];
        totalShares += _amount;
        shares[_wallet] += _amount;
        if (sharesBefore == 0 && shares[_wallet] > 0) {
            totalStakers++;
        }
@>      _resetExcluded(_wallet);
    }

    function _removeShares(address _wallet, uint256 _amount) internal {
        require(shares[_wallet] > 0 && _amount <= shares[_wallet], "RE");
        _distributeReward(_wallet);
        totalShares -= _amount;
        shares[_wallet] -= _amount;
        if (shares[_wallet] == 0) {
            totalStakers--;
        }
@>      _resetExcluded(_wallet);
    }

    function _resetExcluded(address _wallet) internal {
        for (uint256 _i; _i < _allRewardsTokens.length; _i++) {
            address _token = _allRewardsTokens[_i];
            rewards[_token][_wallet].excluded = _cumulativeRewards(_token, shares[_wallet], true);
        }
    }

    function _cumulativeRewards(address _token, uint256 _share, bool _roundUp) internal view returns (uint256 _r) {
@>      _r = (_share * _rewardsPerShare[_token]) / PRECISION;
        if (_roundUp && (_share * _rewardsPerShare[_token]) % PRECISION > 0) {
            _r = _r + 1;
        }
    }
```

### Internal pre-conditions

- Reward token and LP token are cheap (so the overflow scenario is acheivable)
- totalShares is small (e.g. 1 wei).

### External pre-conditions

N/A

### Attack Path

Deposit rewards when `totalShares` is very small and inflate `_rewardsPerShare[]`. This would then eventually cause an overflow if a user interacts with TokenRewards.sol, e.g. when transferring spTKN or staking/unstaking them.

### Impact

The overflow would prevent the user from unstaking his spTKN, locking up his funds.

### PoC

N/A

### Mitigation

Use a smaller PRECISION. 1e18 is more than enough.