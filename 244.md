Scrawny Mahogany Boa

High

# Users'assets losses due to incorrect calculation of `vaultUtilization[_vault]` in the contract `LendingAssetVault`

### Summary

In the contract `LendingAssetVault`, the function `_updateAssetMetadataFromVault` will update the value of `vaultUtilization[_vault])` from based on the CBR updates from last check. When the _vault has bad debt which leads to the situation that `_prevVaultCbr > _vaultWhitelistCbr[_vault]`, the value of `vaultUtilization[_vault]` will be calculated to be `old_vaultUtilization[_vault]*(2-_prevVaultCbr / _vaultWhitelistCbr[_vault])`. However, the `vaultUtilization[_vault]` actually represents the `LendingAssetVault`'s amount of assets with accumulated interests in the `_vault`. 

For example, when the value of `_prevVaultCbr / _vaultWhitelistCbr[_vault]` is `2`, the value of `vaultUtilization[_vault]` will be `0` which means the `LendingAssetVault`'s amount of assets with accumulated interests in the `_vault` is `0`. But actually when the value of `_prevVaultCbr / _vaultWhitelistCbr[_vault]` is `2`, the `LendingAssetVault`'s amount of assets with accumulated interests in the `_vault` is actually half of the previous value rather than `0`. 

This will lead to that, the `_totalAssets` of the `LendingAssetVault` is less than the actual amount of the asset that the `LendingAssetVault`, which finally causes the users who deposit in to the `LendingAssetVault` suffer assets' losses.


[LendingAssetVault](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/LendingAssetVault.sol#L287-L305)

```solidity
    function _updateAssetMetadataFromVault(address _vault) internal {
        uint256 _prevVaultCbr = _vaultWhitelistCbr[_vault];
        _vaultWhitelistCbr[_vault] = IERC4626(_vault).convertToAssets(PRECISION);
        if (_prevVaultCbr == 0) {
            return;
        }
        uint256 _vaultAssetRatioChange = _prevVaultCbr > _vaultWhitelistCbr[_vault]
            ? ((PRECISION * _prevVaultCbr) / _vaultWhitelistCbr[_vault]) - PRECISION
            : ((PRECISION * _vaultWhitelistCbr[_vault]) / _prevVaultCbr) - PRECISION;

        uint256 _currentAssetsUtilized = vaultUtilization[_vault];
        uint256 _changeUtilizedState = (_currentAssetsUtilized * _vaultAssetRatioChange) / PRECISION;
        vaultUtilization[_vault] = _prevVaultCbr > _vaultWhitelistCbr[_vault]
            ? _currentAssetsUtilized < _changeUtilizedState ? 0 : _currentAssetsUtilized - _changeUtilizedState
            : _currentAssetsUtilized + _changeUtilizedState;
        _totalAssetsUtilized = _totalAssetsUtilized - _currentAssetsUtilized + vaultUtilization[_vault];
        _totalAssets = _totalAssets - _currentAssetsUtilized + vaultUtilization[_vault];
        emit UpdateAssetMetadataFromVault(_vault, _totalAssets, _totalAssetsUtilized);
    }
```


### Root Cause

When the _vault has bad debt which leads to the situation that `_prevVaultCbr > _vaultWhitelistCbr[_vault]`, the value of `vaultUtilization[_vault]` will be calculated to be `old_vaultUtilization[_vault]*(2-_prevVaultCbr / _vaultWhitelistCbr[_vault])` which is incorrect. The correct value of `vaultUtilization[_vault]` should be `old_vaultUtilization[_vault]*(_vaultWhitelistCbr[_vault] / _prevVaultCbr)`.


### Internal Pre-conditions

N/A

### External Pre-conditions

N/A

### Attack Path

N/A

### Impact

This will lead to that, the `_totalAssets` of the `LendingAssetVault` is less than the actual amount of the asset that the `LendingAssetVault`, which finally causes the users who deposit in to the `LendingAssetVault` suffer assets' losses.


### PoC

1. The `_totalAssets` of the `LendingAssetVault` is 0.

2. A user deposit `1000e18` assets and mint `1000e18` shares.

2. The `_totalAssets` of the `LendingAssetVault` is 1000e18.

3. The vault1 is one vault that in the `LendingAssetVault`'s `vaultWhitelist`.
    `vaultUtilization[vault1]`=0
    `_vaultWhitelistCbr[vault1]`=0

4. The vault1 invokes the function `whitelistWithdraw` to withdraw the 1000e18 asset from the `LendingAssetVault` and currently the `IERC4626(vault1).convertToAssets(PRECISION)` is a.
    `vaultUtilization[vault1]`=1000e18.
    `_vaultWhitelistCbr[vault1]`= a 
    `_totalAssets`= 1000e18

5. The vault1 has bad debts in liquidation, causing the `IERC4626(vault1).convertToAssets(PRECISION)` to be a/2.

6. The user withdraw assets from the `LendingAssetVault`.
    `_vaultWhitelistCbr[vault1]`= a/2
    `vaultUtilization[vault1]`=1000e18*(2-(a/(a/2)))=0
    `_totalAssets` = 1000e18 - 1000e18 + 0 = 0

    Finally, the user burn `1000e18` shares but get 0 assets.
7. In the meanwhile, actually there still exists `500e18` assets in the vault1.


### Mitigation

Change the calculation of the `_vaultAssetRatioChange` to be `PRECISION - ((PRECISION * _vaultWhitelistCbr[_vault]) / _prevVaultCbr)` when `_prevVaultCbr > _vaultWhitelistCbr[_vault]`.

[LendingAssetVault](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/LendingAssetVault.sol#L287-L305)

```solidity
    function _updateAssetMetadataFromVault(address _vault) internal {
        uint256 _prevVaultCbr = _vaultWhitelistCbr[_vault];
        _vaultWhitelistCbr[_vault] = IERC4626(_vault).convertToAssets(PRECISION);
        if (_prevVaultCbr == 0) {
            return;
        }
        uint256 _vaultAssetRatioChange = _prevVaultCbr > _vaultWhitelistCbr[_vault]
            ? PRECISION - ((PRECISION * _vaultWhitelistCbr[_vault]) / _prevVaultCbr)
            : ((PRECISION * _vaultWhitelistCbr[_vault]) / _prevVaultCbr) - PRECISION;

        uint256 _currentAssetsUtilized = vaultUtilization[_vault];
        uint256 _changeUtilizedState = (_currentAssetsUtilized * _vaultAssetRatioChange) / PRECISION;
        vaultUtilization[_vault] = _prevVaultCbr > _vaultWhitelistCbr[_vault]
            ? _currentAssetsUtilized < _changeUtilizedState ? 0 : _currentAssetsUtilized - _changeUtilizedState
            : _currentAssetsUtilized + _changeUtilizedState;
        _totalAssetsUtilized = _totalAssetsUtilized - _currentAssetsUtilized + vaultUtilization[_vault];
        _totalAssets = _totalAssets - _currentAssetsUtilized + vaultUtilization[_vault];
        emit UpdateAssetMetadataFromVault(_vault, _totalAssets, _totalAssetsUtilized);
    }
```