Magic Fuchsia Guppy

High

# The `LendingAssetVault` will incorrectly account when cbr or vault is decreased

### Summary

The incorrect accounting in `LendingAssetVault.sol::_updateAssetMetadataFromVault` in the case of decreasing cbr will cause unexpected issues.

The decrease in `cbr` of the vault may happen when the Fraxlend pair has a bad debt.


### Root Cause

The `LendingAssetVault.sol::_updateAssetMetadataFromVault` keeps the track of the vault shares evaluation in assets:
https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/LendingAssetVault.sol#L287-L305

It does it by calculating the change in ratio from the current cbr to the new cbr:

```solidity
        uint256 _vaultAssetRatioChange = _prevVaultCbr > _vaultWhitelistCbr[_vault]
            ? ((PRECISION * _prevVaultCbr) / _vaultWhitelistCbr[_vault]) - PRECISION
            : ((PRECISION * _vaultWhitelistCbr[_vault]) / _prevVaultCbr) - PRECISION;

        uint256 _currentAssetsUtilized = vaultUtilization[_vault];
        uint256 _changeUtilizedState = (_currentAssetsUtilized * _vaultAssetRatioChange) / PRECISION;
        vaultUtilization[_vault] = _prevVaultCbr > _vaultWhitelistCbr[_vault]
            ? _currentAssetsUtilized < _changeUtilizedState ? 0 : _currentAssetsUtilized - _changeUtilizedState
            : _currentAssetsUtilized + _changeUtilizedState;
```

It is easier to work with actual numbers, so let's calculate with numbers (just for convenient using smaller decimals, but in reality the PRECISION would be likely to be larger like 1e18).

----------

Example case of cbr increase:
- `_prevVaultCbr` = 100
- new vault cbr from `convertToAssets(PRECISION)` = 120
- the previous `vaultUtilization` is `1e20=100e18`

-> `_vaultAssetRatioChanget` is the second case (increase in cbr)
  = (PRECISION * 120) / 100 - PRECISION =  PRECISION * 20 / 100

-> `_changeUtilizedState` = vaultUtilization * `_vaultAssetRatioChange` / PRECISION
  = `100e18` * PRECISION * 20 / 100 / PRECISION = 20e18

-> the new vaultUtilization will be calculated using the second case (increase in cbr)
  = `_currentAssetsUtilized` + `_changeUtilizedState`
  = 100e18 + 20e18 = `120e18`

---> this correctly count that the vault's holding increased by 20 percent.

----------


Now the example case of cbr decrease:
- `_prevVaultCbr` = 120
- new vault cbr from `convertToAssets(PRECISION)` = 100
- the previous `vaultUtilization` is `120e18`

-> `_vaultAssetRatioChanget` is the first case (decrease in cbr)
  = (PRECISION * `_prevVaultCbr`) / `_vaultWhitelistCbr` - PRECISION
  = (PRECISION * 120) / 100 - PRECISION =  PRECISION * 20 / 100

-> `_changeUtilizedState` = vaultUtilization * `_vaultAssetRatioChange` / PRECISION
  = `120e18` * PRECISION * 20 / 100 / PRECISION = 24e18

-> the new vaultUtilization will be calculated using the first case (decrease in cbr)
  = `_currentAssetsUtilized` - `_changeUtilizedState`
  = 120e18 - 24e18 = `96e18`

---> However, this should be `100e18`. Because in the beginning this vault has 1 share which was 120e18, since the evaluation of share should proportionally decrease as the cbr decreases, hence 100e18.

----------



Whenever there is decrease in `cbr`, the utilization of the vault will decreased more than real decrease. As the result, the vault will underestimate in `vaultUtilization` and `_totalAssetsUtilized` and `_totalAssets`.

Since the `_updateAssetMetadataFromVault` always tracks the utilization via ratio change, this mismatch can never corrected, even the `cbr` increases in the future.



### Internal Pre-conditions

- The `LendingAssetVault` has a whitelisted vault with potential `cbr` decrease. If the Fraxlend pair is used, this will happen when the pair has bad debts.


### External Pre-conditions

- the vault to track gets bad debts


### Attack Path

Attacker can buy shares in discount before redeem from vault, for example.


### Impact

The `vaultUtilization`, `_totalAssetsUtilized`, and `_totalAssets` will be incorrectly underestimated.

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/LendingAssetVault.sol#L303

The `_totalAssets` is used to calculate `_cbr`, which in turn will be used to convert between shares and assets.
So overall shares will be discounted.

Also if the `vaultUtilization` is decreased below `vaultDeposits` of the vault, it may happen that the `vaultDeposits` has lingering value, even though all the shares are redeemed:
https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/LendingAssetVault.sol#L325-L334

These incorrect accounting with `vaultUtilization` and possibly `vaultDeposits` will make the `totalAvailableAssetsForVault` incorrect. This is problematic since they are used to determine allocation and assess overutilization, both for this particular vault and the total utilization across all vaults.

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/fraxlend/src/contracts/libraries/VaultAccount.sol#L20



### PoC

The below PoC demonstrates the deviation of real vault utilization and the accounting of it in the LendingAssetVault.

For ease of simulating bad debt the following function is added the mock ERC4626 vault:

```solidity
// ADDED FUNCTION TO test/mocks/TestERC4626Vault.sol
    function simulateBadDebt(uint assetsSent) public {
      IERC20(_asset).safeTransfer(address(0xdead), assetsSent);
    }
```

The following test function is based on the `test_vaultDepositAndWithdrawWithCbrChange`.
It logs the "real" lendingAssetVault's utilization by calculating the lendingAssetVault's holding of `_testVault`. It prints out both share amounts as well as the converted assets value.
The `vaultUtilization` is supposed to keep track of the assets evaluation of the shares holding.

After the `cbr` increases, the `vaultUtilization` is correctly updated. However once the `cbr` decreases via the `simulateBadDebt`the `vaultUtilization` deviates from the real evaluation.

```solidity
Ran 1 test for test/LendingAssetVault.t.sol:LendingAssetVaultTest
[PASS] test_vaultDepositAndWithdrawWithCbrChange_POC() (gas: 476376)
Logs:
  lendingAssetsVault's holding in testVault as shares amount: 2500000000000000000
  lendingAssetsVault's holding in testVault as assets amount: 5000000000000000000
  vaultUtilization: 5000000000000000000
  the assets in testVault is increased
  and the vaultUtilization is updated
  lendingAssetsVault's holding in testVault as shares amount: 2500000000000000000
  lendingAssetsVault's holding in testVault as assets amount: 10000000000000000000
  vaultUtilization: 10000000000000000000
  the assets in testVault is reduced to simulate bad debt
  and the vaultUtilization is updated
  lendingAssetsVault's holding in testVault as shares amount: 2500000000000000000
  lendingAssetsVault's holding in testVault as assets amount: 6666666666666666665
  vaultUtilization: 4999999999999999997
```

```solidity
    function test_vaultDepositAndWithdrawWithCbrChange_POC() public {
        address[] memory vaults = new address[](1);
        vaults[0] = address(_testVault);
        uint256[] memory percentages = new uint256[](1);
        percentages[0] = 10e18;
        _lendingAssetVault.setVaultMaxAllocation(vaults, percentages);

        uint256 _lavDepAmt = 10e18;
        uint256 _extDepAmt = _lavDepAmt / 2;
        _lendingAssetVault.deposit(_lavDepAmt, address(this));
        assertEq(_lendingAssetVault.totalSupply(), _lavDepAmt);

        _testVault.depositFromLendingAssetVault(address(_lendingAssetVault), _extDepAmt);
        _asset.transfer(address(_testVault), _extDepAmt);
        _testVault.withdrawToLendingAssetVault(address(_lendingAssetVault), _extDepAmt);

        vm.roll(block.timestamp + 1);
        _lendingAssetVault.withdraw(_lavDepAmt / 2, address(this), address(this));

        uint256 _optimalBal = _asset.totalSupply() - _lavDepAmt / 2 - _extDepAmt;
        assertEq(_asset.balanceOf(address(this)), _optimalBal);

        uint sharesHolding = _testVault.balanceOf(address(_lendingAssetVault));
        uint assetsHolding = _testVault.convertToAssets(sharesHolding);
        emit log_named_uint("lendingAssetsVault's holding in testVault as shares amount", sharesHolding);
        emit log_named_uint("lendingAssetsVault's holding in testVault as assets amount", assetsHolding);
        emit log_named_uint("vaultUtilization", _lendingAssetVault.vaultUtilization(address(_testVault)));
        /// @audit: simulate badDebt by getting assets out of _testVault
        emit log("the assets in testVault is increased");
        emit log("and the vaultUtilization is updated");
        _asset.transfer(address(_testVault), _extDepAmt);
        vm.startPrank(address(_testVault));
        _lendingAssetVault.whitelistUpdate(true);
        vm.stopPrank();

        sharesHolding = _testVault.balanceOf(address(_lendingAssetVault));
        assetsHolding = _testVault.convertToAssets(sharesHolding);
        emit log_named_uint("lendingAssetsVault's holding in testVault as shares amount", sharesHolding);
        emit log_named_uint("lendingAssetsVault's holding in testVault as assets amount", assetsHolding);
        emit log_named_uint("vaultUtilization", _lendingAssetVault.vaultUtilization(address(_testVault)));

        /// @audit: simulate badDebt by getting assets out of _testVault
        emit log("the assets in testVault is reduced to simulate bad debt");
        emit log("and the vaultUtilization is updated");
        _testVault.simulateBadDebt(_testVault.totalAssets() / 3);
        vm.startPrank(address(_testVault));
        _lendingAssetVault.whitelistUpdate(true);
        vm.stopPrank();

        sharesHolding = _testVault.balanceOf(address(_lendingAssetVault));
        assetsHolding = _testVault.convertToAssets(sharesHolding);
        emit log_named_uint("lendingAssetsVault's holding in testVault as shares amount", sharesHolding);
        emit log_named_uint("lendingAssetsVault's holding in testVault as assets amount", assetsHolding);
        emit log_named_uint("vaultUtilization", _lendingAssetVault.vaultUtilization(address(_testVault)));
    }
```


### Mitigation

Correct the formula of `_updateAssetMetadataFromVault`
