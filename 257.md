Perfect Porcelain Snail

High

# LVF feature inoperable due to missing `convertToAssets()` on already deployed Pods

### Summary

The absence of a `convertToAssets()` implementation in already deployed pods will cause critical LVF operations to revert. This prevents end-users from calculating prices (via `getPrices()` or `getPodPerBasePrice()`) because `_accountForCBRInPrice()` calls `convertToAssets()`. Consequently, LVF support cannot be enabled on legacy pods, blocking access to leveraged volatility farming benefits

### Root Cause

LVF is the main new feature of the release [docs](https://medium.com/@peapodsfinance).
It clearly say that it's possible to use LVF on already deployed pod [example : Point of View; Volatility Farmer](https://medium.com/@peapodsfinance/lvf-the-r-evolution-of-peapods-69a82687cc68).

[`addLvfSupportForPod()`](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/lvf/LeverageFactory.sol#L118C14-L118C33) function implies LVF should be supported on deployed pods.
  ```Solidity
    function addLvfSupportForPod(
        address _pod,
        address _dexAdapter,
        address _indexUtils,
        bytes memory _aspTknOracleRequiredImmutables,
        bytes memory _aspTknOracleOptionalImmutables,
        bytes memory _fraxlendPairConfigData
    ) external onlyOwner returns (address _aspTkn, address _aspTknOracle, address _fraxlendPair) {
        address _borrowTkn = IDecentralizedIndex(_pod).PAIRED_LP_TOKEN();
        require(ILeverageManagerAccessControl(leverageManager).flashSource(_borrowTkn) != address(0), "FS2");
        uint256 _aspMinDep = IAspTknFactory(aspTknFactory).minimumDepositAtCreation();
        if (_aspMinDep > 0) {
            address _spTkn = IDecentralizedIndex(_pod).lpStakingPool();
            IERC20(_spTkn).safeTransferFrom(_msgSender(), address(this), _aspMinDep);
            IERC20(_spTkn).safeIncreaseAllowance(aspTknFactory, _aspMinDep);
        }
        _aspTkn = _getOrCreateAspTkn(
            "", IERC20Metadata(_pod).name(), IERC20Metadata(_pod).symbol(), _pod, _dexAdapter, _indexUtils, false, false
        );
        _aspTknOracle = IAspTknOracleFactory(aspTknOracleFactory).create(
            _aspTkn, _aspTknOracleRequiredImmutables, _aspTknOracleOptionalImmutables, 0
        );
        _fraxlendPair = _createFraxlendPair(_borrowTkn, _aspTkn, _aspTknOracle, _fraxlendPairConfigData);

        // this effectively is what "turns on" LVF for the pair
        ILeverageManagerAccessControl(leverageManager).setLendingPair(_pod, _fraxlendPair);

        emit AddLvfSupportForPod(_pod, _aspTkn, _aspTknOracle, _fraxlendPair);
    }
  ```

In [spTKNMinimalOracle.sol](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/oracle/spTKNMinimalOracle.sol#L231), the function `_accountForCBRInPrice()` calls `IDecentralizedIndex(_pod).convertToAssets(_pTknAmt)`:
  
  ```solidity
  function _accountForCBRInPrice(address _pod, address _underlying, uint256 _amtUnderlying)
      internal
      view
      returns (uint256)
  {
      require(IDecentralizedIndex(_pod).unlocked() == 1, "OU");
      if (_underlying == address(0)) {
          IDecentralizedIndex.IndexAssetInfo[] memory _assets = IDecentralizedIndex(_pod).getAllAssets();
          _underlying = _assets[0].token;
      }
      uint256 _pTknAmt =
          (_amtUnderlying * 10 ** IERC20Metadata(_pod).decimals()) / 10 ** IERC20Metadata(_underlying).decimals();
      return IDecentralizedIndex(_pod).convertToAssets(_pTknAmt);
  }
  ```
  
 However, in legacy pods, the `convertToAssets()` function is not implemented. 
- pPEAS (10.5M) : https://etherscan.io/token/0x027ce48b9b346728557e8d420fe936a72bf9b1c7#code
- pOHM (1.3M) : https://etherscan.io/token/0x88e08adb69f2618adf1a3ff6cc43c671612d1ca4#code
- apPEAS (840K) : https://arbiscan.io/token/0x6a02f704890f507f13d002f2785ca7ba5bfcc8f7#c

This misconception is due that for all the test, it will take the variables of an already pods and then create a new pod [`_duplicatePod()`](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/test/helpers/PodHelper.t.sol#L97C14-L97C27).

### Internal Pre-conditions

N/A

### External Pre-conditions

N/A

### Attack Path

1. A user  attempts to enable LVF on a legacy pod by calling the `addLvfSupportForPod()` method in the `LeverageFactory` contract.
2. During LVF initialization, the system calls `_accountForCBRInPrice()` (during the `_createFraxlendPair()`), which then calls `IDecentralizedIndex(_pod).convertToAssets(_pTknAmt)`.
3. Since the legacy pod does not implement `convertToAssets()`, the call reverts, halting further LVF processes and rendering LVF features unusable.

### Impact

 End-users and liquidity providers using legacy pods will be unable to access LVF features, leading to missed yield opportunities and halted LVF operations. This critical functional regression severely undermines protocol upgradeability and the overall utility of the new LVF mechanism.

### PoC

The following PoC demonstrates the issue:

[poc_convert_asserts_revert.t.sol.txt](https://github.com/user-attachments/files/18799438/poc_convert_asserts_revert.t.sol.txt)

`forge test -vvvvv --match-path test/poc_convert_asserts_revert.t.sol --fork-url https://rpc.flashbots.net/fast`

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.28;


import "forge-std/Test.sol";
import "../contracts/interfaces/IDecentralizedIndex.sol";

contract POCConvertToAssetsRevert is Test {
    // pOHM pod legacy address on Ethereum Mainnet (as per Etherscan)
    // https://etherscan.io/token/0x88e08adb69f2618adf1a3ff6cc43c671612d1ca4#code
    IDecentralizedIndex constant pOHM = IDecentralizedIndex(address(0x88E08adB69f2618adF1A3FF6CC43c671612D1ca4));
    address constant PAIRED_LP_TOKEN = address(0x6B175474E89094C44Da98b954EedeAC495271d0F); // DAI
    address constant PARTNER = address(0x73b1961F3fb68EDa8bb66C96FfAF2b17DC9715C6); 
    /// @notice Tests that convertToAssets() reverts on legacy pod pOHM.
    function testConvertToAssetsRevert() public {

        // test if correct contract
        address getPairedLpToken = pOHM.PAIRED_LP_TOKEN();
        assertEq(getPairedLpToken, PAIRED_LP_TOKEN);

        address getPartner = pOHM.partner();
        assertEq(getPartner, PARTNER);

        // does not implement convertToAssets(). 
        vm.expectRevert(bytes(""));
        pOHM.convertToAssets(1e18);
        // ├─ [261] WeightedIndex::convertToAssets(1000000000000000000 [1e18]) [staticcall]
        //      └─ ← [Revert] EvmError: Revert
    }
}
```

