Keen Jetblack Deer

Medium

# AutoCompoundingPodLp may double collect protocolFee for pairedLpTKN.


### Summary

AutoCompoundingPodLp may double collect protocolFee for pairedLpTKN.

### Root Cause

First, let's see how the autocompounding process works: 1) swap rewardTKN -> pairedLpTKN, 2) swap a portion (roughly half) of pairedLpTKN -> pTKN, 3) add pairedLpTKN, pTKN to UniV2 LP, 4) stake LP token to spTKN.

If some of the steps failed due to slippage, pairedLpTKN would be leftover in the contract. Then, we need to start from rewardTKN=pairedLpTKN in the next autocompounding phase again.

The bug here is, the protocolFee is collected after step 1, and it will be collect multiple times for pairedLpTKN.

For example:

1. The first autocompound begins with 1000 tokenA. It successfully swaps to 1000 pairedLpTK, and a protocolFee of 10% is collected, so there are 900 pairedLpTKN left. The autocompounding fails in future steps (e.g. due to slippage), so the 900 pairedLpTKN remains in the contract.
2. The second autocompound begins with 900 pairedLpTKN. A 10% protocolFee will be collected again, so we only have 810 pairedLpTKN left.

This process can go on and on in the worst case scenario if autocompounding fails a lot of times, eventually all pairedLpTKNs will be fees.

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/AutoCompoundingPodLp.sol#L213

```solidity
    function _processRewardsToPodLp(uint256 _amountLpOutMin, uint256 _deadline) internal returns (uint256 _lpAmtOut) {
        if (!yieldConvEnabled) {
            return _lpAmtOut;
        }
        address[] memory _tokens = ITokenRewards(IStakingPoolToken(_asset()).POOL_REWARDS()).getAllRewardsTokens();
        uint256 _len = _tokens.length + 1;
        for (uint256 _i; _i < _len; _i++) {
            address _token = _i == _tokens.length ? pod.lpRewardsToken() : _tokens[_i];

            uint256 _bal =
                IERC20(_token).balanceOf(address(this)) - (_token == pod.PAIRED_LP_TOKEN() ? _protocolFees : 0);
            if (_bal == 0) {
                continue;
            }
@>          uint256 _newLp = _tokenToPodLp(_token, _bal, 0, _deadline);
            _lpAmtOut += _newLp;
        }
        _totalAssets += _lpAmtOut;
        require(_lpAmtOut >= _amountLpOutMin, "M");
    }

    function _tokenToPodLp(address _token, uint256 _amountIn, uint256 _amountLpOutMin, uint256 _deadline)
        internal
        returns (uint256 _lpAmtOut)
    {
        uint256 _pairedOut = _tokenToPairedLpToken(_token, _amountIn);
        if (_pairedOut > 0) {
            // @audit-bug: Protocol fee will be collected again for the same amount pairedLpTKN.
@>          uint256 _pairedFee = (_pairedOut * protocolFee) / 1000;
            if (_pairedFee > 0) {
                _protocolFees += _pairedFee;
                _pairedOut -= _pairedFee;
            }
            _lpAmtOut = _pairedLpTokenToPodLp(_pairedOut, _deadline);
            require(_lpAmtOut >= _amountLpOutMin, "M");
        }
    }

    function _tokenToPairedLpToken(address _token, uint256 _amountIn) internal returns (uint256 _amountOut) {
        address _pairedLpToken = pod.PAIRED_LP_TOKEN();
        address _swapOutputTkn = _pairedLpToken;
        // @audit-note: Automatically skip rewardTKN -> pairedLpTKN swap if they are the same.
        if (_token == _pairedLpToken) {
@>          return _amountIn;
        } else if (maxSwap[_token] > 0 && _amountIn > maxSwap[_token]) {
            _amountIn = maxSwap[_token];
        }
        ...
    }

    function _pairedLpTokenToPodLp(uint256 _amountIn, uint256 _deadline) internal returns (uint256 _amountOut) {
        address _pairedLpToken = pod.PAIRED_LP_TOKEN();
        uint256 _pairedSwapAmt = _getSwapAmt(_pairedLpToken, address(pod), _pairedLpToken, _amountIn);
        uint256 _pairedRemaining = _amountIn - _pairedSwapAmt;
        uint256 _minPtknOut;
        if (address(podOracle) != address(0)) {
            // calculate the min out with 5% slippage
            _minPtknOut = (
                podOracle.getPodPerBasePrice() * _pairedSwapAmt * 10 ** IERC20Metadata(address(pod)).decimals() * 95
            ) / 10 ** IERC20Metadata(_pairedLpToken).decimals() / 10 ** 18 / 100;
        }
        IERC20(_pairedLpToken).safeIncreaseAllowance(address(DEX_ADAPTER), _pairedSwapAmt);

        // @audit-note: This may fail due to slippage.
@>      try DEX_ADAPTER.swapV2Single(_pairedLpToken, address(pod), _pairedSwapAmt, _minPtknOut, address(this)) returns (
            uint256 _podAmountOut
        ) {
            // reset here to local balances to accommodate any residual leftover from previous runs
            _podAmountOut = pod.balanceOf(address(this));
            _pairedRemaining = IERC20(_pairedLpToken).balanceOf(address(this)) - _protocolFees;
            IERC20(pod).safeIncreaseAllowance(address(indexUtils), _podAmountOut);
            IERC20(_pairedLpToken).safeIncreaseAllowance(address(indexUtils), _pairedRemaining);

            // @audit-note: This may fail due to slippage.
            try indexUtils.addLPAndStake(
@>              pod, _podAmountOut, _pairedLpToken, _pairedRemaining, _pairedRemaining, lpSlippage, _deadline
            ) returns (uint256 _lpTknOut) {
                _amountOut = _lpTknOut;
            } catch {
                IERC20(pod).safeDecreaseAllowance(address(indexUtils), _podAmountOut);
                IERC20(_pairedLpToken).safeDecreaseAllowance(address(indexUtils), _pairedRemaining);
                emit AddLpAndStakeError(address(pod), _amountIn);
            }
        } catch {
            IERC20(_pairedLpToken).safeDecreaseAllowance(address(DEX_ADAPTER), _pairedSwapAmt);
            emit AddLpAndStakeV2SwapError(_pairedLpToken, address(pod), _pairedRemaining);
        }
    }
```

### Internal pre-conditions

N/A

### External pre-conditions

- Autocompounding fails due to slippage, leaving pairedLpTKN inside AutoCompoundingPodLP contract.

### Attack Path

No attack path required. Explained above.

### Impact

Fees for pairedLpTKN would be collected multiple times.

### PoC

N/A

### Mitigation

Don't collect fees for pairedLpTKN that already processed fees.
