Perfect Porcelain Snail

Medium

# Balancer governance flashloan fee activation breaks Balancer flashloan execution

### Summary

If Balancer Governance activates the flashloan fee, it will be impossible for users to execute flashloans through `BalancerFlashSource.sol` because the fee is not properly forwarded. As a result, vital operations (`addLeverage` or `removeLeverage`) will revert, affecting users who depend on flashloan for leverage.

### Root Cause

In [BalancerFlashSource.sol](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/flash/BalancerFlashSource.sol#L56) the flashloan fee is not properly returned to the callback:
  - The `receiveFlashLoan` function decodes the flashloan data and sets the fee in the local variable `_fData` but then calls the recipient callback without re-encoding the updated data.
    ```solidity
    function receiveFlashLoan(
        IERC20[] memory,
        uint256[] memory,
        uint256[] memory _feeAmounts,
        bytes memory _userData
    )
        external
        override
        workflow(false)
    {
        require(_msgSender() == source, "CBV");
        FlashData memory _fData = abi.decode(_userData, (FlashData));
        _fData.fee = _feeAmounts[0];
        IERC20(_fData.token).safeTransfer(_fData.recipient, _fData.amount);
        IFlashLoanRecipient(_fData.recipient).callback(_userData);
    }
    ```
As the callback is still invoked with the original `_userData` (which lacks the updated fee information), any changes in the fee (as would happen if flashloan fees are activated) result in an underpayment of the fee, causing the flashloan to revert.

### Internal Pre-conditions

1. `BalancerFlashSource` is set for the borrowed token.

### External Pre-conditions

1. Balancer Governance activates flashloan fees, as detailed in [Balancer Protocol Fees Documentation](https://docs-v2.balancer.fi/concepts/governance/protocol-fees.html#flash-loan-fees).
2. The external Balancer contract (via [_calculateFlashLoanFeeAmount()](https://github.com/balancer/balancer-v2-monorepo/blob/36d282374b457dddea828be7884ee0d185db06ba/pkg/vault/contracts/FlashLoans.sol#L60)) returns a non-zero fee in the flashloan, which must be repaid along with the principal.

### Attack Path

1. Balancer Governance activates flashloan fees.
2. A user invokes a flashloan-dependent function (e.g., `addLeverage` or `removeLeverage`) using BalancerFlashSource.
3. During flashloan execution, the Balancer vault calculates a fee based on the requested amount.
4. The `receiveFlashLoan` function in BalancerFlashSource updates the local fee field but fails to re-encode the updated FlashData when calling the recipient callback.
5. The recipient receives outdated data without the actual fee, causing the loan repayment to miss the fee.
6. As a result, the flashloan repayment check fails, reverting the transaction.

### Impact

Users will be unable to successfully execute Balancer flashloan operations if the flashloan fee is activated by governance. This means that any operation relying on a flashloan (`addLeverage` or `removeLeverage`) will revert due to an insufficient fee repayment, essentially rendering the BalancerFlashSource unusable under these conditions.

### PoC

N/A

### Mitigation

Modify the `receiveFlashLoan()` function to ensure that the recipient receives the updated FlashData including the fee by passing the updated `_fData`:

```diff
function receiveFlashLoan(
    IERC20[] memory,
    uint256[] memory,
    uint256[] memory _feeAmounts,
    bytes memory _userData
)
    external
    override
    workflow(false)
{
    require(_msgSender() == source, "CBV");
    FlashData memory _fData = abi.decode(_userData, (FlashData));
    _fData.fee = _feeAmounts[0];
    IERC20(_fData.token).safeTransfer(_fData.recipient, _fData.amount);
-   IFlashLoanRecipient(_fData.recipient).callback(_userData);
+   IFlashLoanRecipient(_fData.recipient).callback(abi.encode(_fData));
}
```

This change ensures that the recipient callback correctly receives the fee amount to send.