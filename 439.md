Keen Jetblack Deer

High

# LendingAssetVault `_updateInterestAndMdInAllVaults()`'s update condition is flawed, leading to inaccurate interest.


### Summary

LendingAssetVault `_updateInterestAndMdInAllVaults()`'s update condition is flawed, leading to inaccurate interest.

### Root Cause

When user interacts with LVA, the CBR (collateral back ratio) is always updated first using `_updateInterestAndMdInAllVaults()` function. What `_updateInterestAndMdInAllVaults()` function does is it iterates all vaults (FraxlendPairs), and for each vault that has new interest collected, it triggers a `_updateAssetMetadataFromVault()` call to update the `_totalAssets` and `_totalAssetsUtilized`, which is used to calculate the CBR for LendingAssetVault.

The bug here is the trigger logic within `_updateInterestAndMdInAllVaults()`. Currently it checks if there is new interest collected in a FraxlendPair. However, there are scenarios where the new interest are not collected, but FraxlendPair asset/share ratio changes a lot. In these scenarios, the CBR of LendingAssetVault should be updated as well, but they currently are not. For example:

1. In the same transaction block, a large liquidation first happens, which leads to a lot of bad debt, greatly reducing the asset/share ratio of the FraxlendPair. Then in the same transaction block, a user interacts with LVA. Since interest was already updated for the FraxlendPair (during the liquidation transaction), `_interestEarned` would return false, and `_updateAssetMetadataFromVault()` is not triggered, so the user still uses the old CBR rate for calculation.

2. Since `Fraxlend#addInterest()` has a cache mechanism based on utilization, if FraxlendPair's asset/share ratio changed a lot, as long as utilization doesn't change (e.g. interest accure leads to utilization rate increase, user deposit leads to utilization rate decrease, so net utilization diff is zero), then `_interestEarned` would still be false, and CBR update would not be triggered.

In these scenarios, users would be using an old CBR rate, which would lead to either loss of funds (if CBR is lesser than expected), or earn more interest than they are deserved (if CBR is larger than expected).

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/LendingAssetVault.sol#L1

```solidity
    function withdraw(uint256 _assets, address _receiver, address _owner) external override returns (uint256 _shares) {
        _updateInterestAndMdInAllVaults(address(0));
        _shares = convertToShares(_assets);
        _withdraw(_shares, _assets, _owner, _msgSender(), _receiver);
    }

    function _updateInterestAndMdInAllVaults(address _vaultToExclude) internal {
        uint256 _l = _vaultWhitelistAry.length;
        for (uint256 _i; _i < _l; _i++) {
            address _vault = _vaultWhitelistAry[_i];
            if (_vault == _vaultToExclude) {
                continue;
            }
            (uint256 _interestEarned,,,,,) = IFraxlendPair(_vault).addInterest(false);
@>          if (_interestEarned > 0) {
                // @audit-bug: This may not be called when should be.
                _updateAssetMetadataFromVault(_vault);
            }
        }
    }

    function _updateAssetMetadataFromVault(address _vault) internal {
        uint256 _prevVaultCbr = _vaultWhitelistCbr[_vault];
        _vaultWhitelistCbr[_vault] = IERC4626(_vault).convertToAssets(PRECISION);
        if (_prevVaultCbr == 0) {
            return;
        }
        uint256 _vaultAssetRatioChange = _prevVaultCbr > _vaultWhitelistCbr[_vault]
            ? ((PRECISION * _prevVaultCbr) / _vaultWhitelistCbr[_vault]) - PRECISION
            : ((PRECISION * _vaultWhitelistCbr[_vault]) / _prevVaultCbr) - PRECISION;

        uint256 _currentAssetsUtilized = vaultUtilization[_vault];
        uint256 _changeUtilizedState = (_currentAssetsUtilized * _vaultAssetRatioChange) / PRECISION;
        vaultUtilization[_vault] = _prevVaultCbr > _vaultWhitelistCbr[_vault]
            ? _currentAssetsUtilized < _changeUtilizedState ? 0 : _currentAssetsUtilized - _changeUtilizedState
            : _currentAssetsUtilized + _changeUtilizedState;
        _totalAssetsUtilized = _totalAssetsUtilized - _currentAssetsUtilized + vaultUtilization[_vault];
        _totalAssets = _totalAssets - _currentAssetsUtilized + vaultUtilization[_vault];
        emit UpdateAssetMetadataFromVault(_vault, _totalAssets, _totalAssetsUtilized);
    }
```

### Internal pre-conditions

Either of the following leading to `_updateAssetMetadataFromVault()` not triggered.

1. FraxlendPair asset/share ratio changes, but utilization does not change.
2. FraxlendPair asset/share ratio changes, but interest is not updated because interest was already updated in a previous transaction of the same transaction block.

### External pre-conditions

N/A

### Attack Path

N/A

### Impact

Users would be using an old CBR rate, which would lead to either loss of funds (if CBR is lesser than expected), or earn more interest than they are deserved (if CBR is larger than expected).

### PoC

N/A

### Mitigation

Remove the check, or maintain a asset/share ratio of the for each FraxlendPair in LVA, and only call `_updateAssetMetadataFromVault()` if it changes.