Keen Jetblack Deer

Medium

# AutoCompoundingPodLp `_tokenToPairedLpToken()` should swap directly from PEAS to pairedLpTKN for self-lending pods.


### Summary

AutoCompoundingPodLp `_tokenToPairedLpToken()` should swap directly from PEAS to pairedLpTKN for self-lending pods.

### Root Cause

First, let's see how the autocompounding process works: 1) swap rewardTKN -> pairedLpTKN, 2) swap a portion (roughly half) of pairedLpTKN -> pTKN, 3) add pairedLpTKN, pTKN to UniV2 LP, 4) stake LP token to spTKN.

In step 1, it is handled differently if rewardTKN was PEAS (lpRewardsTKN): 1) If rewardTKN != lpRewardsTKN, it would use a UniV2 swap; 2) If rewardTKN == lpRewardsTKN, it would use a UniV3 swap with a fixed 1% fee pool.

After consulting with the sponsors, the reasoning is they always commit to maintain a Uniswap 1% fee pool PEAS/pairedLpTKN for supported pods (lpRewardsTKN is always PEAS), so the swap should be done here directly.

The issue is, for self-lending pods, we should still be doing a PEAS -> pairedLpTKN swap in UniV3 pool. But the target token `_swapOutputTkn` is actually the underlying token for the pairedLpTKN (e.g. for fUSDC, it would be USDC). This is the correct target token in UniV2 pool swaps (for better liquidity), but is incorrect for UniV3 pool swaps, and it should directly swap to pairedLpTKN.

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/AutoCompoundingPodLp.sol#L213

```solidity
    function _tokenToPodLp(address _token, uint256 _amountIn, uint256 _amountLpOutMin, uint256 _deadline)
        internal
        returns (uint256 _lpAmtOut)
    {
@>      uint256 _pairedOut = _tokenToPairedLpToken(_token, _amountIn);
        if (_pairedOut > 0) {
            uint256 _pairedFee = (_pairedOut * protocolFee) / 1000;
            if (_pairedFee > 0) {
                _protocolFees += _pairedFee;
                _pairedOut -= _pairedFee;
            }
            _lpAmtOut = _pairedLpTokenToPodLp(_pairedOut, _deadline);
            require(_lpAmtOut >= _amountLpOutMin, "M");
        }
    }

    function _tokenToPairedLpToken(address _token, uint256 _amountIn) internal returns (uint256 _amountOut) {
        address _pairedLpToken = pod.PAIRED_LP_TOKEN();
        address _swapOutputTkn = _pairedLpToken;
        if (_token == _pairedLpToken) {
            return _amountIn;
        } else if (maxSwap[_token] > 0 && _amountIn > maxSwap[_token]) {
            _amountIn = maxSwap[_token];
        }

        // if self lending pod, we need to swap for the lending pair borrow token,
        // then deposit into the lending pair which is the paired LP token for the pod
        if (IS_PAIRED_LENDING_PAIR) {
@>          _swapOutputTkn = IFraxlendPair(_pairedLpToken).asset();
        }

        address _rewardsToken = pod.lpRewardsToken();

        // @audit-note: If rewardTKN was lpRewardsTKN, use UniV2 swap. Or else use UniV3 swap.
@>      if (_token != _rewardsToken) {
            _amountOut = _swap(_token, _swapOutputTkn, _amountIn, 0);
            if (IS_PAIRED_LENDING_PAIR) {
                _amountOut = _depositIntoLendingPair(_pairedLpToken, _swapOutputTkn, _amountOut);
            }
            return _amountOut;
        }
        uint256 _amountInOverride = _tokenToPairedSwapAmountInOverride[_rewardsToken][_swapOutputTkn];
        if (_amountInOverride > 0) {
            _amountIn = _amountInOverride;
        }
        uint256 _minSwap = 10 ** (IERC20Metadata(_rewardsToken).decimals() / 2);
        _minSwap = _minSwap == 0 ? 10 ** IERC20Metadata(_rewardsToken).decimals() : _minSwap;
        IERC20(_rewardsToken).safeIncreaseAllowance(address(DEX_ADAPTER), _amountIn);

        try DEX_ADAPTER.swapV3Single(
            _rewardsToken,
            // @audit-bug: This is wrong for self-lending pods.
@>          _swapOutputTkn,
            REWARDS_POOL_FEE,
            _amountIn,
            0, // _amountOutMin can be 0 because this is nested inside of function with LP slippage provided
            address(this)
        ) returns (uint256 __amountOut) {
            _tokenToPairedSwapAmountInOverride[_rewardsToken][_swapOutputTkn] = 0;
            _amountOut = __amountOut;

            // if this is a self-lending pod, convert the received borrow token
            // into fTKN shares and use as the output since it's the pod paired LP token
            if (IS_PAIRED_LENDING_PAIR) {
                _amountOut = _depositIntoLendingPair(_pairedLpToken, _swapOutputTkn, _amountOut);
            }
        } catch {
            _tokenToPairedSwapAmountInOverride[_rewardsToken][_swapOutputTkn] =
                _amountIn / 2 < _minSwap ? _minSwap : _amountIn / 2;
            IERC20(_rewardsToken).safeDecreaseAllowance(address(DEX_ADAPTER), _amountIn);
            emit TokenToPairedLpSwapError(_rewardsToken, _swapOutputTkn, _amountIn);
        }
    }
```

### Internal pre-conditions

N/A

### External pre-conditions

N/A

### Attack Path

No attack path required. Explained above.

### Impact

Autocompounding for PEAS reward (lpRewardsTKN) would fail for self-lending pods, leading to loss of rewards.

### PoC

N/A

### Mitigation

Directly swap to pairedLpTKN if using the UniV3 pool swap.
