Beautiful Chartreuse Carp

High

# LeverageManager::addLeverage() will revert if flashloan fee is set

### Summary

When adding leverage using `LeverageManager::addLeverage()` [function](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/lvf/LeverageManager.sol#L81C5-L102C6), flashloan is used. If flashloan source for a particular borrow token (i. e. DAI) is set to `BalanceFlashSource`, and flashloan fee is set by the Balancer protocol, then `LeverageManager::addLeverage()` function will revert because of wrong handling of flashloan fee in `BalancerFlashSource::receiveFlashLoan()` [function](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/flash/BalancerFlashSource.sol#L47C5-L57C6).

### Root Cause

In `LeverageManager::addLeverage()` [function](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/lvf/LeverageManager.sol#L81), `_addLeveragePreCallback()` [function](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/lvf/LeverageManager.sol#L92) is called

```solidity
        _addLeveragePreCallback(
            _msgSender(),
            _positionId,
            _pod,
            IERC20(_pod).balanceOf(address(this)) - _pTknBalBefore,
            _pairedLpDesired,
            _userProvidedDebtAmt,
            _hasSelfLendingPairPod,
            _config
        );
```

which then calls `FlashLoanSource::flash()` [function](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/lvf/LeverageManager.sol#L302)

```solidity
        IFlashLoanSource(_getFlashSource(_positionId)).flash(
            _getBorrowTknForPod(_positionId),
            _pairedLpDesired - _userProvidedDebtAmt,
            address(this),
            _getFlashDataAddLeverage(_positionId, _sender, _pTknAmt, _pairedLpDesired, _config)
        );
```

In `BalancerFlashSource::flash()` [function](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/flash/BalancerFlashSource.sol#L33C5-L45C6), the FlashData struct is constructed, and passed as an argument to `BalancerVault::flashLoan()` function.

```solidity
    function flash(address _token, uint256 _amount, address _recipient, bytes calldata _data)
        external
        override
        workflow(true)
        onlyLeverageManager
    {
        IERC20[] memory _tokens = new IERC20[](1);
        uint256[] memory _amounts = new uint256[](1);
        _tokens[0] = IERC20(_token);
        _amounts[0] = _amount;
@>      FlashData memory _fData = FlashData(_recipient, _token, _amount, _data, 0);
@>      IBalancerVault(source).flashLoan(this, _tokens, _amounts, abi.encode(_fData));
    }
```

In Balancer protocol, `FlashLoans::flashLoan()` [function](https://github.com/balancer/balancer-v2-monorepo/blob/36d282374b457dddea828be7884ee0d185db06ba/pkg/vault/contracts/FlashLoans.sol#L38), `feeAmounts` array in constructed with respective fee amounts of each token and passed as an argument to the `receiveFlashLoan()` callback function.

```solidity
@>      uint256[] memory feeAmounts = new uint256[](tokens.length);
        uint256[] memory preLoanBalances = new uint256[](tokens.length);

        // Used to ensure `tokens` is sorted in ascending order, which ensures token uniqueness.
        IERC20 previousToken = IERC20(0);

        for (uint256 i = 0; i < tokens.length; ++i) {
            IERC20 token = tokens[i];
            uint256 amount = amounts[i];

            _require(token > previousToken, token == IERC20(0) ? Errors.ZERO_TOKEN : Errors.UNSORTED_TOKENS);
            previousToken = token;

            preLoanBalances[i] = token.balanceOf(address(this));
@>          feeAmounts[i] = _calculateFlashLoanFeeAmount(amount);

            _require(preLoanBalances[i] >= amount, Errors.INSUFFICIENT_FLASH_LOAN_BALANCE);
            token.safeTransfer(address(recipient), amount);
        }

@>      recipient.receiveFlashLoan(tokens, amounts, feeAmounts, userData);
```

In Peapods protocol, in `BalancerFlashSource::receiveFlashLoan()` [function](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/flash/BalancerFlashSource.sol#L47C5-L57C6), the FlashData struct is decoded and cached locally in `_fData` variable, the `feeAmount` received from the Balancer call, is updated in `_fData.fee`. Now here is the actual `BUG`,  when calling `LeverageManager::callback()` function, the `_userData` variable is passed as argument instead of the locally cached and updated `_fData` variable.

```solidity
    function receiveFlashLoan(IERC20[] memory, uint256[] memory, uint256[] memory _feeAmounts, bytes memory _userData)
        external
        override
        workflow(false)
    {
        require(_msgSender() == source, "CBV");
@>      FlashData memory _fData = abi.decode(_userData, (FlashData));
@>      _fData.fee = _feeAmounts[0];
        IERC20(_fData.token).safeTransfer(_fData.recipient, _fData.amount);
@>      IFlashLoanRecipient(_fData.recipient).callback(_userData); <<@ BUG
    }
```

Now, during the execution flow, in `LeverageManager::_addLeveragePostCallback()` [function](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/lvf/LeverageManager.sol#L310C5-L344C6), when `_flashPaybackAmt` is calculated, flashloan fees is not added because `_d.fee` is `0` at this point.

`uint256 _flashPaybackAmt = _d.amount + _d.fee;`

As a result, in Balancer protocol, in `FlashLoans::flashLoan()` [function](https://github.com/balancer/balancer-v2-monorepo/blob/36d282374b457dddea828be7884ee0d185db06ba/pkg/vault/contracts/FlashLoans.sol#L38), the transaction will revert because of [this](https://github.com/balancer/balancer-v2-monorepo/blob/36d282374b457dddea828be7884ee0d185db06ba/pkg/vault/contracts/FlashLoans.sol#L79) line.

` _require(receivedFeeAmount >= feeAmounts[i], Errors.INSUFFICIENT_FLASH_LOAN_FEE_AMOUNT);`

### Internal Pre-conditions

1. For the borrow token, flash source is set as `BalancerFlashSource`

### External Pre-conditions

1. Flashloan fee is set by the Balancer protocol

### Attack Path

1. User calls `LeverageManager::addLeverage()` function
2. Flashloan is taken from the Balancer Vault
3. Flashloan fee is set by the Balancer protocol
4. While calculating total amount to be paid back to Balancer (princpial + fee), fee is not accounted for properly
5. In Balancer protocol, the transaction reverts throwing `INSUFFICIENT_FLASH_LOAN_FEE_AMOUNT`
6. As a result, `LeverageManager::addLeverage()` function reverts

### Impact

The core functionality of adding leverage in the Peapods procotol is broken.

This issue is being submitted as `High` because the impact is high and there are not much constraints which makes the likelihood high too. 

Balancer Protocol's governance can set flashloan fee at any time, maybe tomorrow, who knows. [balancer docs](https://balancer.gitbook.io/balancer-v2/concepts/fees#flash-loan-fees)

> Flash Loan fees are a type of Protocol Fee on Balancer. The fees collected as interest on flash loans go to the DAO Treasury. At deployment, flash loan fees were set to zero, and as of this writing they have not been activated by governance.

### PoC

_No response_

### Mitigation

In BalancerFlashSource, `receiveFlashLoan()` function should be modified as below

```diff
    function receiveFlashLoan(IERC20[] memory, uint256[] memory, uint256[] memory _feeAmounts, bytes memory _userData)
        external
        override
        workflow(false)
    {
        require(_msgSender() == source, "CBV");
        FlashData memory _fData = abi.decode(_userData, (FlashData));
        _fData.fee = _feeAmounts[0];
        IERC20(_fData.token).safeTransfer(_fData.recipient, _fData.amount);
--      IFlashLoanRecipient(_fData.recipient).callback(_userData);
++      IFlashLoanRecipient(_fData.recipient).callback(_fData);
    }
```

just like already done in `UniswapV3FlashSource::uniswapV3FlashCallback()` function

```solidity
    function uniswapV3FlashCallback(uint256 _fee0, uint256 _fee1, bytes calldata _data)
        external
        override
        workflow(false)
    {
        require(_msgSender() == source, "CBV");
        FlashData memory _fData = abi.decode(_data, (FlashData));
        _fData.fee = _fData.token == IUniswapV3Pool(source).token0() ? _fee0 : _fee1;
        IERC20(_fData.token).safeTransfer(_fData.recipient, _fData.amount);
@>      IFlashLoanRecipient(_fData.recipient).callback(abi.encode(_fData));
    }
```