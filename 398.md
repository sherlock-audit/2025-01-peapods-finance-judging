Boxy Charcoal Perch

High

# Decrease in vaults asset/share ratio will lead to incorrect Asset Metadata calculations in `LendingAssetVault`

### Summary

A derease in a vaults asset/share ratio will lead to incorrect asset tracking and loss for `LendingAssetVault` lenders


### Root Cause

In `LendingAssetVault::_updateAssetMetadataFromVault`

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/LendingAssetVault.sol#L287-L305
```solidity
    function _updateAssetMetadataFromVault(address _vault) internal {
        uint256 _prevVaultCbr = _vaultWhitelistCbr[_vault];
        _vaultWhitelistCbr[_vault] = IERC4626(_vault).convertToAssets(PRECISION);
        if (_prevVaultCbr == 0) {
            return;
        }
        uint256 _vaultAssetRatioChange = _prevVaultCbr > _vaultWhitelistCbr[_vault]
            ? ((PRECISION * _prevVaultCbr) / _vaultWhitelistCbr[_vault]) - PRECISION
            : ((PRECISION * _vaultWhitelistCbr[_vault]) / _prevVaultCbr) - PRECISION;


        uint256 _currentAssetsUtilized = vaultUtilization[_vault];
        uint256 _changeUtilizedState = (_currentAssetsUtilized * _vaultAssetRatioChange) / PRECISION;
        vaultUtilization[_vault] = _prevVaultCbr > _vaultWhitelistCbr[_vault]
            ? _currentAssetsUtilized < _changeUtilizedState ? 0 : _currentAssetsUtilized - _changeUtilizedState
            : _currentAssetsUtilized + _changeUtilizedState;
        _totalAssetsUtilized = _totalAssetsUtilized - _currentAssetsUtilized + vaultUtilization[_vault];
        _totalAssets = _totalAssets - _currentAssetsUtilized + vaultUtilization[_vault];
        emit UpdateAssetMetadataFromVault(_vault, _totalAssets, _totalAssetsUtilized);
    }
```

The function calculates the change in asset price in a whitelisted vault and increases or decreases the it's internal accounting of total assets accordingly.
For clarification, to better understand how it works let's consider 2 cases 
1. where the asset/share ratio increases  
2. where the asset/share ratio decreases.

1. On increase

- previous asset/share ratio of the vault(`_prevVaultCbr`) = 1
- current asset/share ratio of the vault(`_vaultWhiteListCbr`) = 2
- `vaultUtilization` = 100 assets
- `_totalAssetsUtilized` = 200 assets
- `_totalAssets` = 300 assets
- `_vaultAssetRatioChange` = (`_vaultWhiteListCbr` / `_prevVaultCbr`) - 1 = 2/1 - 1 = 1 (100% increase in asset share ratio)
- `_changeUtilizedState` = `_vaultAssetRatioChange` _ `_vaultUtilization` = 1 _ 100 = 100
- new `_vaultUtilization` = `_vaultUtilization` + `_changeUtilizedState` = 100 + 100 = 200
- new `_totalAssetsUtilized` = `_totalAssetsUtilized` + `_changeUtilizedState` = 200 + 100 = 300
- new `_totalAssets` = `_totalAssets` + `_changeUtilizedState` = 300 + 100 = 400

2. On decrease

- previous asset/share ratio of the vault(`_prevVaultCbr`) = 2
- current asset/share ratio of the vault(`_vaultWhiteListCbr`) = 1
- `vaultUtilization` = 100 assets
- `_totalAssetsUtilized` = 200 assets
- `_totalAssets` = 300 assets
- `_vaultAssetRatioChange` = (`_prevVaultCbr`/ `_vaultWhiteListCbr`) - 1 = 2/1 - 1 = 1 (calculates as 100% decrease instead of 50%)
- `_changeUtilizedState` = `_vaultAssetRatioChange` _ `_vaultUtilization` = 1 _ 100 = 100
- new `_vaultUtilization` = `_vaultUtilization` + `_changeUtilizedState` = 100 - 100 = 0
- new `_totalAssetsUtilized` = `_totalAssetsUtilized` + `_changeUtilizedState` = 200 - 100 = 100
- new `_totalAssets` = `_totalAssets` + `_changeUtilizedState` = 300 - 100 = 200

on **Increase** vault asset/share ratio goes from 1 to 2 thats a 100% increase(2x) and the vault utilization doubles as well, however, on **decrease** the vault asset/share ratio goes from 2 to 1 that's a **50% decrease** the vault utilization should be halved instead its 0!

This shows that whenever there's a decrease in asset/share ratio of a whitelisted vault the asset tracking data will be updated incorrectly leading to a loss of funds and a loss for lenders of the LendingAssetVault.


### Internal Pre-conditions

A decrease in the asset/share ratio of a whitelisted vault, one way this can occur is as a result of liquidating bad debt since whitelisted vaults are fraxLend lending vaults


### External Pre-conditions

NONE

### Attack Path

NONE

### Impact

High - loss of funds and loss for lenders


### PoC

_No response_

### Mitigation

For decrease in asset/share ratio (when `_vaultWhiteListCbr` < `_prevVaultCbr`) `_vaultAssetRatioChange` should be calculated as : 1 - (`_vaultWhiteListCbr` / `_prevVaultCbr`)

```diff
    function _updateAssetMetadataFromVault(address _vault) internal {
        uint256 _prevVaultCbr = _vaultWhitelistCbr[_vault];
        _vaultWhitelistCbr[_vault] = IERC4626(_vault).convertToAssets(PRECISION);//1 * totalAssets/totalShares
        if (_prevVaultCbr == 0) {
            return;
        }
        uint256 _vaultAssetRatioChange = _prevVaultCbr > _vaultWhitelistCbr[_vault]
-           ? ((PRECISION * _prevVaultCbr) / _vaultWhitelistCbr[_vault]) - PRECISION
+           ? PRECISION - ((PRECISION * _vaultWhitelistCbr[_vault]) / _prevVaultCbr)
            : ((PRECISION * _vaultWhitelistCbr[_vault]) / _prevVaultCbr) - PRECISION;

        uint256 _currentAssetsUtilized = vaultUtilization[_vault];//total assets in vault say 1000
        uint256 _changeUtilizedState = (_currentAssetsUtilized * _vaultAssetRatioChange) / PRECISION;//1000 * 25% = 250
        vaultUtilization[_vault] = _prevVaultCbr > _vaultWhitelistCbr[_vault]
            ? _currentAssetsUtilized < _changeUtilizedState ? 0 : _currentAssetsUtilized - _changeUtilizedState
            : _currentAssetsUtilized + _changeUtilizedState;//1000 + 250 = 1250
        _totalAssetsUtilized = _totalAssetsUtilized - _currentAssetsUtilized + vaultUtilization[_vault];
        _totalAssets = _totalAssets - _currentAssetsUtilized + vaultUtilization[_vault];
        emit UpdateAssetMetadataFromVault(_vault, _totalAssets, _totalAssetsUtilized);
    }
```