Keen Jetblack Deer

Medium

# Oracle sequencer check is incomplete.


### Summary

Oracle sequencer check is incomplete.

### Root Cause

`ChainlinkSinglePriceOracle` has implemented a sequencer check. However, this is only used in `ChainlinkSinglePriceOracle`, and not in `UniswapV3SinglePriceOracle` and `DIAOracleV2SinglePriceOracle`.

When `getPriceUSD18()` is called for both `UniswapV3SinglePriceOracle` and `DIAOracleV2SinglePriceOracle`, if `_clBaseConversionPoolPriceFeed` is called, it will do a chainlink oracle lookup by the `_getChainlinkPriceFeedPrice18()` function to update the returned price. This is not guarded by the sequencer check.

If the sequencer goes down, oracle data will not be updated, and could become stale. Attackers could take advantage of the stale price and carry out attacks, such as borrowing more against their collateral's value.

Also, for Arbitrum sequencer, it should check for `startedAt=0` case, as written in chainlink docs: https://docs.chain.link/data-feeds/l2-sequencer-feeds. It may happen that `answer=0` but sequencer is not up because `startedAt=0` for Arbitrum sequencer.

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/oracle/ChainlinkSinglePriceOracle.sol#L84

```solidity
    function _getChainlinkPriceFeedPrice18(address _priceFeed)
        internal
        view
        returns (uint256 _price18, uint256 _updatedAt, bool _isBadAnswer)
    {
        uint8 _decimals = AggregatorV2V3Interface(_priceFeed).decimals();
        (, int256 _price,, uint256 _lastUpdated,) = AggregatorV2V3Interface(_priceFeed).latestRoundData();
        _isBadAnswer = _price <= 0 || !_isValidAnswer(_priceFeed, _price);
        _price18 = (uint256(_price) * 10 ** 18) / 10 ** _decimals;
        _updatedAt = _lastUpdated;
    }

   function _sequencerCheck() internal view {
        if (address(_sequencerUptimeFeed) == address(0)) {
            return;
        }

        (
            ,
            /*uint80 roundID*/
            int256 answer,
            uint256 _startedAt, /*uint256 updatedAt*/ /*uint80 answeredInRound*/
            ,
        ) = _sequencerUptimeFeed.latestRoundData();

        // Answer == 0: Sequencer is up
        // Answer == 1: Sequencer is down
        bool _isSequencerUp = answer == 0;
        if (!_isSequencerUp) {
            revert SequencerDown();
        }

        // Make sure the grace period has passed after the
        // sequencer is back up.
        uint256 _timeSinceUp = block.timestamp - _startedAt;
        if (_timeSinceUp <= SEQ_GRACE_PERIOD) {
            revert GracePeriodNotOver();
        }
    }
```

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/oracle/UniswapV3SinglePriceOracle.sol#L26

```solidity
    function getPriceUSD18(
        address _clBaseConversionPoolPriceFeed,
        address _quoteToken,
        address _quoteV3Pool,
        uint256 _twapInterval
    ) external view virtual override returns (bool _isBadData, uint256 _price18) {
        ...
        if (_clBaseConversionPoolPriceFeed != address(0)) {
@>          (_basePrice18, _updatedAt, _isBadData) = _getChainlinkPriceFeedPrice18(_clBaseConversionPoolPriceFeed);
        }
        ...
    }
```

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/oracle/DIAOracleV2SinglePriceOracle.sol#L31

```solidity
    function getPriceUSD18(
        address _clBaseConversionPoolPriceFeed,
        address _quoteToken,
        address _quoteDIAOracle,
        uint256
    ) external view virtual override returns (bool _isBadData, uint256 _price18) {
        ...
        if (_clBaseConversionPoolPriceFeed != address(0)) {
@>          (_basePrice18, _updatedAt, _isBadDataBase) = _getChainlinkPriceFeedPrice18(_clBaseConversionPoolPriceFeed);
            ...
        }
    }
```

### Internal pre-conditions

N/A

### External pre-conditions

N/A

### Attack Path

N/A

### Impact

Oracle data may be incorrect, and attackers could take advantage of the stale price and carry out attacks, such as borrowing more against their collateral's value.

### PoC

N/A

### Mitigation

1. Move the `_sequencerCheck()` function in `_getChainlinkPriceFeedPrice18()`
2. Check startedAt=0 to handle Arbitrum chain.