Boxy Charcoal Perch

Medium

# Incorrect slippage calculations in `Zapper::_swapV3Single` under certain conditions

### Summary

`Zapper::_swapV3Single` applies slippage to user provided minimum amount as well ,leading to a bigger slippage margin than specified by the user which can result in a loss of funds.


### Root Cause

In `Zapper::_swapV3Single`
when the user does not provide a slippage amount(`_amountOutMin == 0`) the function calculates the expected amountOut using the pool price and calculates slippage amount using the confogured pool slippage % or the default slippage % if not configured.

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/Zapper.sol#L154-L180
```solidity
    function _swapV3Single(address _in, uint24 _fee, address _out, uint256 _amountIn, uint256 _amountOutMin)
        internal
        returns (uint256)
    {
        address _v3Pool;
        try DEX_ADAPTER.getV3Pool(_in, _out, uint24(10000)) returns (address __v3Pool) {
            _v3Pool = __v3Pool;
        } catch {
            _v3Pool = DEX_ADAPTER.getV3Pool(_in, _out, int24(200));
        }
        if (_amountOutMin == 0) { //<@
            address _token0 = _in < _out ? _in : _out;
            uint256 _poolPriceX96 =
                V3_TWAP_UTILS.priceX96FromSqrtPriceX96(V3_TWAP_UTILS.sqrtPriceX96FromPoolAndInterval(_v3Pool));
            _amountOutMin = _in == _token0
                ? (_poolPriceX96 * _amountIn) / FixedPoint96.Q96
                : (_amountIn * FixedPoint96.Q96) / _poolPriceX96;
        }


        uint256 _outBefore = IERC20(_out).balanceOf(address(this));
        uint256 _finalSlip = _slippage[_v3Pool] > 0 ? _slippage[_v3Pool] : _defaultSlippage;
        IERC20(_in).safeIncreaseAllowance(address(DEX_ADAPTER), _amountIn);
        DEX_ADAPTER.swapV3Single(
            _in, _out, _fee, _amountIn, (_amountOutMin * (1000 - _finalSlip)) / 1000, address(this) // <@
        );
        return IERC20(_out).balanceOf(address(this)) - _outBefore;
    }
```

The issue is that when the user provides a slippage amount(`_amountOutMin > 0`) the function still applies the configured pool slippage % or the default to the user provided slippage amount, leading to a bigger slippage margin than specified by the user. The call path to `_swapV3Single` is as follows: `IndexUtils::addLpAndStake` -> `Zapper::_zap` (when providedPairedToken != pairedToken of the pod) -> `Zapper::_swapV3Single`.

As a result the swap amount out can be less than the `_amountOutMin` provided by caller and also increases the users exposure to arbitrage sandwich attacks.


### Internal Pre-conditions

user calls `IndexUtils::addLpAndStake` with `_amountPairedLpTokenMin` > 0


### External Pre-conditions

NONE

### Attack Path

NONE

### Impact

Medium - possible loss of funds for the user


### PoC

_No response_

### Mitigation

```diff
    function _swapV3Single(address _in, uint24 _fee, address _out, uint256 _amountIn, uint256 _amountOutMin)
        internal
        returns (uint256)
    {
        address _v3Pool;
        try DEX_ADAPTER.getV3Pool(_in, _out, uint24(10000)) returns (address __v3Pool) {
            _v3Pool = __v3Pool;
        } catch {
            _v3Pool = DEX_ADAPTER.getV3Pool(_in, _out, int24(200));
        }
        if (_amountOutMin == 0) {
            address _token0 = _in < _out ? _in : _out;
            uint256 _poolPriceX96 =
                V3_TWAP_UTILS.priceX96FromSqrtPriceX96(V3_TWAP_UTILS.sqrtPriceX96FromPoolAndInterval(_v3Pool));
            _amountOutMin = _in == _token0
                ? (_poolPriceX96 * _amountIn) / FixedPoint96.Q96
                : (_amountIn * FixedPoint96.Q96) / _poolPriceX96;
+       uint256 _finalSlip = _slippage[_v3Pool] > 0 ? _slippage[_v3Pool] : _defaultSlippage;
+       _amountOutMin = (_amountOutMin * (1000 - _finalSlip)) / 1000;
        }

        uint256 _outBefore = IERC20(_out).balanceOf(address(this));
-       uint256 _finalSlip = _slippage[_v3Pool] > 0 ? _slippage[_v3Pool] : _defaultSlippage;
        IERC20(_in).safeIncreaseAllowance(address(DEX_ADAPTER), _amountIn);
        DEX_ADAPTER.swapV3Single(
-           _in, _out, _fee, _amountIn, (_amountOutMin * (1000 - _finalSlip)) / 1000, address(this)
+           _in, _out, _fee, _amountIn, _amountOutMin, address(this)//_amountOutMin is either user provided or function calculated âœ…
        );
        return IERC20(_out).balanceOf(address(this)) - _outBefore;
    }
```