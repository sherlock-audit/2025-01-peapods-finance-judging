Alert Lime Panda

Medium

# `LeverageManager` will cause DOS by failing to repay Balancer Flash Loan fee

### Summary

The `receiveFlashLoan()` function in the `BalancerFlashSource` contract passes `_userData` directly to the `callback()` function of `_fData.recipient`. However, `_fData.recipient` is expected to repay the flash loan amount along with the fee, and it requires the correct encoded data to compute the repayment amount. The missing `fee` value in `_userData` may lead to an unexpected underpayment and reverting the flashloan due to not paying the required fee.

### Root Cause

The `receiveFlashLoan()` function processes the flash loan repayment but does not properly encode `_fData` before passing it to the recipient([LeverageManager](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/lvf/LeverageManager.sol#L209)):

```solidity
    function receiveFlashLoan(IERC20[] memory, uint256[] memory, uint256[] memory _feeAmounts, bytes memory _userData)
        external
        override
        workflow(false)
    {
        require(_msgSender() == source, "CBV");
        FlashData memory _fData = abi.decode(_userData, (FlashData));
@>      _fData.fee = _feeAmounts[0];
        IERC20(_fData.token).safeTransfer(_fData.recipient, _fData.amount);
@>      IFlashLoanRecipient(_fData.recipient).callback(_userData); // @audit: Incorrect: should be abi.encode(_fData)
    }
```
https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/flash/BalancerFlashSource.sol#L54C9-L56C67

- `_fData.recipient` receives `_userData`, which does not contain the updated `fee` value.
- Since `_fData.recipient` is responsible for [repaying the loan and fee](https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/lvf/LeverageManager.sol#L218C38-L218C62), it may not account for the correct repayment amount, leading to a possible shortfall.

```solidity
    function _addLeveragePostCallback(bytes memory _data) internal returns (uint256 _ptknRefundAmt) {
@>      IFlashLoanSource.FlashData memory _d = abi.decode(_data, (IFlashLoanSource.FlashData));
        (LeverageFlashProps memory _props,) = abi.decode(_d.data, (LeverageFlashProps, bytes));
.
.
.
@>      uint256 _flashPaybackAmt = _d.amount + _d.fee;
        uint256 _borrowAmt = _overrideBorrowAmt > _flashPaybackAmt ? _overrideBorrowAmt : _flashPaybackAmt;

        address _aspTkn = _getAspTkn(_props.positionId);
        IERC20(_aspTkn).safeTransfer(positionProps[_props.positionId].custodian, _aspTknCollateralBal);
        LeveragePositionCustodian(positionProps[_props.positionId].custodian).borrowAsset(
            positionProps[_props.positionId].lendingPair, _borrowAmt, _aspTknCollateralBal, address(this)
        );

        // pay back flash loan and send remaining to borrower
@>      IERC20(_d.token).safeTransfer(IFlashLoanSource(_getFlashSource(_props.positionId)).source(), _flashPaybackAmt);
```
https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/lvf/LeverageManager.sol#L310C3-L338C120

### Internal Pre-conditions

1. When the `LeverageManager` contract uses the `BalancerFlashSource` contract as the flash loan source

### External Pre-conditions

None

### Attack Path

If the `LeverageManager` contract selects the `BalancerFlashSource` contract as the flash loan source when adding leverage, it sets the flash loan `fee` to `0` due to the mistake. This leads to a revert in the [Balancer flash loan function](https://github.com/balancer/balancer-v2-monorepo/blob/36d282374b457dddea828be7884ee0d185db06ba/pkg/vault/contracts/FlashLoans.sol#L78C13-L79C101) during the repayment check, as the required amount is not fully repaid.

### Impact

If `_fData.recipient` does not receive the correct fee information, it may fail to return the full required amount, causing:
- Flash Loan repayment failure and DoS of the add leverage functionality in the `LeverageManager` contract.

### PoC

_No response_

### Mitigation

Update the `receiveFlashLoan()` function to encode `_fData` properly before calling the `callback()` function. This ensures that the recipient contract receives the correct data, including the fee amount, and can correctly compute the repayment obligation.

```diff
    function receiveFlashLoan(IERC20[] memory, uint256[] memory, uint256[] memory _feeAmounts, bytes memory _userData)
        external
        override
        workflow(false)
    {
        require(_msgSender() == source, "CBV");
        FlashData memory _fData = abi.decode(_userData, (FlashData));
        _fData.fee = _feeAmounts[0];
        IERC20(_fData.token).safeTransfer(_fData.recipient, _fData.amount);
-       IFlashLoanRecipient(_fData.recipient).callback(_userData);
+       IFlashLoanRecipient(_fData.recipient).callback(abi.encode(_fData));
    }
```