Boxy Charcoal Perch

High

# Hardcoded V3 pool fee tier in `Zapper::_swapV3Single` can be exploited or lead to DOS under certain conditions

### Summary

`Zapper::_swapV3Single` uses hardcoded fee value to get pool , however when `_amountOutMin` is not provided the pool is used to get the token prices and calculate slippage amount, if the pool does not exist under this hardcoded fee tier then the transaction fails as price cannot retrieved


### Root Cause

In `Zapper::_swapV3Single`

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/Zapper.sol#L154-L180
```solidity
    function _swapV3Single(address _in, uint24 _fee, address _out, uint256 _amountIn, uint256 _amountOutMin)
        internal
        returns (uint256)
    {
        address _v3Pool;
        try DEX_ADAPTER.getV3Pool(_in, _out, uint24(10000)) returns (address __v3Pool) {  //<@
            _v3Pool = __v3Pool;
        } catch {
            _v3Pool = DEX_ADAPTER.getV3Pool(_in, _out, int24(200));
        }
        if (_amountOutMin == 0) {
            address _token0 = _in < _out ? _in : _out;
            uint256 _poolPriceX96 =
                V3_TWAP_UTILS.priceX96FromSqrtPriceX96(V3_TWAP_UTILS.sqrtPriceX96FromPoolAndInterval(_v3Pool));
            _amountOutMin = _in == _token0
                ? (_poolPriceX96 * _amountIn) / FixedPoint96.Q96
                : (_amountIn * FixedPoint96.Q96) / _poolPriceX96;
        }


        uint256 _outBefore = IERC20(_out).balanceOf(address(this));
        uint256 _finalSlip = _slippage[_v3Pool] > 0 ? _slippage[_v3Pool] : _defaultSlippage;
        IERC20(_in).safeIncreaseAllowance(address(DEX_ADAPTER), _amountIn);
        DEX_ADAPTER.swapV3Single(
            _in, _out, _fee, _amountIn, (_amountOutMin * (1000 - _finalSlip)) / 1000, address(this)
        );
        return IERC20(_out).balanceOf(address(this)) - _outBefore;
    }
```

we see that `DEX_ADAPTER.getV3Pool` is called with hardcoded fee of 10000, this function returns the calculated pool address given the token addresses and fee tier and does not check if the pool exists or not. It's important to not that the try-catch is for the different types of Dex adapters and not for retrying if pool does not exist.

When no slippage amount is provided (`_amountOutMin == 0`) , we see that the function attempts to retrieve the token price from the pool to calculate the slippage amount. Since the pool doesnot exist in this fee tier the call reverts causing the transaction to fail.

At worst case a malicious observer can front-run the transaction to deploy the pool at the 10000 fee tier and initialize the pool price to any value (probably 0 or close) to make the slippage calculated amount as low as possible, and sandwich it to cause maximum slippage on the actual pool since the swap is still done on the actual pool with the correct fee tier as seen here
https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/Zapper.sol#L176-L178


### Internal Pre-conditions

User calls `IndexUtils::addLpAndStake` with `_amountPairedLpTokenMin` = 0 when `providedPairedToken != pairedToken` of the pod indicating that a **zap** is required


### External Pre-conditions

NONE

### Attack Path

The call path to `_swapV3Single` is: `IndexUtils::addLpAndStake` -> `Zapper::_zap` (when providedPairedToken != pairedToken of the pod) -> `Zapper::_swapV3Single`.
When `_amountPairedLpTokenMin == 0` indicating that the user did not provide a min slippage.

1. The transaction fails if the pool does not exist for the 10000 fee tier as price cannot be retrieved from the pool

2. A malicious user front-runs to deploy the pool at the 10000 fee tier and initialize the pool price to any value (probably 0 or close) to make the slippage calculated amount as low as possible, whlist still sandwiching the transaction to cause maximum slippage when the swap is done on the actual pool. Leading to a loss of funds


### Impact

High - DoS or loss of funds for the user


### PoC

_No response_

### Mitigation

```diff
    function _swapV3Single(address _in, uint24 _fee, address _out, uint256 _amountIn, uint256 _amountOutMin)
        internal
        returns (uint256)
    {
        address _v3Pool;
-       try DEX_ADAPTER.getV3Pool(_in, _out, uint24(10000)) returns (address __v3Pool) {
+       try DEX_ADAPTER.getV3Pool(_in, _out, _fee) returns (address __v3Pool) {
            _v3Pool = __v3Pool;
        } catch {
            _v3Pool = DEX_ADAPTER.getV3Pool(_in, _out, int24(200));//Info: This is for Aerodrome dex Adapter and tickSpacing is ignored and defaults to 200 in `swapV3Single` anyways
        }
        if (_amountOutMin == 0) {
            address _token0 = _in < _out ? _in : _out;
            uint256 _poolPriceX96 =
                V3_TWAP_UTILS.priceX96FromSqrtPriceX96(V3_TWAP_UTILS.sqrtPriceX96FromPoolAndInterval(_v3Pool));
            _amountOutMin = _in == _token0
                ? (_poolPriceX96 * _amountIn) / FixedPoint96.Q96
                : (_amountIn * FixedPoint96.Q96) / _poolPriceX96;
        }

        uint256 _outBefore = IERC20(_out).balanceOf(address(this));
        uint256 _finalSlip = _slippage[_v3Pool] > 0 ? _slippage[_v3Pool] : _defaultSlippage;
        IERC20(_in).safeIncreaseAllowance(address(DEX_ADAPTER), _amountIn);
        DEX_ADAPTER.swapV3Single(
            _in, _out, _fee, _amountIn, (_amountOutMin * (1000 - _finalSlip)) / 1000, address(this)
        );
        return IERC20(_out).balanceOf(address(this)) - _outBefore;
    }
```