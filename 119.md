Fast Khaki Raccoon

Medium

# `_calculateBasePerPTkn` includes debond fee twice, lowering the price too much

### Summary

`_calculateBasePerPTkn` includes the debonding fee twice, once in `_accountForCBRInPrice` and another time in `_accountForUnwrapFeeInPrice`, which would lower the price even further, allowing liquidators to liquidate users that should be above water.

```solidity
  function _calculateBasePerPTkn(uint256 _price18) internal view returns (uint256 _basePerPTkn18) {
        // ...

        _basePerPTkn18 = _accountForCBRInPrice(pod, underlyingTkn, _price18);   // DEBOND_FEE included here
        _basePerPTkn18 = _accountForUnwrapFeeInPrice(pod, _basePerPTkn18);      // and also here
    }
```

### Root Cause

1. Inside `_calculateBasePerPTkn` we first `_accountForCBRInPrice`, then include de-bond fee using `_accountForUnwrapFeeInPrice` as liquidators will still need to debond, which would pact their margin and thus the price at which liquidations are profitable

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/oracle/spTKNMinimalOracle.sol#L170-L186
```solidity
        _basePerPTkn18 = _accountForCBRInPrice(pod, underlyingTkn, _price18);
        _basePerPTkn18 = _accountForUnwrapFeeInPrice(pod, _basePerPTkn18);
```

2. When we look into `_accountForUnwrapFeeInPrice` we can see that it simply removes the debond fee from the price:

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/oracle/spTKNMinimalOracle.sol#L246-L253
```solidity
    function _accountForUnwrapFeeInPrice(address _pod, uint256 _currentPrice)
        internal
        view
        returns (uint256 _newPrice)
    {
        uint16 _unwrapFee = IDecentralizedIndex(_pod).DEBOND_FEE();
        _newPrice = _currentPrice - (_currentPrice * _unwrapFee) / 10000;
    }
```

3. Going back to`_calculateBasePerPTkn` we further look into how `_accountForCBRInPrice` would get the pod and use `convertToAssets` to convert a standard unit of shares to an asset

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/oracle/spTKNMinimalOracle.sol#L231-L244
```solidity
    function _accountForCBRInPrice(address _pod, address _underlying, uint256 _amtUnderlying)
        internal
        view
        returns (uint256)
    {
        require(IDecentralizedIndex(_pod).unlocked() == 1, "OU");

        if (_underlying == address(0)) {
            IDecentralizedIndex.IndexAssetInfo[] memory _assets = IDecentralizedIndex(_pod).getAllAssets();
            _underlying = _assets[0].token;
        }

        uint256 _pTknAmt =
            (_amtUnderlying * 10 ** IERC20Metadata(_pod).decimals()) / 10 ** IERC20Metadata(_underlying).decimals();

        return IDecentralizedIndex(_pod).convertToAssets(_pTknAmt);
    }
```

4. But in`convertToAssets` we can see that at it's end after doing all of the conversions it also losers the asset amount by `_fees.debond`, meaning it actually accounts for it

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/WeightedIndex.sol#L120-L129
```solidity
    function convertToAssets(uint256 _shares) external view override returns (uint256 _assets) {
        // _totalSupply == 0
        bool _firstIn = _isFirstIn();

        uint256 _percSharesX96_2 = _firstIn 
            ? 2 ** (96 / 2) 
            : (_shares * 2 ** (96 / 2)) / _totalSupply;

        if (_firstIn) {
            _assets = (indexTokens[0].q1 * _percSharesX96_2) / FixedPoint96.Q96 / 2 ** (96 / 2);
        } else {
            _assets = (_totalAssets[indexTokens[0].token] * _percSharesX96_2) / 2 ** (96 / 2);
        }

        _assets -= ((_assets * _fees.debond) / DEN);
    }
```


**This means that the inside `_calculateBasePerPTkn` we include the debonding fee twice, once in `_accountForCBRInPrice` and another time in `_accountForUnwrapFeeInPrice`**


Note that the same is also done inside `_checkAndHandleBaseTokenPodConfig`:

```solidity
    function _checkAndHandleBaseTokenPodConfig(uint256 _currentPrice18) internal view returns (uint256 _finalPrice18) {
        _finalPrice18 = _accountForCBRInPrice(BASE_TOKEN, address(0), _currentPrice18);
        _finalPrice18 = _accountForUnwrapFeeInPrice(BASE_TOKEN, _finalPrice18);
    }
```

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

The price mechanism will not work correctly allowing liquidators to liquidate users earlier, even when they are above the collateralization threshold.

### Impact

Debonding fee would be included twice inside the price calculation, meaning that a lower price is given over all.

### PoC

_No response_

### Mitigation

There is no need for the second function as `_accountForCBRInPrice` already calculates the debonding fee when calling `convertToAssets`.