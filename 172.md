Fast Khaki Raccoon

Medium

# Having a bad frax vault will cause insolvency inside `LendingAssetVault`

### Summary

Frax is made to cover bad debt, by socializing it, the same is not true for `LendingAssetVault`.

### Root Cause

`redeemFromVault` is used by an admin if the system decides to be no longer part of a specific frax vault. Where we can redeem 100% of our shares and leave the vault. After `redeem` follows the reduction in the `vaultUtilization[_vault]` and `_totalAssetsUtilized`.

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/LendingAssetVault.sol#L325-L334
```solidity
    function redeemFromVault(address _vault, uint256 _amountShares) external onlyOwner {
        _updateAssetMetadataFromVault(_vault);

        _amountShares = _amountShares == 0 
            ? IERC20(_vault).balanceOf(address(this)) 
            : _amountShares;

        uint256 _amountAssets = IERC4626(_vault).redeem(_amountShares, address(this), address(this));

        uint256 _redeemAmt = vaultUtilization[_vault] < _amountAssets 
            ? vaultUtilization[_vault] 
            : _amountAssets;

        vaultDeposits[_vault] -= _redeemAmt > vaultDeposits[_vault] 
            ? vaultDeposits[_vault] 
            : _redeemAmt;

        vaultUtilization[_vault] -= _redeemAmt;
        _totalAssetsUtilized -= _redeemAmt;

        emit RedeemFromVault(_vault, _amountShares, _redeemAmt);
    }
```

The issue that we currently have is that if the vault was in bad debt and we finish with negative PnL our `vaultUtilization[_vault]` and ` vaultDeposits[_vault]` would be positive, thus our `_totalAssetsUtilized` for this vault is gonna be positive too.

`_totalAssetsUtilized` is the total between all vaults. Imagine that all other vaults withdraw after we close this one on a loss, in this case `_totalAssetsUtilized` will be positive even though we are not taking part in any vault.


If it happens once `LendingAssetVault` would be insolvent no matter how much profits the rest of the vaults generate. That is because we use `totalAvailableAssets` to calculate the max amount we can withdraw:

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/LendingAssetVault.sol#L176-L187
```solidity
    function _withdraw(uint256 _shares, uint256 _assets, address _owner, address _caller, address _receiver) internal {
        if (_caller != _owner) {
            _spendAllowance(_owner, _caller, _shares);
        }

        // _totalAssets - _totalAssetsUtilized
        uint256 _totalAvailable = totalAvailableAssets();
        _totalAssets -= _assets;

        require(_totalAvailable >= _assets, "AV");
        _burn(_owner, _shares);
        IERC20(_asset).safeTransfer(_receiver, _assets);
        emit Withdraw(_owner, _receiver, _receiver, _assets, _shares);
    }

    function totalAvailableAssets() public view override returns (uint256) {
        return _totalAssets - _totalAssetsUtilized;
    }
```


But convert our shares using `_totalAssets`.

https://github.com/sherlock-audit/2025-01-peapods-finance/blob/main/contracts/contracts/LendingAssetVault.sol#L82-L84

```solidity
    function convertToShares(uint256 _assets) public view override returns (uint256 _shares) {
        // assets * 1e27 / cbr
        _shares = (_assets * PRECISION) / _cbr();
    }
    function _cbr() internal view returns (uint256) {
        uint256 _supply = totalSupply();
        return _supply == 0 
            ? PRECISION
            // 1e27 * _totalAssets / _supply
            : (PRECISION * _totalAssets) / _supply;
    }
```

### Internal Pre-conditions

_No response_

### External Pre-conditions

_No response_

### Attack Path

1. We invest in a frax vault
2. Due to unforeseen circumstances the vault generates some bad debt
3. People leave the vault as it turned into negative, or at least low PnL
4. `redeemFromVault` is called by admins to leave the vault and redeem all shares `LendingAssetVault` has
5. Since there was a slight dip we are unable to close 100% of the utilization and `_totalAssetsUtilized` for this vault

Now `LendingAssetVault` is insolvent. No amount of profit from other vaults would fix this.

### Impact

Insolvency. Not treatable. If it happens once `LendingAssetVault` would be insolvent no matter how much profits the rest of the vaults generate. The last few users of `LendingAssetVault` won't be able to withdraw their assets.

### PoC

_No response_

### Mitigation

Consider having for when we withdraw 100% of the shares, but are not able to cover 100% of the utilization. In such cases, try to socialize the debt.